OBJECT Table 60 Document Sending Profile
{
  OBJECT-PROPERTIES
  {
    Date=23-10-15;
    Time=12:00:00;
    Version List=NAVW19.00.00.43402;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DocumentSendingProfile@1000 : Record 60;
             BEGIN
               DocumentSendingProfile.SETRANGE(Default,TRUE);
               IF NOT DocumentSendingProfile.FINDFIRST THEN
                 Default := TRUE;
             END;

    OnDelete=VAR
               Customer@1000 : Record 18;
             BEGIN
               IF Default THEN
                 ERROR(CannotDeleteDefaultRuleErr);

               Customer.SETRANGE("Document Sending Profile",Code);
               IF Customer.FINDFIRST THEN BEGIN
                 IF CONFIRM(UpdateAssCustomerQst,FALSE,Code) THEN
                   Customer.MODIFYALL("Document Sending Profile",'')
                 ELSE
                   ERROR(CannotDeleteErr);
               END;
             END;

    CaptionML=[DAN=Dokumentafsendelsesprofil;
               ENU=Document Sending Profile];
    LookupPageID=Page359;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[DAN=Kode;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 10  ;   ;Printer             ;Option        ;CaptionML=[DAN=Printer;
                                                              ENU=Printer];
                                                   OptionCaptionML=[DAN=Nej,Ja (Bed om indstillinger),Ja (Brug standardindstillinger);
                                                                    ENU=No,Yes (Prompt for Settings),Yes (Use Default Settings)];
                                                   OptionString=No,Yes (Prompt for Settings),Yes (Use Default Settings) }
    { 11  ;   ;E-Mail              ;Option        ;CaptionML=[DAN=Mail;
                                                              ENU=E-Mail];
                                                   OptionCaptionML=[DAN=Nej,Ja (Bed om indstillinger),Ja (Brug standardindstillinger);
                                                                    ENU=No,Yes (Prompt for Settings),Yes (Use Default Settings)];
                                                   OptionString=No,Yes (Prompt for Settings),Yes (Use Default Settings) }
    { 12  ;   ;E-Mail Attachment   ;Option        ;CaptionML=[DAN=Vedhæftet fil til mail;
                                                              ENU=E-Mail Attachment];
                                                   OptionCaptionML=[DAN=PDF,Elektronisk dokument,PDF og elektronisk dokument;
                                                                    ENU=PDF,Electronic Document,PDF & Electronic Document];
                                                   OptionString=PDF,Electronic Document,PDF & Electronic Document }
    { 13  ;   ;E-Mail Format       ;Code20        ;TableRelation="Electronic Document Format".Code;
                                                   CaptionML=[DAN=Mailformat;
                                                              ENU=E-Mail Format] }
    { 15  ;   ;Disk                ;Option        ;CaptionML=[DAN=Disk;
                                                              ENU=Disk];
                                                   OptionCaptionML=[DAN=Nej,PDF,Elektronisk dokument,PDF og elektronisk dokument;
                                                                    ENU=No,PDF,Electronic Document,PDF & Electronic Document];
                                                   OptionString=No,PDF,Electronic Document,PDF & Electronic Document }
    { 16  ;   ;Disk Format         ;Code20        ;TableRelation="Electronic Document Format".Code;
                                                   CaptionML=[DAN=Diskformat;
                                                              ENU=Disk Format] }
    { 20  ;   ;Electronic Document ;Option        ;CaptionML=[DAN=Elektronisk dokument;
                                                              ENU=Electronic Document];
                                                   OptionCaptionML=[DAN=Nej,Via dokumentudvekslingstjeneste;
                                                                    ENU=No,Through Document Exchange Service];
                                                   OptionString=No,Through Document Exchange Service }
    { 21  ;   ;Electronic Format   ;Code20        ;TableRelation="Electronic Document Format".Code;
                                                   CaptionML=[DAN=Elektronisk format;
                                                              ENU=Electronic Format] }
    { 30  ;   ;Default             ;Boolean       ;OnValidate=VAR
                                                                DocumentSendingProfile@1001 : Record 60;
                                                              BEGIN
                                                                IF (xRec.Default = TRUE) AND (Default = FALSE) THEN
                                                                  ERROR(CannotRemoveDefaultRuleErr);

                                                                DocumentSendingProfile.SETRANGE(Default,TRUE);
                                                                DocumentSendingProfile.MODIFYALL(Default,FALSE,FALSE);
                                                              END;

                                                   CaptionML=[DAN=Standard;
                                                              ENU=Default] }
    { 50  ;   ;Send To             ;Option        ;CaptionML=[DAN=Send til;
                                                              ENU=Send To];
                                                   OptionCaptionML=[DAN=Disk,Mail,Print,Elektroniske dokument;
                                                                    ENU=Disk,E-Mail,Print,Electronic Document];
                                                   OptionString=Disk,E-Mail,Print,Electronic Document }
    { 51  ;   ;Usage               ;Option        ;CaptionML=[DAN=Forbrug;
                                                              ENU=Usage];
                                                   OptionCaptionML=[DAN=Salgsfaktura,Salgskreditnota,Servicefaktura,Servicekreditnota;
                                                                    ENU=Sales Invoice,Sales Credit Memo,Service Invoice,Service Credit Memo];
                                                   OptionString=Sales Invoice,Sales Credit Memo,Service Invoice,Service Credit Memo }
    { 52  ;   ;One Related Party Selected;Boolean ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "One Related Party Selected" THEN BEGIN
                                                                  "Electronic Document" := "Electronic Document"::No;
                                                                  "Electronic Format" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=En relateret part er valgt;
                                                              ENU=One Related Party Selected] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DefaultCodeTxt@1000 : TextConst '@@@=Translate as we translate default term in local languages;DAN=STANDARD;ENU=DEFAULT';
      DefaultDescriptionTxt@1001 : TextConst 'DAN=Standardreglen bruges i mangel af anden regel;ENU=Default rule used if no other provided';
      RecordAsTextFormatterTxt@1002 : TextConst 'DAN="%1 ; %2";ENU="%1 ; %2"';
      FieldCaptionContentFormatterTxt@1007 : TextConst '@@@="%1=Field Caption (e.g. E-mail), %2=Field Content (e.g. PDF) so for example ''E-mail (PDF)''";DAN=%1 (%2);ENU=%1 (%2)';
      CannotDeleteDefaultRuleErr@1003 : TextConst 'DAN=Du kan ikke slette standardreglen. Tildel først en anden regel, der skal være standardreglen.;ENU=You cannot delete the default rule. Assign other rule to be default first.';
      CannotRemoveDefaultRuleErr@1004 : TextConst 'DAN=Der skal være en standardregel i systemet. Du kan fjerne standardegenskaben fra denne regel ved at tildele standarden til en anden regel.;ENU=There must be one default rule in the system. To remove the default property from this rule, assign default to another rule.';
      UpdateAssCustomerQst@1005 : TextConst 'DAN=Hvis du sletter dokumentafsendelsesprofilen %1, slettes den også på de debitorkort, der bruger profilen.\\Vil du fortsætte?;ENU=If you delete document sending profile %1, it will also be deleted on customer cards that use the profile.\\Do you want to continue?';
      CannotDeleteErr@1006 : TextConst 'DAN=Dokumentafsendelsesprofilen kan ikke slettes.;ENU=Cannot delete the document sending profile.';
      CannotSendMultipleSalesDocsErr@1008 : TextConst 'DAN=Du kan kun send ét elektronisk salgsdokument ad gangen.;ENU=You can only send one electronic sales document at a time.';

    PROCEDURE GetDefaultForCustomer@4(CustomerNo@1000 : Code[20];VAR DocumentSendingProfile@1002 : Record 60);
    VAR
      Customer@1001 : Record 18;
    BEGIN
      IF Customer.GET(CustomerNo) THEN
        IF DocumentSendingProfile.GET(Customer."Document Sending Profile") THEN
          EXIT;

      GetDefault(DocumentSendingProfile);
    END;

    PROCEDURE GetDefault@1(VAR DefaultDocumentSendingProfile@1000 : Record 60);
    VAR
      DocumentSendingProfile@1001 : Record 60;
    BEGIN
      DocumentSendingProfile.SETRANGE(Default,TRUE);
      IF NOT DocumentSendingProfile.FINDFIRST THEN BEGIN
        DocumentSendingProfile.INIT;
        DocumentSendingProfile.VALIDATE(Code,DefaultCodeTxt);
        DocumentSendingProfile.VALIDATE(Description,DefaultDescriptionTxt);
        DocumentSendingProfile.VALIDATE("E-Mail","E-Mail"::"Yes (Prompt for Settings)");
        DocumentSendingProfile.VALIDATE("E-Mail Attachment","E-Mail Attachment"::PDF);
        DocumentSendingProfile.VALIDATE(Default,TRUE);
        DocumentSendingProfile.INSERT(TRUE);
      END;

      DefaultDocumentSendingProfile := DocumentSendingProfile;
    END;

    PROCEDURE GetRecordAsText@2() : Text;
    VAR
      RecordAsText@1000 : Text;
    BEGIN
      RecordAsText := '';

      IF ("Electronic Document" <> "Electronic Document"::No) AND ("Electronic Format" <> '') THEN
        RecordAsText := STRSUBSTNO(
            RecordAsTextFormatterTxt,
            STRSUBSTNO(FieldCaptionContentFormatterTxt,FIELDCAPTION("Electronic Document"),"Electronic Document"),RecordAsText);

      IF "E-Mail" <> "E-Mail"::No THEN
        RecordAsText := STRSUBSTNO(
            RecordAsTextFormatterTxt,
            STRSUBSTNO(FieldCaptionContentFormatterTxt,FIELDCAPTION("E-Mail"),"E-Mail Attachment"),RecordAsText);

      IF Printer <> Printer::No THEN
        RecordAsText := STRSUBSTNO(RecordAsTextFormatterTxt,FIELDCAPTION(Printer),RecordAsText);

      IF Disk <> Disk::No THEN
        RecordAsText := STRSUBSTNO(
            RecordAsTextFormatterTxt,STRSUBSTNO(FieldCaptionContentFormatterTxt,FIELDCAPTION(Disk),Disk),RecordAsText);

      EXIT(RecordAsText);
    END;

    PROCEDURE WillUserBePrompted@5() : Boolean;
    BEGIN
      EXIT(
        (Printer = Printer::"Yes (Prompt for Settings)") OR
        ("E-Mail" = "E-Mail"::"Yes (Prompt for Settings)"));
    END;

    PROCEDURE SetDocumentUsage@3(DocumentVariant@1000 : Variant);
    VAR
      ElectronicDocumentFormat@1001 : Record 61;
      DocumentUsage@1002 : Option;
    BEGIN
      ElectronicDocumentFormat.GetDocumentUsage(DocumentUsage,DocumentVariant);
      VALIDATE(Usage,DocumentUsage);
    END;

    PROCEDURE VerifySelectedOptionsValid@6();
    BEGIN
      IF "One Related Party Selected" THEN
        EXIT;

      IF "E-Mail Attachment" > "E-Mail Attachment"::PDF THEN
        ERROR(CannotSendMultipleSalesDocsErr);

      IF "Electronic Document" > "Electronic Document"::No THEN
        ERROR(CannotSendMultipleSalesDocsErr);
    END;

    BEGIN
    END.
  }
}

