OBJECT Codeunit 179 Reversal-Post
{
  OBJECT-PROPERTIES
  {
    Date=23-09-13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=179;
    OnRun=VAR
            GLReg@1003 : Record 45;
            GenJnlTemplate@1002 : Record 80;
            GenJnlPostReverse@1001 : Codeunit 17;
            Txt@1004 : Text[1024];
            WarningText@1000 : Text[250];
            Number@1005 : Integer;
          BEGIN
            RESET;
            SETRANGE("Entry Type","Entry Type"::"Fixed Asset");
            IF FINDFIRST THEN
              WarningText := Text007;
            SETRANGE("Entry Type");
            IF PrintRegister THEN
              Txt := Text004 + WarningText + '\' + Text005
            ELSE
              Txt := Text004 + WarningText + '\' + Text002;
            IF NOT FINDFIRST THEN
              ERROR(Text006);
            IF CONFIRM(Txt,FALSE) THEN BEGIN
              ReversalEntry := Rec;
              IF "Reversal Type" = "Reversal Type"::Transaction THEN
                ReversalEntry.SetReverseFilter("Transaction No.","Reversal Type")
              ELSE
                ReversalEntry.SetReverseFilter("G/L Register No.","Reversal Type");
              ReversalEntry.CheckEntries;
              GET(1);
              IF "Reversal Type" = "Reversal Type"::Register THEN
                Number := "G/L Register No."
              ELSE
                Number := "Transaction No.";
              IF NOT ReversalEntry.VerifyReversalEntries(Rec,Number,"Reversal Type") THEN
                ERROR(Text008);
              GenJnlPostReverse.Reverse(ReversalEntry,Rec);
              IF PrintRegister THEN BEGIN
                GenJnlTemplate.VALIDATE(Type);
                IF GenJnlTemplate."Posting Report ID" <> 0 THEN
                  IF GLReg.FINDLAST THEN BEGIN;
                    GLReg.SETRECFILTER;
                    REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg);
                  END;
              END;
              DELETEALL;
              MESSAGE(Text003);
            END;
          END;

  }
  CODE
  {
    VAR
      Text002@1007 : TextConst 'DAN=Vil du tilbageføre posterne?;ENU=Do you want to reverse the entries?';
      Text003@1005 : TextConst 'DAN=Posterne blev tilbageført.;ENU=The entries were successfully reversed.';
      Text004@1012 : TextConst 'DAN=Der vil blive bogført korrigerende poster for at tilbageføre disse poster.;ENU=To reverse these entries, correcting entries will be posted.';
      Text005@1000 : TextConst 'DAN=Vil du tilbageføre posterne og udskrive rapporten?;ENU=Do you want to reverse the entries and print the report?';
      Text006@1001 : TextConst 'DAN=Der er ikke noget at tilbageføre.;ENU=There is nothing to reverse.';
      Text007@1004 : TextConst 'DAN=\Der er en eller flere anlægsposter. Du bør overveje at bruge anlægsfunktionen Annuller poster.;ENU=\There are one or more FA Ledger Entries. You should consider using the fixed asset function Cancel Entries.';
      Text008@1003 : TextConst 'DAN=Der er foretaget ændringer i bogførte poster, efter at vinduet blev åbnet.\Luk vinduet, og åbn det igen for at fortsætte.;ENU=Changes have been made to posted entries after the window was opened.\Close and reopen the window to continue.';
      ReversalEntry@1002 : Record 179;
      PrintRegister@1009 : Boolean;

    PROCEDURE SetPrint@1000(NewPrintRegister@1001 : Boolean);
    BEGIN
      PrintRegister := NewPrintRegister;
    END;

    BEGIN
    END.
  }
}

