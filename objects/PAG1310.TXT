OBJECT Page 1310 Mini Activities
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Aktiviteter;
               ENU=Activities];
    SourceTable=Table1313;
    PageType=CardPart;
    ShowFilter=No;
    OnInit=BEGIN
             UpdateGroupsVisibility;
             AutoPlayWelcomeVideo := VideoVisible AND MiniGettingStartedMgt.ShouldWelcomeVideoBePlayed;
           END;

    OnOpenPage=BEGIN
                 RESET;
                 IF NOT GET THEN BEGIN
                   INIT;
                   INSERT;
                 END;
                 SETFILTER("Due Date Filter",'>=%1',WORKDATE);
                 SETFILTER("Overdue Date Filter",'<%1',WORKDATE);
                 SETFILTER("User ID Filter",USERID);

                 HasCamera := CameraProvider.IsAvailable;
                 IF HasCamera THEN
                   CameraProvider := CameraProvider.Create;
               END;

    OnAfterGetRecord=VAR
                       DocExchServiceSetup@1000 : Record 1275;
                     BEGIN
                       CalculateCueFieldValues;
                       ShowDocumentsPendingDocExchService := FALSE;
                       IF DocExchServiceSetup.GET THEN
                         ShowDocumentsPendingDocExchService := DocExchServiceSetup.Enabled;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF GettingStartedVisible <> MiniGettingStartedMgt.IsGettingStartedVisible THEN BEGIN
                             UpdateGroupsVisibility;
                             CurrPage.UPDATE;
                           END;
                         END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 7       ;1   ;Action    ;
                      Name=Set Up Cues;
                      CaptionML=[DAN=Konfigurer køindikatorer;
                                 ENU=Set Up Cues];
                      Image=Setup;
                      OnAction=VAR
                                 CueRecordRef@1000 : RecordRef;
                               BEGIN
                                 CueRecordRef.GETTABLE(Rec);
                                 CueSetup.OpenCustomizePageForCurrentUser(CueRecordRef.NUMBER);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 16  ;1   ;Group     ;
                CaptionML=[DAN=Introduktion;
                           ENU=Getting Started];
                Visible=GettingStartedVisible;
                GroupType=CueGroup;
                ActionList=ACTIONS
                {
                  { 12      ;    ;Action    ;
                                  Name=WelcomeIntroductionVideo;
                                  CaptionML=[DAN=Velkommen!;
                                             ENU=Welcome!];
                                  ToolTipML=[DAN=Se en kort video for at få mere at vide om de fem ting, du skal vide først.;
                                             ENU=Watch a short video to learn five things you need to know first.];
                                  Visible=VideoVisible;
                                  Image=TileVideo;
                                  OnAction=BEGIN
                                             MiniGettingStartedMgt.PlayWelcomeVideoForWebClient;
                                           END;
                                            }
                  { 14      ;    ;Action    ;
                                  Name=SettingupYourSystem;
                                  CaptionML=[DAN=Konfigurerer systemet;
                                             ENU=Setting Up Your System];
                                  ToolTipML=[DAN=Se en video for at få mere at vide om, hvordan du konfigurerer systemet til dit behov.;
                                             ENU=Watch a video to learn how to set up the system for your needs.];
                                  Visible=VideoVisible;
                                  Image=TileVideo;
                                  OnAction=BEGIN
                                             CASE CURRENTCLIENTTYPE OF
                                               CLIENTTYPE::Tablet:
                                                 MiniGettingStartedMgt.PlaySettingUpYourSystemVideoForTablet;
                                               ELSE
                                                 MiniGettingStartedMgt.PlaySettingUpYourSystemVideoForWebClient;
                                             END;
                                           END;
                                            }
                  { 13      ;    ;Action    ;
                                  Name=ManageYourBusiness;
                                  CaptionML=[DAN=Administrer din virksomhed;
                                             ENU=Manage Your Business];
                                  ToolTipML=[DAN=Få mere at vide om, hvordan du udfører dine forretningsopgaver.;
                                             ENU=Learn about how to do your business tasks.];
                                  Image=TileHelp }
                }
                 }

    { 10  ;1   ;Group     ;
                CaptionML=[DAN=Salg;
                           ENU=Sales];
                GroupType=CueGroup }

    { 2   ;2   ;Field     ;
                ToolTipML=[DAN=Viser antallet af åbne salgsfakturaer.;
                           ENU=Shows the number of open sales invoices.];
                SourceExpr="Ongoing Sales Invoices";
                DrillDownPageID=Mini Sales Invoice List }

    { 20  ;2   ;Field     ;
                ToolTipML=[DAN=Viser antallet af åbne salgstilbud.;
                           ENU=Shows the number of open sales quotes.];
                SourceExpr="Ongoing Sales Quotes";
                DrillDownPageID=Mini Sales Quotes List }

    { 17  ;2   ;Field     ;
                ToolTipML=[DAN=Viser summen af forfaldne betalinger fra debitorer.;
                           ENU=Shows the sum of overdue payments from customers.];
                SourceExpr="Overdue Sales Invoice Amount";
                OnDrillDown=BEGIN
                              MiniActivitiesMgt.DrillDownCalcOverdueSalesInvoiceAmount;
                            END;
                             }

    { 3   ;2   ;Field     ;
                ToolTipML=[DAN=Viser summen af salg i den aktuelle måned.;
                           ENU=Shows the sum of sales in the current month.];
                SourceExpr="Sales This Month";
                DrillDownPageID=Mini Sales Invoice List;
                OnDrillDown=BEGIN
                              MiniActivitiesMgt.DrillDownSalesThisMonth;
                            END;
                             }

    { 4   ;2   ;Field     ;
                ToolTipML=[DAN=Andelen af dette års salg til de ti største kunder.;
                           ENU=The share of sales this year made to the ten largest customers.];
                SourceExpr="Top 10 Customer Sales YTD" }

    { 28  ;1   ;Group     ;
                CaptionML=[DAN=Dokumentudvekslingstjeneste;
                           ENU=Document Exchange Service];
                Visible=ShowDocumentsPendingDocExchService;
                GroupType=CueGroup }

    { 27  ;2   ;Field     ;
                SourceExpr="Sales Inv. - Pending Doc.Exch.";
                Visible=ShowDocumentsPendingDocExchService;
                DrillDownPageID=Mini Posted Sales Invoices }

    { 26  ;2   ;Field     ;
                SourceExpr="Sales CrM. - Pending Doc.Exch.";
                Visible=ShowDocumentsPendingDocExchService;
                DrillDownPageID=Mini Posted Sales Credit Memos }

    { 9   ;1   ;Group     ;
                CaptionML=[DAN=Køb;
                           ENU=Purchases];
                GroupType=CueGroup }

    { 5   ;2   ;Field     ;
                ToolTipML=[DAN=Viser antallet af åbne købsfakturaer.;
                           ENU=Shows the number of open purchase invoices.];
                SourceExpr="Ongoing Purchase Invoices";
                DrillDownPageID=Mini Purchase Invoice List }

    { 18  ;2   ;Field     ;
                ToolTipML=[DAN=Viser summen af dine forfaldne betalinger til kreditorer.;
                           ENU=Shows the sum of your overdue payments to vendors.];
                SourceExpr="Overdue Purch. Invoice Amount";
                OnDrillDown=BEGIN
                              MiniActivitiesMgt.DrillDownOverduePurchaseInvoiceAmount;
                            END;
                             }

    { 24  ;1   ;Group     ;
                CaptionML=[DAN=Godkendelser;
                           ENU=Approvals];
                Visible=FALSE;
                GroupType=CueGroup }

    { 25  ;2   ;Field     ;
                ToolTipML=[DAN=Viser antallet af godkendelsesanmodninger, der kræver din godkendelse.;
                           ENU=Shows the number of approval requests that require your approval.];
                SourceExpr="Requests to Approve";
                DrillDownPageID=Requests to Approve }

    { 8   ;1   ;Group     ;
                CaptionML=[DAN=Økonomisk resultat;
                           ENU=Financial Performance];
                GroupType=CueGroup }

    { 19  ;2   ;Field     ;
                ToolTipML=[DAN=Angiver, hvor lang tid debitorerne brugte på at betale fakturaer i de seneste tre måneder. Dette er det gennemsnitlige antal dage, fra fakturaerne udstedes, til debitorerne betaler fakturaerne.;
                           ENU=Specifies how long customers took to pay invoices in the last three months. This is the average number of days from when invoices are issued to when customers pay the invoices.];
                SourceExpr="Average Collection Days";
                Image=Calendar }

    { 33  ;1   ;Group     ;
                CaptionML=[DAN=Kamera;
                           ENU=Camera];
                Visible=HasCamera;
                GroupType=CueGroup;
                ActionList=ACTIONS
                {
                  { 21      ;    ;Action    ;
                                  Name=CreateIncomingDocumentFromCamera;
                                  CaptionML=[DAN=Opret indgående bilag fra kamera;
                                             ENU=Create Incoming Doc. from Camera];
                                  Image=TileCamera;
                                  OnAction=VAR
                                             CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
                                           BEGIN
                                             IF NOT HasCamera THEN
                                               EXIT;

                                             CameraOptions := CameraOptions.CameraOptions;
                                             CameraOptions.Quality := 100; // 100%
                                             CameraProvider.RequestPictureAsync(CameraOptions);
                                           END;
                                            }
                }
                 }

    { 34  ;1   ;Group     ;
                CaptionML=[DAN=Indgående bilag;
                           ENU=Incoming Documents];
                GroupType=CueGroup }

    { 30  ;2   ;Field     ;
                SourceExpr="My Incoming Documents" }

    { 15  ;1   ;Group     ;
                Visible=AutoPlayWelcomeVideo;
                GroupType=Group }

    { 11  ;2   ;Field     ;
                Name=WebClientPageReady;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.PageReady;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      MiniGettingStartedMgt@1001 : Codeunit 1321;
      FileManagement@1003 : Codeunit 419;
      MiniActivitiesMgt@1009 : Codeunit 1311;
      CueSetup@1005 : Codeunit 9701;
      CameraProvider@1007 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      GettingStartedVisible@1002 : Boolean INDATASET;
      VideoVisible@1000 : Boolean INDATASET;
      AutoPlayWelcomeVideo@1004 : Boolean INDATASET;
      HasCamera@1008 : Boolean;
      ShowDocumentsPendingDocExchService@1006 : Boolean;

    LOCAL PROCEDURE CalculateCueFieldValues@6();
    BEGIN
      IF FIELDACTIVE("Overdue Sales Invoice Amount") THEN
        "Overdue Sales Invoice Amount" := MiniActivitiesMgt.CalcOverdueSalesInvoiceAmount;
      IF FIELDACTIVE("Overdue Purch. Invoice Amount") THEN
        "Overdue Purch. Invoice Amount" := MiniActivitiesMgt.CalcOverduePurchaseInvoiceAmount;
      IF FIELDACTIVE("Sales This Month") THEN
        "Sales This Month" := MiniActivitiesMgt.CalcSalesThisMonthAmount;
      IF FIELDACTIVE("Top 10 Customer Sales YTD") THEN
        "Top 10 Customer Sales YTD" := MiniActivitiesMgt.CalcTop10CustomerSalesRatioYTD;
      IF FIELDACTIVE("Average Collection Days") THEN
        "Average Collection Days" := MiniActivitiesMgt.CalcAverageCollectionDays;
    END;

    LOCAL PROCEDURE WelcomeVideoDisplayTargetIsSupported@4() : Boolean;
    BEGIN
      EXIT(CURRENTCLIENTTYPE = CLIENTTYPE::Web);
    END;

    LOCAL PROCEDURE UpdateGroupsVisibility@9();
    BEGIN
      GettingStartedVisible := MiniGettingStartedMgt.IsGettingStartedVisible;
      VideoVisible := GettingStartedVisible AND WelcomeVideoDisplayTargetIsSupported;
    END;

    EVENT WebClientPageReady@-11::AddInReady@0();
    BEGIN
      IF FileManagement.IsWebClient THEN
        MiniGettingStartedMgt.PlayWelcomeVideoOnFirstLogin;
    END;

    EVENT CameraProvider@1007::PictureAvailable@10(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    VAR
      IncomingDocument@1002 : Record 130;
    BEGIN
      IncomingDocument.CreateIncomingDocumentFromServerFile(PictureName,PictureFilePath);
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

