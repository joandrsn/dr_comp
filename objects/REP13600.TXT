OBJECT Report 13600 Create Electronic Invoices
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVDK9.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Opret elektroniske fakturaer;
               ENU=Create Electronic Invoices];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 5581;    ;DataItem;                    ;
               DataItemTable=Table112;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               SalesInvHeader@1101100000 : Record 112;
                             BEGIN
                               Counter := 0;

                               // Any electronic invoices?
                               SalesInvHeader.COPY("Sales Invoice Header");
                               SalesInvHeader.FILTERGROUP(8);
                               SalesInvHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT SalesInvHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic invoices?
                               SalesInvHeader.SETRANGE("EAN No.",'');
                               IF SalesInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               SalesInvHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               SalesInvHeader.SETRANGE("Electronic Invoice Created",TRUE);
                               IF SalesInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=VAR
                                  OIOUBLExportSalesInvoice@1060001 : Codeunit 13609;
                                BEGIN
                                  OIOUBLExportSalesInvoice.ExportXML("Sales Invoice Header");

                                  IF LogInteraction THEN
                                    IF "Bill-to Contact No." <> '' THEN
                                      SegManagement.LogDocument(
                                        4,"No.",0,0,DATABASE::Contact,"Bill-to Contact No.","Salesperson Code",
                                        "Campaign No.","Posting Description",'')
                                    ELSE
                                      SegManagement.LogDocument(
                                        4,"No.",0,0,DATABASE::Customer,"Bill-to Customer No.","Salesperson Code",
                                        "Campaign No.","Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.,EAN No.,Electronic Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[DAN=Logfør interaktion;
                             ENU=Log Interaction];
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1101100000 : TextConst 'DAN=En eller flere fakturaer, som opfylder filterkriterierne, bliver sprunget over, da de ikke er elektroniske fakturaer.\\Vil du fortsætte?;ENU=One or more invoices that match your filter criteria are not electronic invoices and will be skipped.\\Do you want to continue?';
      Text001@1101100001 : TextConst 'DAN=En eller flere fakturaer, som stemmer overens med dine filterkriterier, er oprettet tidligere.\\Vil du fortsætte?;ENU=One or more invoices that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1101100003 : TextConst 'DAN=Der er oprettet %1 elektronisk(e) faktura(er).;ENU=Successfully created %1 electronic invoice(s).';
      SegManagement@1101100007 : Codeunit 5051;
      Counter@1101100004 : Integer;
      Text003@1101100005 : TextConst 'DAN=Der er intet at oprette.;ENU=There is nothing to create.';
      LogInteraction@1101100006 : Boolean;
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100000();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(4) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

