OBJECT Table 337 Reservation Entry
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ActionMessageEntry@1000 : Record 99000849;
             BEGIN
               ActionMessageEntry.SETCURRENTKEY("Reservation Entry");
               ActionMessageEntry.SETRANGE("Reservation Entry","Entry No.");
               ActionMessageEntry.DELETEALL;
             END;

    CaptionML=[DAN=Reservationspost;
               ENU=Reservation Entry];
    LookupPageID=Page497;
    DrillDownPageID=Page497;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[DAN=Løbenr.;
                                                              ENU=Entry No.] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[DAN=Varenr.;
                                                              ENU=Item No.] }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[DAN=Lokationskode;
                                                              ENU=Location Code] }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                Quantity := CalcReservationQuantity;
                                                                "Qty. to Handle (Base)" := "Quantity (Base)";
                                                                "Qty. to Invoice (Base)" := "Quantity (Base)";
                                                              END;

                                                   CaptionML=[DAN=Antal (basis);
                                                              ENU=Quantity (Base)];
                                                   DecimalPlaces=0:5 }
    { 5   ;   ;Reservation Status  ;Option        ;CaptionML=[DAN=Reservationsstatus;
                                                              ENU=Reservation Status];
                                                   OptionCaptionML=[DAN=Reservation,Sporing,Overskud,Emne;
                                                                    ENU=Reservation,Tracking,Surplus,Prospect];
                                                   OptionString=Reservation,Tracking,Surplus,Prospect }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 8   ;   ;Creation Date       ;Date          ;CaptionML=[DAN=Oprettelsesdato;
                                                              ENU=Creation Date] }
    { 9   ;   ;Transferred from Entry No.;Integer ;TableRelation="Reservation Entry";
                                                   CaptionML=[DAN=Overført fra post nr.;
                                                              ENU=Transferred from Entry No.] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[DAN=Kildetype;
                                                              ENU=Source Type] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[DAN=Kildeundertype;
                                                              ENU=Source Subtype];
                                                   OptionCaptionML=[DAN=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ENU=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[DAN=Kilde-id;
                                                              ENU=Source ID] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[DAN=Kildekladdenavn;
                                                              ENU=Source Batch Name] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[DAN=Kildeprod.ordrelinje;
                                                              ENU=Source Prod. Order Line] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[DAN=Kildereferencenr.;
                                                              ENU=Source Ref. No.] }
    { 16  ;   ;Item Ledger Entry No.;Integer      ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[DAN=Varepostløbenr.;
                                                              ENU=Item Ledger Entry No.];
                                                   Editable=No }
    { 22  ;   ;Expected Receipt Date;Date         ;CaptionML=[DAN=Forventet modt.dato;
                                                              ENU=Expected Receipt Date] }
    { 23  ;   ;Shipment Date       ;Date          ;CaptionML=[DAN=Afsendelsesdato;
                                                              ENU=Shipment Date] }
    { 24  ;   ;Serial No.          ;Code20        ;CaptionML=[DAN=Serienr.;
                                                              ENU=Serial No.] }
    { 25  ;   ;Created By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Created By");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Oprettet af;
                                                              ENU=Created By] }
    { 27  ;   ;Changed By          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Changed By");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Ændret af;
                                                              ENU=Changed By] }
    { 28  ;   ;Positive            ;Boolean       ;CaptionML=[DAN=Positiv;
                                                              ENU=Positive];
                                                   Editable=No }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                Quantity := ROUND("Quantity (Base)" / "Qty. per Unit of Measure",0.00001);
                                                              END;

                                                   CaptionML=[DAN=Antal pr. enhed;
                                                              ENU=Qty. per Unit of Measure];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 30  ;   ;Quantity            ;Decimal       ;CaptionML=[DAN=Antal;
                                                              ENU=Quantity];
                                                   DecimalPlaces=0:5 }
    { 31  ;   ;Action Message Adjustment;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Action Message Entry".Quantity WHERE (Reservation Entry=FIELD(Entry No.),
                                                                                                          Calculation=CONST(Sum)));
                                                   CaptionML=[DAN=Aktionsmeddelelsesjustering;
                                                              ENU=Action Message Adjustment];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 32  ;   ;Binding             ;Option        ;CaptionML=[DAN=Binding;
                                                              ENU=Binding];
                                                   OptionCaptionML=[DAN=" ,Ordre-til-ordre";
                                                                    ENU=" ,Order-to-Order"];
                                                   OptionString=[ ,Order-to-Order];
                                                   Editable=No }
    { 33  ;   ;Suppressed Action Msg.;Boolean     ;CaptionML=[DAN=Undertrykt aktionsmeddelelse;
                                                              ENU=Suppressed Action Msg.] }
    { 34  ;   ;Planning Flexibility;Option        ;CaptionML=[DAN=Planlægningsfleksibilitet;
                                                              ENU=Planning Flexibility];
                                                   OptionCaptionML=[DAN=Ubegrænset,Ingen;
                                                                    ENU=Unlimited,None];
                                                   OptionString=Unlimited,None }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;CaptionML=[DAN=Udl.varepostløbenr.;
                                                              ENU=Appl.-to Item Entry] }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=[DAN=Garantiophør den;
                                                              ENU=Warranty Date];
                                                   Editable=No }
    { 41  ;   ;Expiration Date     ;Date          ;CaptionML=[DAN=Udløbsdato;
                                                              ENU=Expiration Date];
                                                   Editable=No }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;CaptionML=[DAN=Håndteringsantal (basis);
                                                              ENU=Qty. to Handle (Base)];
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;CaptionML=[DAN=Fakturer antal (basis);
                                                              ENU=Qty. to Invoice (Base)];
                                                   DecimalPlaces=0:5 }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=[DAN=Faktureret antal (basis);
                                                              ENU=Quantity Invoiced (Base)];
                                                   DecimalPlaces=0:5 }
    { 80  ;   ;New Serial No.      ;Code20        ;CaptionML=[DAN=Nyt serienr.;
                                                              ENU=New Serial No.];
                                                   Editable=No }
    { 81  ;   ;New Lot No.         ;Code20        ;CaptionML=[DAN=Nyt lotnr.;
                                                              ENU=New Lot No.];
                                                   Editable=No }
    { 900 ;   ;Disallow Cancellation;Boolean      ;CaptionML=[DAN=Afvis annullering;
                                                              ENU=Disallow Cancellation] }
    { 5400;   ;Lot No.             ;Code20        ;CaptionML=[DAN=Lotnr.;
                                                              ENU=Lot No.] }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[DAN=Variantkode;
                                                              ENU=Variant Code] }
    { 5811;   ;Appl.-from Item Entry;Integer      ;CaptionML=[DAN=Udlign fra-varepost;
                                                              ENU=Appl.-from Item Entry];
                                                   MinValue=0 }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=[DAN=Rettelse;
                                                              ENU=Correction] }
    { 6505;   ;New Expiration Date ;Date          ;CaptionML=[DAN=Ny udløbsdato;
                                                              ENU=New Expiration Date];
                                                   Editable=No }
    { 6510;   ;Item Tracking       ;Option        ;CaptionML=[DAN=Varesporing;
                                                              ENU=Item Tracking];
                                                   OptionCaptionML=[DAN=Ingen,Lotnr.,Lot- og serienr.,Serienr.;
                                                                    ENU=None,Lot No.,Lot and Serial No.,Serial No.];
                                                   OptionString=None,Lot No.,Lot and Serial No.,Serial No.;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.,Positive                      ;Clustered=Yes }
    {    ;Source ID,Source Ref. No.,Source Type,Source Subtype,Source Batch Name,Source Prod. Order Line,Reservation Status,Shipment Date,Expected Receipt Date;
                                                   SumIndexFields=Quantity (Base),Quantity;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code,Reservation Status,Shipment Date,Expected Receipt Date,Serial No.,Lot No.;
                                                   SumIndexFields=Quantity (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Source Type,Source Subtype,Reservation Status,Location Code,Variant Code,Shipment Date,Expected Receipt Date,Serial No.,Lot No.;
                                                   SumIndexFields=Quantity (Base),Quantity;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code,Location Code,Item Tracking,Reservation Status,Lot No.,Serial No.;
                                                   SumIndexFields=Quantity (Base);
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    { No ;Lot No.                                  }
    { No ;Serial No.                               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Positive,Item No.,Description,Quantity }
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'DAN=Linje;ENU=Line';

    PROCEDURE TextCaption@1() : Text[255];
    VAR
      ItemLedgEntry@1000 : Record 32;
      SalesLine@1007 : Record 37;
      ReqLine@1006 : Record 246;
      PurchLine@1005 : Record 39;
      ItemJnlLine@1004 : Record 83;
      ProdOrderLine@1003 : Record 5406;
      ProdOrderComp@1002 : Record 5407;
      AssemblyHeader@1011 : Record 900;
      AssemblyLine@1012 : Record 901;
      TransLine@1001 : Record 5741;
      ServLine@1009 : Record 5902;
      JobJnlLine@1010 : Record 210;
    BEGIN
      CASE "Source Type" OF
        DATABASE::"Item Ledger Entry":
          EXIT(ItemLedgEntry.TABLECAPTION);
        DATABASE::"Sales Line":
          EXIT(SalesLine.TABLECAPTION);
        DATABASE::"Requisition Line":
          EXIT(ReqLine.TABLECAPTION);
        DATABASE::"Purchase Line":
          EXIT(PurchLine.TABLECAPTION);
        DATABASE::"Item Journal Line":
          EXIT(ItemJnlLine.TABLECAPTION);
        DATABASE::"Job Journal Line":
          EXIT(JobJnlLine.TABLECAPTION);
        DATABASE::"Prod. Order Line":
          EXIT(ProdOrderLine.TABLECAPTION);
        DATABASE::"Prod. Order Component":
          EXIT(ProdOrderComp.TABLECAPTION);
        DATABASE::"Assembly Header":
          EXIT(AssemblyHeader.TABLECAPTION);
        DATABASE::"Assembly Line":
          EXIT(AssemblyLine.TABLECAPTION);
        DATABASE::"Transfer Line":
          EXIT(TransLine.TABLECAPTION);
        DATABASE::"Service Line":
          EXIT(ServLine.TABLECAPTION);
        ELSE
          EXIT(Text001);
      END;
    END;

    PROCEDURE SummEntryNo@3() : Integer;
    BEGIN
      CASE "Source Type" OF
        DATABASE::"Item Ledger Entry":
          EXIT(1);
        DATABASE::"Purchase Line":
          EXIT(11 + "Source Subtype");
        DATABASE::"Requisition Line":
          EXIT(21);
        DATABASE::"Sales Line":
          EXIT(31 + "Source Subtype");
        DATABASE::"Item Journal Line":
          EXIT(41 + "Source Subtype");
        DATABASE::"Job Journal Line":
          EXIT(51 + "Source Subtype");
        DATABASE::"Prod. Order Line":
          EXIT(61 + "Source Subtype");
        DATABASE::"Prod. Order Component":
          EXIT(71 + "Source Subtype");
        DATABASE::"Transfer Line":
          EXIT(101 + "Source Subtype");
        DATABASE::"Service Line":
          EXIT(110);
        DATABASE::"Assembly Header":
          EXIT(141 + "Source Subtype");
        DATABASE::"Assembly Line":
          EXIT(151 + "Source Subtype");
        ELSE
          EXIT(0);
      END;
    END;

    PROCEDURE SetPointer@2(RowID@1000 : Text[250]);
    VAR
      ItemTrackingMgt@1001 : Codeunit 6500;
      StrArray@1002 : ARRAY [6] OF Text[100];
    BEGIN
      ItemTrackingMgt.DecomposeRowID(RowID,StrArray);
      EVALUATE("Source Type",StrArray[1]);
      EVALUATE("Source Subtype",StrArray[2]);
      "Source ID" := StrArray[3];
      "Source Batch Name" := StrArray[4];
      EVALUATE("Source Prod. Order Line",StrArray[5]);
      EVALUATE("Source Ref. No.",StrArray[6]);
    END;

    PROCEDURE Lock@4();
    VAR
      Rec2@1000 : Record 337;
    BEGIN
      Rec2.SETCURRENTKEY("Item No.");
      IF "Item No." <> '' THEN
        Rec2.SETRANGE("Item No.","Item No.");
      Rec2.LOCKTABLE;
      IF Rec2.FINDLAST THEN;
    END;

    PROCEDURE UpdateItemTracking@5();
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      "Item Tracking" := ItemTrackingMgt.ItemTrackingOption("Lot No.","Serial No.");
    END;

    PROCEDURE ClearItemTrackingFields@7();
    BEGIN
      "Lot No." := '';
      "Serial No." := '';
      UpdateItemTracking;
    END;

    LOCAL PROCEDURE FilterLinesWithItemToPlan@70(VAR Item@1000 : Record 27;IsReceipt@1001 : Boolean);
    BEGIN
      RESET;
      SETCURRENTKEY(
        "Item No.","Variant Code","Location Code","Reservation Status","Shipment Date","Expected Receipt Date");
      SETRANGE("Item No.",Item."No.");
      SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      SETRANGE("Reservation Status","Reservation Status"::Reservation);
      SETFILTER(Binding,'<>%1',Binding::"Order-to-Order");
      IF IsReceipt THEN
        SETFILTER("Expected Receipt Date",Item.GETFILTER("Date Filter"))
      ELSE
        SETFILTER("Shipment Date",Item.GETFILTER("Date Filter"));
      SETFILTER("Quantity (Base)",'<>0');
    END;

    LOCAL PROCEDURE FindLinesWithItemToPlan@68(VAR Item@1000 : Record 27;IsReceipt@1001 : Boolean) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item,IsReceipt);
      EXIT(FIND('-'));
    END;

    LOCAL PROCEDURE LinesWithItemToPlanExist@67(VAR Item@1000 : Record 27;IsReceipt@1001 : Boolean) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item,IsReceipt);
      EXIT(NOT ISEMPTY);
    END;

    LOCAL PROCEDURE CalcReservationQuantity@62() : Decimal;
    VAR
      ReservEntry@1001 : Record 337;
    BEGIN
      IF "Qty. per Unit of Measure" = 1 THEN
        EXIT("Quantity (Base)");

      ReservEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
      ReservEntry.SETRANGE("Source ID","Source ID");
      ReservEntry.SETRANGE("Source Batch Name","Source Batch Name");
      ReservEntry.SETRANGE("Source Ref. No.","Source Ref. No.");
      ReservEntry.SETRANGE("Source Type","Source Type");
      ReservEntry.SETRANGE("Source Subtype","Source Subtype");
      ReservEntry.SETRANGE("Source Prod. Order Line","Source Prod. Order Line");
      ReservEntry.SETRANGE("Reservation Status","Reservation Status"::Reservation);
      ReservEntry.CALCSUMS("Quantity (Base)",Quantity);
      EXIT(
        ROUND((ReservEntry."Quantity (Base)" + "Quantity (Base)") / "Qty. per Unit of Measure",0.00001) -
        ReservEntry.Quantity);
    END;

    PROCEDURE ClearApplFromToItemEntry@6();
    BEGIN
      IF Positive THEN
        "Appl.-to Item Entry" := 0
      ELSE
        "Appl.-from Item Entry" := 0;
    END;

    PROCEDURE TrackingExists@11() : Boolean;
    BEGIN
      EXIT(("Serial No." <> '') OR ("Lot No." <> ''));
    END;

    BEGIN
    END.
  }
}

