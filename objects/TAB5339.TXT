OBJECT Table 5339 Integration Synch. Job Errors
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Fejl i integrationsynk.job;
               ENU=Integration Synch. Job Errors];
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[DAN=Nummer;
                                                              ENU=No.] }
    { 2   ;   ;Integration Synch. Job ID;GUID     ;TableRelation="Integration Synch. Job".ID;
                                                   CaptionML=[DAN=Id for integrationssynk.job;
                                                              ENU=Integration Synch. Job ID] }
    { 3   ;   ;Source Record ID    ;RecordID      ;CaptionML=[DAN=Kilderecord-id;
                                                              ENU=Source Record ID] }
    { 4   ;   ;Destination Record ID;RecordID     ;CaptionML=[DAN=Destinationspost-id;
                                                              ENU=Destination Record ID] }
    { 5   ;   ;Message             ;Text250       ;CaptionML=[DAN=Meddelelse;
                                                              ENU=Message] }
    { 6   ;   ;Date/Time           ;DateTime      ;CaptionML=[DAN=Dato/klokkeslæt;
                                                              ENU=Date/Time] }
    { 7   ;   ;Exception Detail    ;BLOB          ;CaptionML=[DAN=Undtagelsesoplysninger;
                                                              ENU=Exception Detail] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Integration Synch. Job ID,Date/Time      }
    {    ;Date/Time,Integration Synch. Job ID      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeleteEntriesQst@1000 : TextConst '@@@="%1 = Integration Synch. Job Errors caption";DAN=Er du sikker på, at du vil slette posterne %1?;ENU=Are you sure that you want to delete the %1 entries?';

    PROCEDURE DeleteEntries@2(DaysOld@1000 : Integer);
    BEGIN
      IF NOT CONFIRM(STRSUBSTNO(DeleteEntriesQst,TABLECAPTION)) THEN
        EXIT;

      SETFILTER("Date/Time",'<=%1',CREATEDATETIME(TODAY - DaysOld,TIME));
      DELETEALL;
      SETRANGE("Date/Time");
    END;

    PROCEDURE LogSynchError@1(IntegrationSynchJobId@1006 : GUID;SourceRecordId@1000 : RecordID;DestinationRecordId@1001 : RecordID;ErrorMessage@1002 : Text);
    VAR
      IntegrationSynchJobErrors@1003 : Record 5339;
      StackTraceOutStream@1004 : OutStream;
    BEGIN
      WITH IntegrationSynchJobErrors DO BEGIN
        INIT;
        "Integration Synch. Job ID" := IntegrationSynchJobId;
        "Source Record ID" := SourceRecordId;
        "Destination Record ID" := DestinationRecordId;
        "Date/Time" := CURRENTDATETIME;
        Message := COPYSTR(ErrorMessage,1,MAXSTRLEN(Message));
        "Exception Detail".CREATEOUTSTREAM(StackTraceOutStream);
        StackTraceOutStream.WRITE(GETLASTERRORCALLSTACK);
        INSERT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

