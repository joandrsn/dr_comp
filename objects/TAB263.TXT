OBJECT Table 263 Intrastat Jnl. Line
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IntraJnlTemplate.GET("Journal Template Name");
               IntrastatJnlBatch.GET("Journal Template Name","Journal Batch Name");
             END;

    OnModify=BEGIN
               IntrastatJnlBatch.GET("Journal Template Name","Journal Batch Name");
               IntrastatJnlBatch.TESTFIELD(Reported,FALSE);
             END;

    OnRename=BEGIN
               IntrastatJnlBatch.GET(xRec."Journal Template Name",xRec."Journal Batch Name");
               IntrastatJnlBatch.TESTFIELD(Reported,FALSE);
             END;

    CaptionML=[DAN=Intrastatkladdelinje;
               ENU=Intrastat Jnl. Line];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Intrastat Jnl. Template";
                                                   CaptionML=[DAN=Kladdetypenavn;
                                                              ENU=Journal Template Name] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Intrastat Jnl. Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[DAN=Kladdenavn;
                                                              ENU=Journal Batch Name] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DAN=Linjenr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Type                ;Option        ;CaptionML=[DAN=Type;
                                                              ENU=Type];
                                                   OptionCaptionML=[DAN=Modtagelse,Afsendelse;
                                                                    ENU=Receipt,Shipment];
                                                   OptionString=Receipt,Shipment }
    { 5   ;   ;Date                ;Date          ;CaptionML=[DAN=Dato;
                                                              ENU=Date] }
    { 6   ;   ;Tariff No.          ;Code20        ;TableRelation="Tariff Number";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Item No.",'');
                                                                GetItemDescription;
                                                              END;

                                                   CaptionML=[DAN=Varekode;
                                                              ENU=Tariff No.];
                                                   NotBlank=Yes }
    { 7   ;   ;Item Description    ;Text50        ;CaptionML=[DAN=Varebeskrivelse;
                                                              ENU=Item Description] }
    { 8   ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[DAN=Lande-/områdekode;
                                                              ENU=Country/Region Code] }
    { 9   ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=[DAN=Transaktionsart;
                                                              ENU=Transaction Type] }
    { 10  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=[DAN=Transportmåde;
                                                              ENU=Transport Method] }
    { 11  ;   ;Source Type         ;Option        ;CaptionML=[DAN=Kildetype;
                                                              ENU=Source Type];
                                                   OptionCaptionML=[DAN=,Varepost,Sagspost;
                                                                    ENU=,Item Entry,Job Entry];
                                                   OptionString=,Item Entry,Job Entry;
                                                   BlankZero=Yes }
    { 12  ;   ;Source Entry No.    ;Integer       ;TableRelation=IF (Source Type=CONST(Item Entry)) "Item Ledger Entry"
                                                                 ELSE IF (Source Type=CONST(Job Entry)) "Job Ledger Entry";
                                                   CaptionML=[DAN=Kildepostløbenr.;
                                                              ENU=Source Entry No.];
                                                   Editable=No }
    { 13  ;   ;Net Weight          ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity <> 0 THEN
                                                                  "Total Weight" := ROUND("Net Weight" * Quantity,0.00001)
                                                                ELSE
                                                                  "Total Weight" := 0;
                                                              END;

                                                   CaptionML=[DAN=Nettovægt;
                                                              ENU=Net Weight];
                                                   DecimalPlaces=2:5 }
    { 14  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF "Cost Regulation %" <> 0 THEN
                                                                  VALIDATE("Cost Regulation %")
                                                                ELSE
                                                                  "Statistical Value" := Amount + "Indirect Cost";
                                                              END;

                                                   CaptionML=[DAN=Beløb;
                                                              ENU=Amount];
                                                   DecimalPlaces=0:0 }
    { 15  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF (Quantity <> 0) AND Item.GET("Item No.") THEN
                                                                  VALIDATE("Net Weight",Item."Net Weight")
                                                                ELSE
                                                                  VALIDATE("Net Weight",0);
                                                              END;

                                                   CaptionML=[DAN=Antal;
                                                              ENU=Quantity];
                                                   DecimalPlaces=0:0 }
    { 16  ;   ;Cost Regulation %   ;Decimal       ;OnValidate=BEGIN
                                                                "Indirect Cost" := ROUND(Amount * "Cost Regulation %" / 100,1);
                                                                "Statistical Value" := ROUND(Amount + "Indirect Cost",1);
                                                              END;

                                                   CaptionML=[DAN=Reguleringspct. (kostpris);
                                                              ENU=Cost Regulation %];
                                                   DecimalPlaces=2:2;
                                                   MinValue=-100;
                                                   MaxValue=100 }
    { 17  ;   ;Indirect Cost       ;Decimal       ;OnValidate=BEGIN
                                                                "Cost Regulation %" := 0;
                                                                "Statistical Value" := Amount + "Indirect Cost";
                                                              END;

                                                   CaptionML=[DAN=Indir. omkostninger;
                                                              ENU=Indirect Cost];
                                                   DecimalPlaces=0:0 }
    { 18  ;   ;Statistical Value   ;Decimal       ;CaptionML=[DAN=Statistisk værdi;
                                                              ENU=Statistical Value];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 19  ;   ;Document No.        ;Code20        ;CaptionML=[DAN=Bilagsnr.;
                                                              ENU=Document No.] }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Source Type",0);

                                                                IF "Item No." = '' THEN
                                                                  CLEAR(Item)
                                                                ELSE BEGIN
                                                                  Item.GET("Item No.");
                                                                  Item.TESTFIELD("Tariff No.");
                                                                END;

                                                                Name := Item.Description;
                                                                "Tariff No." := Item."Tariff No.";
                                                                "Country/Region of Origin Code" := Item."Country/Region of Origin Code";
                                                                GetItemDescription;
                                                              END;

                                                   CaptionML=[DAN=Varenr.;
                                                              ENU=Item No.] }
    { 21  ;   ;Name                ;Text50        ;CaptionML=[DAN=Navn;
                                                              ENU=Name] }
    { 22  ;   ;Total Weight        ;Decimal       ;CaptionML=[DAN=Totalvægt;
                                                              ENU=Total Weight];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 23  ;   ;Supplementary Units ;Boolean       ;CaptionML=[DAN=Supplerende enheder;
                                                              ENU=Supplementary Units];
                                                   Editable=No }
    { 24  ;   ;Internal Ref. No.   ;Text10        ;CaptionML=[DAN=Internt referencenr.;
                                                              ENU=Internal Ref. No.];
                                                   Editable=No }
    { 25  ;   ;Country/Region of Origin Code;Code10;
                                                   TableRelation=Country/Region;
                                                   CaptionML=[DAN=Lande-/områdekode for oprindelse;
                                                              ENU=Country/Region of Origin Code] }
    { 26  ;   ;Entry/Exit Point    ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=[DAN=Indførsels-/udførselssted;
                                                              ENU=Entry/Exit Point] }
    { 27  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=[DAN=Område;
                                                              ENU=Area] }
    { 28  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=[DAN=Transaktionsspecifikation;
                                                              ENU=Transaction Specification] }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Line No.;
                                                   SumIndexFields=Statistical Value;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Source Type,Source Entry No.             }
    {    ;Type,Country/Region Code,Tariff No.,Transaction Type,Transport Method }
    {    ;Internal Ref. No.                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      IntraJnlTemplate@1000 : Record 261;
      IntrastatJnlBatch@1001 : Record 262;
      Item@1002 : Record 27;
      TariffNumber@1003 : Record 260;

    LOCAL PROCEDURE GetItemDescription@1();
    BEGIN
      IF "Tariff No." <> '' THEN BEGIN
        TariffNumber.GET("Tariff No.");
        "Item Description" := TariffNumber.Description;
        "Supplementary Units" := TariffNumber."Supplementary Units";
      END ELSE
        "Item Description" := '';
    END;

    PROCEDURE IsOpenedFromBatch@42() : Boolean;
    VAR
      IntrastatJnlBatch@1002 : Record 262;
      TemplateFilter@1001 : Text;
      BatchFilter@1000 : Text;
    BEGIN
      BatchFilter := GETFILTER("Journal Batch Name");
      IF BatchFilter <> '' THEN BEGIN
        TemplateFilter := GETFILTER("Journal Template Name");
        IF TemplateFilter <> '' THEN
          IntrastatJnlBatch.SETFILTER("Journal Template Name",TemplateFilter);
        IntrastatJnlBatch.SETFILTER(Name,BatchFilter);
        IntrastatJnlBatch.FINDFIRST;
      END;

      EXIT((("Journal Batch Name" <> '') AND ("Journal Template Name" = '')) OR (BatchFilter <> ''));
    END;

    BEGIN
    END.
  }
}

