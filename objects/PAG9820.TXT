OBJECT Page 9820 Control Add-ins
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Tilføjelseskontrolelementer;
               ENU=Control Add-ins];
    SourceTable=Table2000000069;
    PageType=List;
    PromotedActionCategoriesML=[DAN=Ny,Proces,Rapport,Ressource for tilføjelseskontrolelement;
                                ENU=New,Process,Report,Control Add-in Resource];
    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 13      ;1   ;ActionGroup;
                      CaptionML=[DAN=Ressource for tilføjelseskontrolelement;
                                 ENU=Control Add-in Resource] }
      { 9       ;2   ;Action    ;
                      Name=Import;
                      CaptionML=[DAN=Indlæs;
                                 ENU=Import];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Import;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 TempBlob@1002 : Record 99008535;
                                 FileManagement@1000 : Codeunit 419;
                                 ResourceName@1001 : Text;
                               BEGIN
                                 IF Resource.HASVALUE THEN
                                   IF NOT CONFIRM(ImportQst) THEN
                                     EXIT;

                                 ResourceName := FileManagement.BLOBImportWithFilter(
                                     TempBlob,ImportTitleTxt,'',
                                     ImportFileTxt + ' (*.zip)|*.zip|' + AllFilesTxt + ' (*.*)|*.*','*.*');

                                 IF ResourceName <> '' THEN BEGIN
                                   Resource := TempBlob.Blob;
                                   CurrPage.SAVERECORD;

                                   MESSAGE(ImportDoneMsg);
                                 END;
                               END;
                                }
      { 10      ;2   ;Action    ;
                      Name=Export;
                      CaptionML=[DAN=Udlæs;
                                 ENU=Export];
                      Promoted=Yes;
                      Image=Export;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 TempBlob@1001 : Record 99008535;
                                 FileManagement@1000 : Codeunit 419;
                               BEGIN
                                 IF NOT Resource.HASVALUE THEN BEGIN
                                   MESSAGE(NoResourceMsg);
                                   EXIT;
                                 END;

                                 TempBlob.Blob := Resource;
                                 FileManagement.BLOBExport(TempBlob,"Add-in Name" + '.zip',TRUE);
                               END;
                                }
      { 12      ;2   ;Action    ;
                      Name=Remove;
                      CaptionML=[DAN=Fjern;
                                 ENU=Remove];
                      Promoted=Yes;
                      Image=Cancel;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 IF NOT Resource.HASVALUE THEN
                                   EXIT;

                                 CLEAR(Resource);
                                 CurrPage.SAVERECORD;

                                 MESSAGE(RemoveDoneMsg);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                SourceExpr="Add-in Name" }

    { 4   ;2   ;Field     ;
                SourceExpr="Public Key Token" }

    { 5   ;2   ;Field     ;
                SourceExpr=Version }

    { 14  ;2   ;Field     ;
                SourceExpr=Category }

    { 6   ;2   ;Field     ;
                SourceExpr=Description }

    { 7   ;2   ;Field     ;
                SourceExpr=Resource }

  }
  CODE
  {
    VAR
      AllFilesTxt@1001 : TextConst 'DAN=Alle filer;ENU=All Files';
      ImportFileTxt@1002 : TextConst 'DAN=Ressource for tilføjelseskontrolelement;ENU=Control Add-in Resource';
      ImportDoneMsg@1003 : TextConst 'DAN=Ressourcen for tilføjelseskontrolelementet er importeret.;ENU=The control add-in resource has been imported.';
      ImportQst@1004 : TextConst 'DAN=Ressourcen for tilføjelseskontrolelementet er allerede angivet.\Vil du overskrive den?;ENU=The control add-in resource is already specified.\Do you want to overwrite it?';
      ImportTitleTxt@1005 : TextConst 'DAN=Importér ressource for tilføjelseskontrolelement;ENU=Import Control Add-in Resource';
      NoResourceMsg@1006 : TextConst 'DAN=Der er ingen ressource for tilføjelseskontrolelementet.;ENU=There is no resource for the control add-in.';
      RemoveDoneMsg@1007 : TextConst 'DAN=Ressourcen for tilføjelseskontrolelementet er fjernet.;ENU=The control add-in resource has been removed.';

    BEGIN
    END.
  }
}

