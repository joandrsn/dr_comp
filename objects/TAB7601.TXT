OBJECT Table 7601 Base Calendar Change
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Base Calendar Code;
    OnInsert=BEGIN
               CheckEntryLine;
             END;

    OnModify=BEGIN
               CheckEntryLine;
             END;

    OnRename=BEGIN
               CheckEntryLine;
             END;

    CaptionML=[DAN=Basiskalenderændring;
               ENU=Base Calendar Change];
  }
  FIELDS
  {
    { 1   ;   ;Base Calendar Code  ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[DAN=Basiskalenderkode;
                                                              ENU=Base Calendar Code];
                                                   Editable=No }
    { 2   ;   ;Recurring System    ;Option        ;OnValidate=BEGIN
                                                                IF "Recurring System" <> xRec."Recurring System" THEN
                                                                  CASE "Recurring System" OF
                                                                    "Recurring System"::"Annual Recurring":
                                                                      Day := Day::" ";
                                                                    "Recurring System"::"Weekly Recurring":
                                                                      Date := 0D;
                                                                  END;
                                                              END;

                                                   CaptionML=[DAN=Tilbagevendende;
                                                              ENU=Recurring System];
                                                   OptionCaptionML=[DAN=" ,Årligt,Ugentligt";
                                                                    ENU=" ,Annual Recurring,Weekly Recurring"];
                                                   OptionString=[ ,Annual Recurring,Weekly Recurring] }
    { 3   ;   ;Date                ;Date          ;OnValidate=BEGIN
                                                                IF ("Recurring System" = "Recurring System"::" ") OR
                                                                   ("Recurring System" = "Recurring System"::"Annual Recurring")
                                                                THEN
                                                                  TESTFIELD(Date)
                                                                ELSE
                                                                  TESTFIELD(Date,0D);
                                                                UpdateDayName;
                                                              END;

                                                   CaptionML=[DAN=Dato;
                                                              ENU=Date] }
    { 4   ;   ;Day                 ;Option        ;OnValidate=BEGIN
                                                                IF "Recurring System" = "Recurring System"::"Weekly Recurring" THEN
                                                                  TESTFIELD(Day);
                                                                UpdateDayName;
                                                              END;

                                                   CaptionML=[DAN=Dag;
                                                              ENU=Day];
                                                   OptionCaptionML=[DAN=" ,Mandag,Tirsdag,Onsdag,Torsdag,Fredag,Lørdag,Søndag";
                                                                    ENU=" ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday"];
                                                   OptionString=[ ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday] }
    { 5   ;   ;Description         ;Text30        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 6   ;   ;Nonworking          ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DAN=Fridag;
                                                              ENU=Nonworking] }
  }
  KEYS
  {
    {    ;Base Calendar Code,Recurring System,Date,Day;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE UpdateDayName@1();
    VAR
      DateTable@1000 : Record 2000000007;
    BEGIN
      IF (Date > 0D) AND
         ("Recurring System" = "Recurring System"::"Annual Recurring")
      THEN
        Day := Day::" "
      ELSE BEGIN
        DateTable.SETRANGE("Period Type",DateTable."Period Type"::Date);
        DateTable.SETRANGE("Period Start",Date);
        IF DateTable.FINDFIRST THEN
          Day := DateTable."Period No.";
      END;
      IF (Date = 0D) AND (Day = Day::" ") THEN BEGIN
        Day := xRec.Day;
        Date := xRec.Date;
      END;
      IF "Recurring System" = "Recurring System"::"Annual Recurring" THEN
        TESTFIELD(Day,Day::" ");
    END;

    LOCAL PROCEDURE CheckEntryLine@2();
    BEGIN
      CASE "Recurring System" OF
        "Recurring System"::" ":
          BEGIN
            TESTFIELD(Date);
            TESTFIELD(Day);
          END;
        "Recurring System"::"Annual Recurring":
          BEGIN
            TESTFIELD(Date);
            TESTFIELD(Day,Day::" ");
          END;
        "Recurring System"::"Weekly Recurring":
          BEGIN
            TESTFIELD(Date,0D);
            TESTFIELD(Day);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

