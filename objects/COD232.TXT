OBJECT Codeunit 232 Gen. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DAN=kan ikke afgrænses ved hjælp af et filter, når du bogfører gentagelseskladder.;ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'DAN=Skal kladden bogføres og rapporten eller rapporterne udskrives?;ENU=Do you want to post the journal lines and print the report(s)?';
      Text002@1002 : TextConst 'DAN=Der er intet at bogføre.;ENU=There is nothing to post.';
      Text003@1003 : TextConst 'DAN=Kladden er bogført.;ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'DAN=Kladden er bogført. Nu vises kladde %1.;ENU=The journal lines were successfully posted. You are now in the %1 journal.';
      GenJnlTemplate@1005 : Record 80;
      GenJnlLine@1006 : Record 81;
      GLReg@1007 : Record 45;
      CustLedgEntry@1010 : Record 21;
      VendLedgEntry@1011 : Record 25;
      GenJnlPostBatch@1008 : Codeunit 13;
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate."Force Posting Report" OR
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) AND (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        THEN
          GenJnlTemplate.TESTFIELD("Posting Report ID");
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        GenJnlPostBatch.RUN(GenJnlLine);

        IF GLReg.GET("Line No.") THEN BEGIN
          IF GenJnlTemplate."Cust. Receipt Report ID" <> 0 THEN BEGIN
            CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",FALSE,FALSE,CustLedgEntry);
          END;
          IF GenJnlTemplate."Vendor Receipt Report ID" <> 0 THEN BEGIN
            VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",FALSE,FALSE,VendLedgEntry);
          END;
          IF GenJnlTemplate."Posting Report ID" <> 0 THEN BEGIN
            GLReg.SETRECFILTER;
            REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg);
          END;
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

