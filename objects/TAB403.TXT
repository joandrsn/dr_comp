OBJECT Table 403 Change Log Setup (Table)
{
  OBJECT-PROPERTIES
  {
    Date=23-09-13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Opsætn. af ændringslog (tabel);
               ENU=Change Log Setup (Table)];
  }
  FIELDS
  {
    { 1   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[DAN=Tabelnr.;
                                                              ENU=Table No.] }
    { 2   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[DAN=Tabeltitel;
                                                              ENU=Table Caption] }
    { 3   ;   ;Log Insertion       ;Option        ;OnValidate=BEGIN
                                                                IF (xRec."Log Insertion" = xRec."Log Insertion"::"Some Fields") AND (xRec."Log Insertion" <> "Log Insertion") THEN
                                                                  IF CONFIRM(STRSUBSTNO(Text001,xRec.FIELDCAPTION("Log Insertion"),xRec."Log Insertion"),FALSE) THEN
                                                                    DelChangeLogFields(0);
                                                              END;

                                                   CaptionML=[DAN=Logfør indsættelse;
                                                              ENU=Log Insertion];
                                                   OptionCaptionML=[DAN=" ,Nogle felter,Alle felter";
                                                                    ENU=" ,Some Fields,All Fields"];
                                                   OptionString=[ ,Some Fields,All Fields] }
    { 4   ;   ;Log Modification    ;Option        ;OnValidate=BEGIN
                                                                IF (xRec."Log Modification" = xRec."Log Modification"::"Some Fields") AND (xRec."Log Modification" <> "Log Modification") THEN
                                                                  IF CONFIRM(STRSUBSTNO(Text001,xRec.FIELDCAPTION("Log Modification"),xRec."Log Modification"),FALSE) THEN
                                                                    DelChangeLogFields(1);
                                                              END;

                                                   CaptionML=[DAN=Logfør ændring;
                                                              ENU=Log Modification];
                                                   OptionCaptionML=[DAN=" ,Nogle felter,Alle felter";
                                                                    ENU=" ,Some Fields,All Fields"];
                                                   OptionString=[ ,Some Fields,All Fields] }
    { 5   ;   ;Log Deletion        ;Option        ;OnValidate=BEGIN
                                                                IF (xRec."Log Deletion" = xRec."Log Deletion"::"Some Fields") AND (xRec."Log Deletion" <> "Log Deletion") THEN
                                                                  IF CONFIRM(STRSUBSTNO(Text001,xRec.FIELDCAPTION("Log Deletion"),xRec."Log Deletion"),FALSE) THEN
                                                                    DelChangeLogFields(2);
                                                              END;

                                                   CaptionML=[DAN=Logfør sletning;
                                                              ENU=Log Deletion];
                                                   OptionCaptionML=[DAN=" ,Nogle felter,Alle felter";
                                                                    ENU=" ,Some Fields,All Fields"];
                                                   OptionString=[ ,Some Fields,All Fields] }
  }
  KEYS
  {
    {    ;Table No.                               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'DAN=Du har ændret feltet %1, så det ikke længere er %2. Vil du fjerne feltmarkeringerne?;ENU=You have changed the %1 field to no longer be %2. Do you want to remove the field selections?';

    PROCEDURE DelChangeLogFields@1(InsModDel@1000 : Integer);
    VAR
      ChangeLogSetupField@1001 : Record 404;
    BEGIN
      ChangeLogSetupField.SETRANGE("Table No.","Table No.");
      CASE InsModDel OF
        0:
          ChangeLogSetupField.SETRANGE("Log Insertion",TRUE);
        1:
          ChangeLogSetupField.SETRANGE("Log Modification",TRUE);
        2:
          ChangeLogSetupField.SETRANGE("Log Deletion",TRUE);
      END;
      IF ChangeLogSetupField.FIND('-') THEN
        REPEAT
          CASE InsModDel OF
            0:
              ChangeLogSetupField."Log Insertion" := FALSE;
            1:
              ChangeLogSetupField."Log Modification" := FALSE;
            2:
              ChangeLogSetupField."Log Deletion" := FALSE;
          END;
          WITH ChangeLogSetupField DO
            IF "Log Insertion" OR "Log Modification" OR "Log Deletion" THEN
              MODIFY
            ELSE
              DELETE;
        UNTIL ChangeLogSetupField.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

