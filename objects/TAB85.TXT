OBJECT Table 85 Acc. Schedule Line
{
  OBJECT-PROPERTIES
  {
    Date=01-08-17;
    Time=12:00:00;
    Version List=NAVW19.00.00.48992;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF xRec."Line No." = 0 THEN
                 IF NOT AccSchedName.GET("Schedule Name") THEN BEGIN
                   AccSchedName.INIT;
                   AccSchedName.Name := "Schedule Name";
                   IF AccSchedName.Name = '' THEN
                     AccSchedName.Description := Text000;
                   AccSchedName.INSERT;
                 END;
             END;

    CaptionML=[DAN=Kontoskemalinje;
               ENU=Acc. Schedule Line];
  }
  FIELDS
  {
    { 1   ;   ;Schedule Name       ;Code10        ;TableRelation="Acc. Schedule Name";
                                                   CaptionML=[DAN=Skemanavn;
                                                              ENU=Schedule Name] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DAN=Linjenr.;
                                                              ENU=Line No.] }
    { 3   ;   ;Row No.             ;Code10        ;CaptionML=[DAN=Rubriknr.;
                                                              ENU=Row No.] }
    { 4   ;   ;Description         ;Text80        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 5   ;   ;Totaling            ;Text250       ;TableRelation=IF (Totaling Type=CONST(Posting Accounts)) "G/L Account"
                                                                 ELSE IF (Totaling Type=CONST(Total Accounts)) "G/L Account"
                                                                 ELSE IF (Totaling Type=CONST(Cash Flow Entry Accounts)) "Cash Flow Account"
                                                                 ELSE IF (Totaling Type=CONST(Cash Flow Total Accounts)) "Cash Flow Account"
                                                                 ELSE IF (Totaling Type=CONST(Cost Type)) "Cost Type"
                                                                 ELSE IF (Totaling Type=CONST(Cost Type Total)) "Cost Type";
                                                   OnValidate=BEGIN
                                                                CASE "Totaling Type" OF
                                                                  "Totaling Type"::"Posting Accounts","Totaling Type"::"Total Accounts":
                                                                    BEGIN
                                                                      GLAcc.SETFILTER("No.",Totaling);
                                                                      GLAcc.CALCFIELDS(Balance);
                                                                    END;
                                                                  "Totaling Type"::Formula,"Totaling Type"::"Set Base For Percent":
                                                                    BEGIN
                                                                      Totaling := UPPERCASE(Totaling);
                                                                      CheckFormula(Totaling);
                                                                    END;
                                                                  "Totaling Type"::"Cost Type","Totaling Type"::"Cost Type Total":
                                                                    BEGIN
                                                                      CostType.SETFILTER("No.",Totaling);
                                                                      CostType.CALCFIELDS(Balance);
                                                                    END;
                                                                  "Totaling Type"::"Cash Flow Entry Accounts","Totaling Type"::"Cash Flow Total Accounts":
                                                                    BEGIN
                                                                      CFAccount.SETFILTER("No.",Totaling);
                                                                      CFAccount.CALCFIELDS(Amount);
                                                                    END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupTotaling;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Sammentælling;
                                                              ENU=Totaling] }
    { 6   ;   ;Totaling Type       ;Option        ;OnValidate=BEGIN
                                                                VALIDATE(Totaling);
                                                              END;

                                                   CaptionML=[DAN=Sammentællingstype;
                                                              ENU=Totaling Type];
                                                   OptionCaptionML=[DAN=Kontosum,sumsum,Formel,,,Angiv grundlag for procent,Omk.typesum,Omk.typesumsum,Pengestrømskontosum,Pengestrømssum;
                                                                    ENU=Posting Accounts,Total Accounts,Formula,,,Set Base For Percent,Cost Type,Cost Type Total,Cash Flow Entry Accounts,Cash Flow Total Accounts];
                                                   OptionString=Posting Accounts,Total Accounts,Formula,,,Set Base For Percent,Cost Type,Cost Type Total,Cash Flow Entry Accounts,Cash Flow Total Accounts }
    { 7   ;   ;New Page            ;Boolean       ;CaptionML=[DAN=Sideskift;
                                                              ENU=New Page] }
    { 11  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[DAN=Datofilter;
                                                              ENU=Date Filter] }
    { 12  ;   ;Dimension 1 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[DAN=Dimension 1-filter;
                                                              ENU=Dimension 1 Filter];
                                                   CaptionClass=GetCaptionClass(1) }
    { 13  ;   ;Dimension 2 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[DAN=Dimension 2-filter;
                                                              ENU=Dimension 2 Filter];
                                                   CaptionClass=GetCaptionClass(2) }
    { 14  ;   ;G/L Budget Filter   ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="G/L Budget Name";
                                                   CaptionML=[DAN=Finansbudgetfilter;
                                                              ENU=G/L Budget Filter] }
    { 15  ;   ;Business Unit Filter;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Business Unit";
                                                   CaptionML=[DAN=Koncernvirk.filter;
                                                              ENU=Business Unit Filter] }
    { 16  ;   ;Show                ;Option        ;CaptionML=[DAN=Vis;
                                                              ENU=Show];
                                                   OptionCaptionML=[DAN=Ja,Nej,Hvis kolonne ikke nul,Ved positiv saldo,Ved negativ saldo;
                                                                    ENU=Yes,No,If Any Column Not Zero,When Positive Balance,When Negative Balance];
                                                   OptionString=Yes,No,If Any Column Not Zero,When Positive Balance,When Negative Balance }
    { 17  ;   ;Dimension 3 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[DAN=Dimension 3-filter;
                                                              ENU=Dimension 3 Filter];
                                                   CaptionClass=GetCaptionClass(3) }
    { 18  ;   ;Dimension 4 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[DAN=Dimension 4-filter;
                                                              ENU=Dimension 4 Filter];
                                                   CaptionClass=GetCaptionClass(4) }
    { 19  ;   ;Dimension 1 Totaling;Text250       ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 348=R;
                                                   CaptionML=[DAN=Dimension 1 - sammentælling;
                                                              ENU=Dimension 1 Totaling];
                                                   CaptionClass=GetCaptionClass(5) }
    { 20  ;   ;Dimension 2 Totaling;Text250       ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 348=R;
                                                   CaptionML=[DAN=Dimension 2 - sammentælling;
                                                              ENU=Dimension 2 Totaling];
                                                   CaptionClass=GetCaptionClass(6) }
    { 21  ;   ;Dimension 3 Totaling;Text250       ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 350=R;
                                                   CaptionML=[DAN=Dimension 3 - sammentælling;
                                                              ENU=Dimension 3 Totaling];
                                                   CaptionClass=GetCaptionClass(7) }
    { 22  ;   ;Dimension 4 Totaling;Text250       ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 350=R;
                                                   CaptionML=[DAN=Dimension 4 - sammentælling;
                                                              ENU=Dimension 4 Totaling];
                                                   CaptionClass=GetCaptionClass(8) }
    { 23  ;   ;Bold                ;Boolean       ;CaptionML=[DAN=Fed;
                                                              ENU=Bold] }
    { 24  ;   ;Italic              ;Boolean       ;CaptionML=[DAN=Kursiv;
                                                              ENU=Italic] }
    { 25  ;   ;Underline           ;Boolean       ;OnValidate=BEGIN
                                                                IF "Double Underline" AND Underline THEN BEGIN
                                                                  "Double Underline" := FALSE;
                                                                  MESSAGE(ForceUnderLineMsg,FIELDCAPTION("Double Underline"));
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Understreget;
                                                              ENU=Underline] }
    { 26  ;   ;Show Opposite Sign  ;Boolean       ;CaptionML=[DAN=Vis modsat fortegn;
                                                              ENU=Show Opposite Sign] }
    { 27  ;   ;Row Type            ;Option        ;CaptionML=[DAN=Rækketype;
                                                              ENU=Row Type];
                                                   OptionCaptionML=[DAN=Bevægelse,Saldo til dato,Startsaldo;
                                                                    ENU=Net Change,Balance at Date,Beginning Balance];
                                                   OptionString=Net Change,Balance at Date,Beginning Balance }
    { 28  ;   ;Amount Type         ;Option        ;CaptionML=[DAN=Beløbstype;
                                                              ENU=Amount Type];
                                                   OptionCaptionML=[DAN=Nettobeløb,Debetbeløb,Kreditbeløb;
                                                                    ENU=Net Amount,Debit Amount,Credit Amount];
                                                   OptionString=Net Amount,Debit Amount,Credit Amount }
    { 30  ;   ;Double Underline    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Double Underline" AND Underline THEN BEGIN
                                                                  Underline := FALSE;
                                                                  MESSAGE(ForceUnderLineMsg,FIELDCAPTION(Underline));
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Dobbelt understregning;
                                                              ENU=Double Underline] }
    { 840 ;   ;Cash Flow Forecast Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Cash Flow Forecast";
                                                   CaptionML=[DAN=Pengestrømsprognosefilter;
                                                              ENU=Cash Flow Forecast Filter] }
    { 1100;   ;Cost Center Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Center";
                                                   CaptionML=[DAN=Omkostningsstedsfilter;
                                                              ENU=Cost Center Filter] }
    { 1101;   ;Cost Object Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Object";
                                                   CaptionML=[DAN=Omkostningsemnefilter;
                                                              ENU=Cost Object Filter] }
    { 1102;   ;Cost Center Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Omkostningsstedssammentælling;
                                                              ENU=Cost Center Totaling] }
    { 1103;   ;Cost Object Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Omkostningsemnesammentælling;
                                                              ENU=Cost Object Totaling] }
    { 1104;   ;Cost Budget Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Budget Name";
                                                   CaptionML=[DAN=Omkostningsbudgetfilter;
                                                              ENU=Cost Budget Filter] }
  }
  KEYS
  {
    {    ;Schedule Name,Line No.                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ForceUnderLineMsg@1022 : TextConst '@@@="%1= Field underline ";DAN=%1 angives til falsk.;ENU=%1 will be set to false.';
      Text000@1000 : TextConst 'DAN=Standardskema;ENU=Default Schedule';
      Text001@1001 : TextConst 'DAN=Parentesen på position %1 står forkert.;ENU=The parenthesis at position %1 is misplaced.';
      Text002@1002 : TextConst 'DAN=Der må ikke stå to operatorer efter hinanden. Fejlen opstod på position %1.;ENU=You cannot have two consecutive operators. The error occurred at position %1.';
      Text003@1003 : TextConst 'DAN=Der mangler en operand efter position %1.;ENU=There is an operand missing after position %1.';
      Text004@1004 : TextConst 'DAN=Der er flere venstreparenteser end højreparenteser.;ENU=There are more left parentheses than right parentheses.';
      Text005@1005 : TextConst 'DAN=Der er flere højreparenteser end venstreparenteser.;ENU=There are more right parentheses than left parentheses.';
      Text006@1006 : TextConst 'DAN=1,6,,Dimension 1-filter;ENU=1,6,,Dimension 1 Filter';
      Text007@1007 : TextConst 'DAN=1,6,,Dimension 2-filter;ENU=1,6,,Dimension 2 Filter';
      Text008@1008 : TextConst 'DAN=1,6,,Dimension 3-filter;ENU=1,6,,Dimension 3 Filter';
      Text009@1009 : TextConst 'DAN=1,6,,Dimension 4-filter;ENU=1,6,,Dimension 4 Filter';
      Text010@1010 : TextConst 'DAN=,, Sammentælling;ENU=,, Totaling';
      Text011@1011 : TextConst 'DAN=1,5,,Dimension 1-sammentælling;ENU=1,5,,Dimension 1 Totaling';
      Text012@1012 : TextConst 'DAN=1,5,,Dimension 2-sammentælling;ENU=1,5,,Dimension 2 Totaling';
      Text013@1013 : TextConst 'DAN=1,5,,Dimension 3-sammentælling;ENU=1,5,,Dimension 3 Totaling';
      Text014@1014 : TextConst 'DAN=1,5,,Dimension 4-sammentælling;ENU=1,5,,Dimension 4 Totaling';
      AccSchedName@1015 : Record 84;
      GLAcc@1016 : Record 15;
      CFAccount@1841 : Record 841;
      AnalysisView@1017 : Record 363;
      GLSetup@1018 : Record 98;
      CostType@1021 : Record 1103;
      HasGLSetup@1019 : Boolean;
      Text015@1020 : TextConst 'DAN=%1 refererer til %2 %3, som ikke findes. Feltet %4 i tabellen %5 er blevet slettet.;ENU=The %1 refers to %2 %3, which does not exist. The field %4 on table %5 has now been deleted.';

    PROCEDURE LookUpDimFilter@57(DimNo@1000 : Integer;VAR Text@1001 : Text[250]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
      DimValList@1003 : Page 560;
    BEGIN
      GetAccSchedSetup;
      CASE DimNo OF
        1:
          DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 1 Code");
        2:
          DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 2 Code");
        3:
          DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 3 Code");
        4:
          DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 4 Code");
      END;

      DimValList.LOOKUPMODE(TRUE);
      DimValList.SETTABLEVIEW(DimVal);
      IF DimValList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValList.GETRECORD(DimVal);
        Text := DimValList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    PROCEDURE CheckFormula@1(Formula@1000 : Code[250]);
    VAR
      i@1001 : Integer;
      ParenthesesLevel@1002 : Integer;
      HasOperator@1003 : Boolean;
    BEGIN
      ParenthesesLevel := 0;
      FOR i := 1 TO STRLEN(Formula) DO BEGIN
        IF Formula[i] = '(' THEN
          ParenthesesLevel := ParenthesesLevel + 1
        ELSE
          IF Formula[i] = ')' THEN
            ParenthesesLevel := ParenthesesLevel - 1;
        IF ParenthesesLevel < 0 THEN
          ERROR(Text001,i);
        IF Formula[i] IN ['+','-','*','/','^'] THEN BEGIN
          IF HasOperator THEN
            ERROR(Text002,i);

          HasOperator := TRUE;

          IF i = STRLEN(Formula) THEN
            ERROR(Text003,i);

          IF Formula[i + 1] = ')' THEN
            ERROR(Text003,i);
        END ELSE
          HasOperator := FALSE;
      END;
      IF ParenthesesLevel > 0 THEN
        ERROR(Text004);

      IF ParenthesesLevel < 0 THEN
        ERROR(Text005);
    END;

    LOCAL PROCEDURE GetCaptionClass@7(AnalysisViewDimType@1000 : Integer) : Text[250];
    BEGIN
      GetAccSchedSetup;
      CASE AnalysisViewDimType OF
        1:
          BEGIN
            IF AnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 1 Code");

            EXIT(Text006);
          END;
        2:
          BEGIN
            IF AnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 2 Code");

            EXIT(Text007);
          END;
        3:
          BEGIN
            IF AnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 3 Code");

            EXIT(Text008);
          END;
        4:
          BEGIN
            IF AnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 4 Code");

            EXIT(Text009);
          END;
        5:
          BEGIN
            IF AnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 1 Code" + Text010);

            EXIT(Text011);
          END;
        6:
          BEGIN
            IF AnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 2 Code" + Text010);

            EXIT(Text012);
          END;
        7:
          BEGIN
            IF AnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 3 Code" + Text010);

            EXIT(Text013);
          END;
        8:
          BEGIN
            IF AnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 4 Code" + Text010);

            EXIT(Text014);
          END;
      END;
    END;

    LOCAL PROCEDURE GetAccSchedSetup@2();
    BEGIN
      IF "Schedule Name" <> AccSchedName.Name THEN
        AccSchedName.GET("Schedule Name");
      IF AccSchedName."Analysis View Name" <> '' THEN
        IF AccSchedName."Analysis View Name" <> AnalysisView.Code THEN
          IF NOT AnalysisView.GET(AccSchedName."Analysis View Name") THEN BEGIN
            MESSAGE(
              Text015,
              AccSchedName.TABLECAPTION,AnalysisView.TABLECAPTION,AccSchedName."Analysis View Name",
              AccSchedName.FIELDCAPTION("Analysis View Name"),AccSchedName.TABLECAPTION);
            AccSchedName."Analysis View Name" := '';
            AccSchedName.MODIFY;
          END;

      IF AccSchedName."Analysis View Name" = '' THEN BEGIN
        IF NOT HasGLSetup THEN BEGIN
          GLSetup.GET;
          HasGLSetup := TRUE;
        END;
        CLEAR(AnalysisView);
        AnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
        AnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
      END;
    END;

    LOCAL PROCEDURE LookupTotaling@3();
    VAR
      GLAccList@1000 : Page 18;
      CostTypeList@1001 : Page 1124;
      CFAccList@1002 : Page 855;
    BEGIN
      CASE "Totaling Type" OF
        "Totaling Type"::"Posting Accounts",
        "Totaling Type"::"Total Accounts":
          BEGIN
            GLAccList.LOOKUPMODE(TRUE);
            IF GLAccList.RUNMODAL = ACTION::LookupOK THEN
              VALIDATE(Totaling,GLAccList.GetSelectionFilter);
          END;
        "Totaling Type"::"Cost Type",
        "Totaling Type"::"Cost Type Total":
          BEGIN
            CostTypeList.LOOKUPMODE(TRUE);
            IF CostTypeList.RUNMODAL = ACTION::LookupOK THEN
              VALIDATE(Totaling,CostTypeList.GetSelectionFilter);
          END;
        "Totaling Type"::"Cash Flow Entry Accounts",
        "Totaling Type"::"Cash Flow Total Accounts":
          BEGIN
            CFAccList.LOOKUPMODE(TRUE);
            IF CFAccList.RUNMODAL = ACTION::LookupOK THEN
              VALIDATE(Totaling,CFAccList.GetSelectionFilter);
          END;
      END;
    END;

    PROCEDURE LookupGLBudgetFilter@4(VAR Text@1000 : Text[10]) : Boolean;
    VAR
      GLBudgetNames@1001 : Page 121;
    BEGIN
      GLBudgetNames.LOOKUPMODE(TRUE);
      IF GLBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := GLBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    PROCEDURE LookupCostBudgetFilter@5(VAR Text@1000 : Text[10]) : Boolean;
    VAR
      CostBudgetNames@1001 : Page 1116;
    BEGIN
      CostBudgetNames.LOOKUPMODE(TRUE);
      IF CostBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := CostBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    BEGIN
    END.
  }
}

