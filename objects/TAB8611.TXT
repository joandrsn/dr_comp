OBJECT Table 8611 Config. Question Area
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ConfigQuestion@1000 : Record 8612;
             BEGIN
               ConfigQuestion.RESET;
               ConfigQuestion.SETRANGE("Questionnaire Code","Questionnaire Code");
               ConfigQuestion.SETRANGE("Question Area Code",Code);
               ConfigQuestion.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text003);
             END;

    CaptionML=[DAN=Konfig.spørgsmålsområde;
               ENU=Config. Question Area];
    LookupPageID=Page8613;
  }
  FIELDS
  {
    { 1   ;   ;Questionnaire Code  ;Code10        ;TableRelation="Config. Questionnaire";
                                                   CaptionML=[DAN=Spørgeskemakode;
                                                              ENU=Questionnaire Code];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Code                ;Code10        ;OnValidate=VAR
                                                                TestValue@1000 : Integer;
                                                              BEGIN
                                                                IF EVALUATE(TestValue,COPYSTR(Code,1,1)) THEN
                                                                  ERROR(Text002);
                                                              END;

                                                   CaptionML=[DAN=Kode;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 4   ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=FILTER(Table));
                                                   OnValidate=VAR
                                                                ConfigQuestion@1000 : Record 8612;
                                                                ConfigQuestionArea@1001 : Record 8611;
                                                              BEGIN
                                                                IF (xRec."Table ID" <> "Table ID") AND (xRec."Table ID" > 0) THEN BEGIN
                                                                  ConfigQuestion.SETRANGE("Questionnaire Code","Questionnaire Code");
                                                                  ConfigQuestion.SETRANGE("Question Area Code",Code);
                                                                  IF NOT ConfigQuestion.ISEMPTY THEN
                                                                    ERROR(STRSUBSTNO(Text000,Code));
                                                                  ConfigQuestionArea.SETRANGE("Questionnaire Code","Questionnaire Code");
                                                                  ConfigQuestionArea.SETRANGE("Table ID","Table ID");
                                                                  IF NOT ConfigQuestionArea.ISEMPTY THEN
                                                                    ERROR(STRSUBSTNO(Text001,"Table ID"));
                                                                END;
                                                                CALCFIELDS("Table Name","Table Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigValidateMgt@1000 : Codeunit 8617;
                                                            BEGIN
                                                              ConfigValidateMgt.LookupTable("Table ID");
                                                              VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[DAN=Tabel-id;
                                                              ENU=Table ID] }
    { 5   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=[DAN=Tabelnavn;
                                                              ENU=Table Name];
                                                   Editable=No }
    { 6   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[DAN=Tabeltitel;
                                                              ENU=Table Caption] }
    { 7   ;   ;No. of Questions    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Question" WHERE (Questionnaire Code=FIELD(Questionnaire Code),
                                                                                               Question Area Code=FIELD(Code)));
                                                   CaptionML=[DAN=Antal spørgsmål;
                                                              ENU=No. of Questions];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Questionnaire Code,Code                 ;Clustered=Yes }
    {    ;Table ID                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1003 : TextConst 'DAN=Slet spørgsmål til spørgsmålsområdet %1 for at ændre tabellens relation.;ENU=Delete questions for question area %1 to change the table relationship.';
      Text001@1000 : TextConst 'DAN=Der findes allerede et spørgsmålsområde til tabel %1.;ENU=A question area already exists for table %1.';
      Text002@1001 : TextConst 'DAN=Det første tegn kan ikke være en numerisk værdi.;ENU=The first character cannot be a numeric value.';
      Text003@1002 : TextConst 'DAN=Du kan ikke omdøbe et spørgsmålsområde.;ENU=You cannot rename a question area.';

    BEGIN
    END.
  }
}

