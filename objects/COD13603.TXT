OBJECT Codeunit 13603 OIOUBL Check Fin. Charge Memo
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVDK9.00;
  }
  PROPERTIES
  {
    TableNo=302;
    OnRun=BEGIN
            IF "EAN No." <> '' THEN BEGIN
              ReadCompanyInfo;
              ReadGLSetup;

              CompanyInfo.TESTFIELD("VAT Registration No.");
              CompanyInfo.TESTFIELD(Name);
              CompanyInfo.TESTFIELD(Address);
              CompanyInfo.TESTFIELD(City);
              CompanyInfo.TESTFIELD("Post Code");
              CompanyInfo.TESTFIELD("Country/Region Code");
              IF CompanyInfo.IBAN = '' THEN
                CompanyInfo.TESTFIELD("Bank Account No.");
              CompanyInfo.TESTFIELD("Bank Branch No.");
              IF "Payment Channel" = "Payment Channel"::" " THEN
                FIELDERROR("Payment Channel",MusthaveavalueErr);
              IF NOT OIOUBLDocumentEncode.IsValidEANNo("EAN No.") THEN
                FIELDERROR("EAN No.",Text13606);
              OIOUBLDocumentEncode.GetOIOUBLCountryRegionCode("Country/Region Code");
              OIOUBLDocumentEncode.GetOIOUBLCountryRegionCode(CompanyInfo."Country/Region Code");
              OIOUBLDocumentEncode.IsValidCountryCode("Country/Region Code");
              TESTFIELD(Name);
              TESTFIELD(Address);
              TESTFIELD(City);
              TESTFIELD("Post Code");
              TESTFIELD(Contact);
              "Currency Code" := OIOUBLDocumentEncode.GetOIOUBLCurrencyCode("Currency Code");
              CheckFinChargeMemoLines(Rec);
            END;
          END;

  }
  CODE
  {
    VAR
      CompanyInfo@1101100002 : Record 79;
      GLSetup@1101100003 : Record 98;
      OIOUBLDocumentEncode@1101100004 : Codeunit 13600;
      CompanyInfoRead@1101100000 : Boolean;
      GLSetupRead@1101100001 : Boolean;
      Text13606@1101100005 : TextConst 'DAN=indeholder ikke et gyldigt 13-cifret EAN-nr.;ENU=does not contain a valid, 13-digit EAN No.';
      EmptyDescriptionErr@13607 : TextConst 'DAN=Rentenotaen %1 indeholder linjer, hvor Type og Nr. er angivet, men Beskrivelse er tom. Dette er ikke tilladt i et OIOUBL-dokument, som muligvis bliver oprettet på baggrund af det bogførte dokument.;ENU=The Finance Charge Memo %1 contains lines in which the Type and the No. are specified, but the Description is empty. This is not allowed for an OIOUBL document which might be created from the posted document.';
      EmptyFieldsQst@13608 : TextConst 'DAN=Rentenotaen %1 indeholder linjer, hvor enten Dokumentnr. eller Nr. er tom. Disse linjer medregnes ikke, når der oprettes et OIOUBL-dokument./Vil du fortsætte?;ENU=The Finance Charge Memo %1 contains lines in which either the Document No. or the No. is empty. Such lines will not be taken into account when creating an OIOUBL document./Do you want to continue?';
      Text13610@13610 : TextConst 'DAN=Oprettelsen er afbrudt for at overholde advarslen.;ENU=The issuing has been interrupted to respect the warning.';
      MusthaveavalueErr@1060000 : TextConst 'DAN=skal indeholde en værdi;ENU=must have a value';

    LOCAL PROCEDURE ReadCompanyInfo@1101100003();
    BEGIN
      IF NOT CompanyInfoRead THEN BEGIN
        CompanyInfo.GET;
        CompanyInfoRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE ReadGLSetup@1101100011();
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckFinChargeMemoLines@1160000(FinChargeMemoHeader@1160000 : Record 302);
    VAR
      FinChargeMemoLine@1160001 : Record 303;
      EmptyLineFound@1160002 : Boolean;
    BEGIN
      EmptyLineFound := FALSE;
      WITH FinChargeMemoLine DO BEGIN
        RESET;
        SETRANGE("Finance Charge Memo No.",FinChargeMemoHeader."No.");
        IF FINDSET THEN
          REPEAT
            IF Description = '' THEN
              IF (Type <> Type::" ") AND ("No." <> '') THEN
                ERROR(EmptyDescriptionErr,"Finance Charge Memo No.");
            IF Type = Type::" " THEN
              EmptyLineFound := TRUE;
            IF (Type = Type::"G/L Account") AND ("No." = '') THEN
              EmptyLineFound := TRUE;
            IF (Type = Type::"Customer Ledger Entry") AND ("Document No." = '') THEN
              EmptyLineFound := TRUE;
          UNTIL (NEXT = 0);

        IF EmptyLineFound THEN
          IF NOT CONFIRM(EmptyFieldsQst,TRUE,"Finance Charge Memo No.") THEN
            ERROR(Text13610);
      END;
    END;

    BEGIN
    END.
  }
}

