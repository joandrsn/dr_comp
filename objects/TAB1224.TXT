OBJECT Table 1224 Data Exch. Mapping
{
  OBJECT-PROPERTIES
  {
    Date=26-01-19;
    Time=12:00:00;
    Version List=NAVW19.00.00.51014;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               DataExchFieldMapping.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
               DataExchFieldMapping.SETRANGE("Table ID","Table ID");
               DataExchFieldMapping.DELETEALL;
             END;

    OnRename=BEGIN
               IF HasFieldMappings THEN
                 ERROR(RenameErr);
             END;

    CaptionML=[DAN=Dataudvekslingskobling;
               ENU=Data Exch. Mapping];
  }
  FIELDS
  {
    { 1   ;   ;Data Exch. Def Code ;Code20        ;TableRelation="Data Exch. Def";
                                                   CaptionML=[DAN=Dataudvekslingsdefinitionskode;
                                                              ENU=Data Exch. Def Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[DAN=Tabel-id;
                                                              ENU=Table ID];
                                                   NotBlank=Yes }
    { 3   ;   ;Name                ;Text250       ;CaptionML=[DAN=Navn;
                                                              ENU=Name] }
    { 4   ;   ;Mapping Codeunit    ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DAN=Koblings-codeunit;
                                                              ENU=Mapping Codeunit] }
    { 6   ;   ;Data Exch. No. Field ID;Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=[DAN=Felt-id for dataudvekslingsnr.;
                                                              ENU=Data Exch. No. Field ID];
                                                   Description=The ID of the field in the target table that contains the Data Exchange No.. }
    { 7   ;   ;Data Exch. Line Field ID;Integer   ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=[DAN=Felt-id for dataudvekslingslinje;
                                                              ENU=Data Exch. Line Field ID];
                                                   Description=The ID of the field in the target table that contains the Data Exchange Line No.. }
    { 8   ;   ;Data Exch. Line Def Code;Code20    ;TableRelation="Data Exch. Line Def".Code WHERE (Data Exch. Def Code=FIELD(Data Exch. Def Code));
                                                   CaptionML=[DAN=Definitionskode for dataudvekslingslinje;
                                                              ENU=Data Exch. Line Def Code];
                                                   NotBlank=Yes }
    { 9   ;   ;Pre-Mapping Codeunit;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DAN=Codeunit til førtilknytning;
                                                              ENU=Pre-Mapping Codeunit] }
    { 10  ;   ;Post-Mapping Codeunit;Integer      ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DAN=Codeunit til eftertilknytning;
                                                              ENU=Post-Mapping Codeunit] }
    { 20  ;   ;Use as Intermediate Table;Boolean  ;CaptionML=[DAN=Brug som midlertidig tabel;
                                                              ENU=Use as Intermediate Table] }
  }
  KEYS
  {
    {    ;Data Exch. Def Code,Data Exch. Line Def Code,Table ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DataExchFieldMapping@1000 : Record 1225;
      RecordNameFormatTok@1003 : TextConst 'DAN=%1 til %2;ENU=%1 to %2';
      RenameErr@1004 : TextConst 'DAN=Du kan ikke omdøbe posten, hvis der findes én eller flere feltkoblingslinjer.;ENU=You cannot rename the record if one or more field mapping lines exist.';

    PROCEDURE InsertRec@1(DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1006 : Code[20];TableId@1003 : Integer;Name@1001 : Text[250];MappingCodeunit@1004 : Integer;DataExchNoFieldId@1000 : Integer;DataExchLineFieldId@1005 : Integer);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      VALIDATE("Table ID",TableId);
      VALIDATE(Name,Name);
      VALIDATE("Mapping Codeunit",MappingCodeunit);
      VALIDATE("Data Exch. No. Field ID",DataExchNoFieldId);
      VALIDATE("Data Exch. Line Field ID",DataExchLineFieldId);
      INSERT;
    END;

    PROCEDURE InsertRecForExport@2(DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1006 : Code[20];TableId@1003 : Integer;Name@1001 : Text[250];ProcessingCodeunit@1004 : Integer);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      VALIDATE("Table ID",TableId);
      VALIDATE(Name,Name);
      VALIDATE("Mapping Codeunit",ProcessingCodeunit);
      INSERT;
    END;

    PROCEDURE InsertRecForImport@3(DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1006 : Code[20];TableId@1003 : Integer;Name@1001 : Text[250];DataExchNoFieldId@1000 : Integer;DataExchLineFieldId@1005 : Integer);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      VALIDATE("Table ID",TableId);
      VALIDATE(Name,Name);
      VALIDATE("Data Exch. No. Field ID",DataExchNoFieldId);
      VALIDATE("Data Exch. Line Field ID",DataExchLineFieldId);
      INSERT;
    END;

    PROCEDURE CreateDataExchMapping@8(TableID@1000 : Integer;CodeunitID@1001 : Integer;DataExchNoFieldID@1002 : Integer;DataExchLineFieldID@1003 : Integer);
    BEGIN
      InsertRec("Data Exch. Def Code","Data Exch. Line Def Code",TableID,
        CreateName(TableID,"Data Exch. Def Code"),CodeunitID,DataExchNoFieldID,DataExchLineFieldID);
    END;

    LOCAL PROCEDURE CreateName@5(TableID@1000 : Integer;Code@1002 : Code[20]) : Text[250];
    VAR
      recRef@1001 : RecordRef;
    BEGIN
      recRef.OPEN(TableID);
      EXIT(STRSUBSTNO(RecordNameFormatTok,Code,recRef.CAPTION));
    END;

    LOCAL PROCEDURE HasFieldMappings@6() : Boolean;
    VAR
      DataExchFieldMapping@1000 : Record 1225;
    BEGIN
      DataExchFieldMapping.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
      DataExchFieldMapping.SETRANGE("Data Exch. Line Def Code","Data Exch. Line Def Code");
      DataExchFieldMapping.SETRANGE("Table ID",xRec."Table ID");
      DataExchFieldMapping.SETFILTER("Column No.",'<>%1',0);
      EXIT(NOT DataExchFieldMapping.ISEMPTY);
    END;

    PROCEDURE PositivePayUpdateCodeunits@4() : Boolean;
    VAR
      DataExchDef@1000 : Record 1222;
      DataExchLineDef@1001 : Record 1227;
    BEGIN
      DataExchDef.SETRANGE(Code,"Data Exch. Def Code");
      IF DataExchDef.FINDFIRST THEN
        IF DataExchDef.Type = DataExchDef.Type::"Positive Pay Export" THEN BEGIN
          DataExchLineDef.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
          DataExchLineDef.SETRANGE(Code,"Data Exch. Line Def Code");
          IF DataExchLineDef.FINDFIRST THEN BEGIN
            CASE DataExchLineDef."Line Type" OF
              DataExchLineDef."Line Type"::Header:
                BEGIN
                  "Pre-Mapping Codeunit" := CODEUNIT::"Exp. Pre-Mapping Head Pos. Pay";
                  "Mapping Codeunit" := CODEUNIT::"Exp. Mapping Head Pos. Pay";
                END;
              DataExchLineDef."Line Type"::Detail:
                BEGIN
                  "Pre-Mapping Codeunit" := CODEUNIT::"Exp. Pre-Mapping Det Pos. Pay";
                  "Mapping Codeunit" := CODEUNIT::"Exp. Mapping Det Pos. Pay";
                END;
              DataExchLineDef."Line Type"::Footer:
                BEGIN
                  "Pre-Mapping Codeunit" := CODEUNIT::"Exp. Pre-Mapping Foot Pos. Pay";
                  "Mapping Codeunit" := CODEUNIT::"Exp. Mapping Foot Pos. Pay";
                END;
            END;
            EXIT(TRUE);
          END;
        END;

      IF DataExchDef.Type <> DataExchDef.Type::"Positive Pay Export" THEN BEGIN
        "Pre-Mapping Codeunit" := 0;
        "Mapping Codeunit" := 0;
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

