OBJECT Codeunit 111 Vend. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=29-06-17;
    Time=12:00:00;
    Version List=NAVW19.00.00.48822;
  }
  PROPERTIES
  {
    Permissions=TableData 25=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      VendEntryApplID@1000 : Code[50];

    PROCEDURE SetApplId@1(VAR VendLedgEntry@1000 : Record 25;ApplyingVendLedgEntry@1001 : Record 25;AppliesToID@1005 : Code[50]);
    VAR
      TempVendLedgEntry@1003 : TEMPORARY Record 25;
      VendLedgEntryToUpdate@1002 : Record 25;
    BEGIN
      VendLedgEntry.LOCKTABLE;
      IF VendLedgEntry.FINDSET THEN BEGIN
        // Make Applies-to ID
        IF VendLedgEntry."Applies-to ID" <> '' THEN
          VendEntryApplID := ''
        ELSE BEGIN
          VendEntryApplID := AppliesToID;
          IF VendEntryApplID = '' THEN BEGIN
            VendEntryApplID := USERID;
            IF VendEntryApplID = '' THEN
              VendEntryApplID := '***';
          END;
        END;
        REPEAT
          TempVendLedgEntry := VendLedgEntry;
          TempVendLedgEntry.INSERT;
        UNTIL VendLedgEntry.NEXT = 0;
      END;

      IF TempVendLedgEntry.FINDSET THEN
        REPEAT
          VendLedgEntryToUpdate.COPY(TempVendLedgEntry);
          VendLedgEntryToUpdate.TESTFIELD(Open,TRUE);
          VendLedgEntryToUpdate."Applies-to ID" := VendEntryApplID;
          IF VendLedgEntryToUpdate."Applies-to ID" = '' THEN BEGIN
            VendLedgEntryToUpdate."Accepted Pmt. Disc. Tolerance" := FALSE;
            VendLedgEntryToUpdate."Accepted Payment Tolerance" := 0;
          END;

          IF ((VendLedgEntryToUpdate."Amount to Apply" <> 0) AND (VendEntryApplID = '')) OR
             (VendEntryApplID = '')
          THEN
            VendLedgEntryToUpdate."Amount to Apply" := 0
          ELSE
            IF VendLedgEntryToUpdate."Amount to Apply" = 0 THEN BEGIN
              VendLedgEntryToUpdate.CALCFIELDS("Remaining Amount");
              IF VendLedgEntryToUpdate."Remaining Amount" <> 0 THEN
                VendLedgEntryToUpdate."Amount to Apply" := VendLedgEntryToUpdate."Remaining Amount";
            END;

          IF VendLedgEntryToUpdate."Entry No." = ApplyingVendLedgEntry."Entry No." THEN
            VendLedgEntryToUpdate."Applying Entry" := ApplyingVendLedgEntry."Applying Entry";
          VendLedgEntryToUpdate.MODIFY;
        UNTIL TempVendLedgEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

