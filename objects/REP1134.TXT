OBJECT Report 1134 Copy Cost Budget
{
  OBJECT-PROPERTIES
  {
    Date=09-09-14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Kopier omkostningsbudget;
               ENU=Copy Cost Budget];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NoOfCopies = 0 THEN
                     NoOfCopies := 1;
                   IF Factor = 0 THEN
                     Factor := 1;
                 END;

    OnPreReport=BEGIN
                  EVALUATE(DateFormula,DateChangeFormula);
                END;

  }
  DATASET
  {
    { 3233;    ;DataItem;                    ;
               DataItemTable=Table1109;
               DataItemTableView=SORTING(Budget Name,Cost Type No.,Date);
               OnPreDataItem=BEGIN
                               IF Factor = 0 THEN
                                 ERROR(Text000);

                               IF NoOfCopies < 1 THEN
                                 ERROR(Text001);

                               IF (NoOfCopies > 1) AND (DateChangeFormula = '') THEN
                                 ERROR(Text002);

                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text008);

                               IF CostBudgetEntryReqForm."Budget Name" = '' THEN
                                 ERROR(Text009);

                               IF CostBudgetEntryReqForm."Budget Name" <> '' THEN
                                 CostBudgetNameTarget.GET(CostBudgetEntryReqForm."Budget Name")
                               ELSE
                                 CostBudgetNameTarget.GET(GETFILTER("Budget Name"));

                               IF NOT CONFIRM(
                                    Text004,FALSE,GETFILTER("Budget Name"),CostBudgetNameTarget.Name,Factor,NoOfCopies,GETFILTER(Date),DateChangeFormula)
                               THEN
                                 ERROR('');

                               LOCKTABLE;

                               IF CostBudgetEntryTarget.FINDLAST THEN
                                 LastEntryNo := CostBudgetEntryTarget."Entry No.";

                               NextEntryNo := LastEntryNo + 1;

                               Window.OPEN(Text005);

                               Window.UPDATE(1,COUNT);
                               Window.UPDATE(2,NoOfCopies);
                             END;

               OnAfterGetRecord=VAR
                                  CostAccMgt@1000 : Codeunit 1100;
                                BEGIN
                                  IF "Entry No." > LastEntryNo THEN
                                    CurrReport.BREAK;

                                  CostBudgetEntryTarget := "Cost Budget Entry";

                                  CostBudgetEntryTarget.Description := STRSUBSTNO(Text006,GETFILTER("Budget Name"));
                                  CostBudgetEntryTarget."Budget Name" := CostBudgetEntryReqForm."Budget Name";

                                  IF CostBudgetEntryReqForm."Cost Type No." <> '' THEN
                                    CostBudgetEntryTarget."Cost Type No." := CostBudgetEntryReqForm."Cost Type No.";
                                  IF CostBudgetEntryReqForm."Cost Center Code" <> '' THEN
                                    CostBudgetEntryTarget."Cost Center Code" := CostBudgetEntryReqForm."Cost Center Code";
                                  IF CostBudgetEntryReqForm."Cost Object Code" <> '' THEN
                                    CostBudgetEntryTarget."Cost Object Code" := CostBudgetEntryReqForm."Cost Object Code";

                                  CostBudgetEntryTarget.Amount := ROUND(Amount * Factor,0.01);
                                  CostBudgetEntryTarget.Allocated := FALSE;

                                  FOR i := 1 TO NoOfCopies DO BEGIN
                                    CostBudgetEntryTarget."Entry No." := NextEntryNo;
                                    NextEntryNo := NextEntryNo + 1;
                                    IF DateChangeFormula <> '' THEN
                                      CostBudgetEntryTarget.Date := CALCDATE(DateFormula,CostBudgetEntryTarget.Date);
                                    CostBudgetEntryTarget.INSERT;
                                    NoInserted := NoInserted + 1;

                                    IF CostBudgetRegNo = 0 THEN
                                      CostBudgetRegNo :=
                                        CostAccMgt.InsertCostBudgetRegister(
                                          CostBudgetEntryTarget."Entry No.",CostBudgetEntryTarget."Budget Name",CostBudgetEntryTarget.Amount)
                                    ELSE
                                      CostAccMgt.UpdateCostBudgetRegister(
                                        CostBudgetRegNo,CostBudgetEntryTarget."Entry No.",CostBudgetEntryTarget.Amount);

                                    IF (NoInserted MOD 100) = 0 THEN
                                      Window.UPDATE(3,NoInserted);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF NoInserted = 0 THEN BEGIN
                                  MESSAGE(Text010);
                                  ERROR('');
                                END;

                                IF NOT CONFIRM(Text007,TRUE,NoInserted,CostBudgetNameTarget.Name) THEN
                                  ERROR('');
                              END;

               ReqFilterFields=Budget Name,Cost Type No.,Cost Center Code,Cost Object Code,Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               CostBudgetEntryReqForm.INIT;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 21  ;2   ;Group     ;
                  CaptionML=[DAN=Kopier til...;
                             ENU=Copy to...] }

      { 1   ;3   ;Field     ;
                  Name=Budget Name;
                  Lookup=Yes;
                  CaptionML=[DAN=Budgetnavn;
                             ENU=Budget Name];
                  SourceExpr=CostBudgetEntryReqForm."Budget Name";
                  TableRelation="Cost Budget Name" }

      { 2   ;3   ;Field     ;
                  Name=Cost Type No.;
                  Lookup=Yes;
                  CaptionML=[DAN=Omkostningstypenr.;
                             ENU=Cost Type No.];
                  SourceExpr=CostBudgetEntryReqForm."Cost Type No.";
                  TableRelation="Cost Type" }

      { 4   ;3   ;Field     ;
                  Name=Cost Center Code;
                  Lookup=Yes;
                  CaptionML=[DAN=Omkostningsstedskode;
                             ENU=Cost Center Code];
                  SourceExpr=CostBudgetEntryReqForm."Cost Center Code";
                  TableRelation="Cost Center" }

      { 5   ;3   ;Field     ;
                  Name=Cost Object Code;
                  Lookup=Yes;
                  CaptionML=[DAN=Omkostningsemnekode;
                             ENU=Cost Object Code];
                  SourceExpr=CostBudgetEntryReqForm."Cost Object Code";
                  TableRelation="Cost Object" }

      { 3   ;2   ;Field     ;
                  Name=Amount multiplication factor;
                  CaptionML=[DAN=Beløbsmultiplikationsfaktor;
                             ENU=Amount multiplication factor];
                  SourceExpr=Factor }

      { 8   ;2   ;Field     ;
                  Name=No. of Copies;
                  CaptionML=[DAN=Antal kopier;
                             ENU=No. of Copies];
                  SourceExpr=NoOfCopies }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=[DAN=Flyt datoer med;
                             ENU=Date Change Formula];
                  DateFormula=Yes;
                  SourceExpr=DateChangeFormula }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostBudgetEntryReqForm@1000 : Record 1109;
      CostBudgetEntryTarget@1001 : Record 1109;
      CostBudgetNameTarget@1002 : Record 1110;
      Window@1003 : Dialog;
      DateChangeFormula@1004 : Code[10];
      DateFormula@1005 : DateFormula;
      LastEntryNo@1006 : Integer;
      NextEntryNo@1007 : Integer;
      NoOfCopies@1008 : Integer;
      Factor@1009 : Decimal;
      i@1010 : Integer;
      NoInserted@1011 : Integer;
      CostBudgetRegNo@1012 : Integer;
      Text000@1013 : TextConst 'DAN=Multiplikationsfaktoren må ikke være 0.;ENU=The multiplication factor must not be 0.';
      Text001@1014 : TextConst 'DAN=Antal kopier skal mindst være 1.;ENU=Number of copies must be at least 1.';
      Text002@1015 : TextConst 'DAN=Hvis der oprettes mere end en kopi, skal der defineres en formel for datoændring.;ENU=If more than one copy is created, a formula for date change must be defined.';
      Text004@1017 : TextConst '@@@="%3=multiplication factor (decimal);%4=No of copies (integer)";DAN=Budgettet %1 kopieres til budgettet %2. Budgetbeløbene multipliceres med faktoren %3. \%4 kopier oprettes, og datoen fra intervallet %5 stiger med %6.\\Vil du kopiere budgettet?;ENU=Budget %1 will be copied to Budget %2. The budget amounts will be multiplied by a factor of %3. \%4 copies will be created and the date from range %5 will be incremented by %6.\\Do you want do copy the budget?';
      Text005@1018 : TextConst 'DAN=Kopierer budgetposter\Antal poster #1#####\Antal kopier  #2#####\Kopieret        #3#####;ENU=Copying budget entries\No of entries #1#####\No of copies  #2#####\Copied        #3#####';
      Text006@1019 : TextConst '@@@=%1 - Budget Name.;DAN=Kopi af omkostningsbudget %1;ENU=Copy of cost budget %1';
      Text007@1020 : TextConst 'DAN=%1 poster er genereret i budgettet %2.\\Vil du kopiere posterne?;ENU=%1 entries generated in budget %2.\\Do you want to copy entries?';
      Text008@1021 : TextConst 'DAN=Definer navnet på kildebudgettet.;ENU=Define name of source budget.';
      Text009@1022 : TextConst 'DAN=Definer navnet på målbudgettet.;ENU=Define name of target budget.';
      Text010@1023 : TextConst 'DAN=Ingen poster blev kopieret.;ENU=No entries were copied.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

