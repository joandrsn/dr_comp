OBJECT Table 474 Job Queue Log Entry
{
  OBJECT-PROPERTIES
  {
    Date=22-02-18;
    Time=12:00:00;
    Version List=NAVW19.00.00.50087;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Logpost for opgavekø;
               ENU=Job Queue Log Entry];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[DAN=Løbenr.;
                                                              ENU=Entry No.] }
    { 2   ;   ;ID                  ;GUID          ;CaptionML=[DAN=Id;
                                                              ENU=ID] }
    { 3   ;   ;User ID             ;Text65        ;TableRelation=User."User Name";
                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[DAN=Bruger-id;
                                                              ENU=User ID] }
    { 4   ;   ;Start Date/Time     ;DateTime      ;CaptionML=[DAN=Startdato/-tidspunkt;
                                                              ENU=Start Date/Time] }
    { 5   ;   ;End Date/Time       ;DateTime      ;CaptionML=[DAN=Slutdato/-tidspunkt;
                                                              ENU=End Date/Time] }
    { 6   ;   ;Object Type to Run  ;Option        ;CaptionML=[DAN=Objekttype, der skal aktiveres;
                                                              ENU=Object Type to Run];
                                                   OptionCaptionML=[DAN=,,,Rapport,,Codeunit;
                                                                    ENU=,,,Report,,Codeunit];
                                                   OptionString=,,,Report,,Codeunit }
    { 7   ;   ;Object ID to Run    ;Integer       ;CaptionML=[DAN=Objekt-id, der skal aktiveres;
                                                              ENU=Object ID to Run] }
    { 8   ;   ;Object Caption to Run;Text250      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type to Run),
                                                                                                                Object ID=FIELD(Object ID to Run)));
                                                   CaptionML=[DAN=Objektoverskrift, der skal køres;
                                                              ENU=Object Caption to Run] }
    { 9   ;   ;Status              ;Option        ;CaptionML=[DAN=Status;
                                                              ENU=Status];
                                                   OptionCaptionML=[DAN=Fuldført,Igangsat,Fejl;
                                                                    ENU=Success,In Process,Error];
                                                   OptionString=Success,In Process,Error }
    { 10  ;   ;Description         ;Text50        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 11  ;   ;Error Message       ;Text250       ;CaptionML=[DAN=Fejlmeddelelse;
                                                              ENU=Error Message] }
    { 12  ;   ;Error Message 2     ;Text250       ;CaptionML=[DAN=Fejlmeddelelse 2;
                                                              ENU=Error Message 2] }
    { 13  ;   ;Error Message 3     ;Text250       ;CaptionML=[DAN=Fejlmeddelelse 3;
                                                              ENU=Error Message 3] }
    { 14  ;   ;Error Message 4     ;Text250       ;CaptionML=[DAN=Fejlmeddelelse 4;
                                                              ENU=Error Message 4] }
    { 15  ;   ;Job Queue Code      ;Code10        ;TableRelation="Job Queue";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Opgavekøkode;
                                                              ENU=Job Queue Code] }
    { 16  ;   ;Processed by User ID;Text65        ;DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[DAN=Behandlet af bruger-id;
                                                              ENU=Processed by User ID] }
    { 17  ;   ;Job Queue Category Code;Code10     ;TableRelation="Job Queue Category";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Opgavekøkategorikode;
                                                              ENU=Job Queue Category Code] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;ID                                       }
    {    ;Start Date/Time,ID                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DAN=Der er ingen fejlmeddelelse.;ENU=There is no error message.';
      Text002@1000 : TextConst 'DAN=Er du sikker på, at du vil slette opgavekølogposterne?;ENU=Are you sure that you want to delete job queue log entries?';
      Text003@1002 : TextConst 'DAN=Markeret som fejl af %1.;ENU=Marked as Error by %1.';
      Text004@1003 : TextConst 'DAN=Det er kun poster med statussen Igangværende, der kan markeres som fejl.;ENU=Only entries with status In Progress can be marked as Error.';

    PROCEDURE GetErrorMessage@1() : Text;
    BEGIN
      EXIT("Error Message" + "Error Message 2" + "Error Message 3" + "Error Message 4");
    END;

    PROCEDURE SetErrorMessage@2(ErrorText@1000 : Text);
    BEGIN
      "Error Message 2" := '';
      "Error Message 3" := '';
      "Error Message 4" := '';
      "Error Message" := COPYSTR(ErrorText,1,250);
      IF STRLEN(ErrorText) > 250 THEN
        "Error Message 2" := COPYSTR(ErrorText,251,250);
      IF STRLEN(ErrorText) > 500 THEN
        "Error Message 3" := COPYSTR(ErrorText,501,250);
      IF STRLEN(ErrorText) > 750 THEN
        "Error Message 4" := COPYSTR(ErrorText,751,250);
    END;

    PROCEDURE DeleteEntries@3(DaysOld@1000 : Integer);
    BEGIN
      IF NOT CONFIRM(Text002) THEN
        EXIT;
      SETFILTER(Status,'<>%1',Status::"In Process");
      SETFILTER("End Date/Time",'<=%1',CREATEDATETIME(TODAY - DaysOld,TIME));
      DELETEALL;
      SETRANGE("End Date/Time");
      SETRANGE(Status);
    END;

    PROCEDURE ShowErrorMessage@8();
    VAR
      e@1000 : Text;
    BEGIN
      e := GetErrorMessage;
      IF e = '' THEN
        e := Text001;
      MESSAGE(e);
    END;

    PROCEDURE MarkAsError@4();
    VAR
      JobQueueEntry@1003 : Record 472;
      ErrorMessage@1002 : Text[1000];
    BEGIN
      IF Status <> Status::"In Process" THEN
        ERROR(Text004);

      ErrorMessage := STRSUBSTNO(Text003,USERID);

      IF JobQueueEntry.GET(ID) THEN
        JobQueueEntry.SetError(ErrorMessage);

      Status := Status::Error;
      SetErrorMessage(ErrorMessage);
      MODIFY;
    END;

    BEGIN
    END.
  }
}

