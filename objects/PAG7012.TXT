OBJECT Page 7012 Purchase Prices
{
  OBJECT-PROPERTIES
  {
    Date=30-05-17;
    Time=12:00:00;
    Version List=NAVW19.00.00.48628;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Købspriser;
               ENU=Purchase Prices];
    SourceTable=Table7012;
    DataCaptionExpr=GetCaption;
    DelayedInsert=Yes;
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 GetRecFilters;
                 SetRecFilters;
                 IsLookupMode := CurrPage.LOOKUPMODE;
               END;

    ActionList=ACTIONS
    {
      { 3       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 5       ;1   ;Action    ;
                      Name=CopyPrices;
                      CaptionML=[DAN=Kopiér priser;
                                 ENU=Copy Prices];
                      ToolTipML=[DAN=Vælg priser, og tryk på OK for at kopiere dem til Kreditornr.;
                                 ENU=Select prices and press OK to copy them to Vendor No.];
                      Visible=NOT IsLookupMode;
                      Image=Copy;
                      OnAction=BEGIN
                                 CopyPrices;
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 25  ;1   ;Group     ;
                CaptionML=[DAN=Generelt;
                           ENU=General] }

    { 26  ;2   ;Field     ;
                Name=VendNoFilterCtrl;
                CaptionML=[DAN=Leverandørnr.filter;
                           ENU=Vendor No. Filter];
                SourceExpr=VendNoFilter;
                OnValidate=BEGIN
                             VendNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           VendList@1005 : Page 27;
                         BEGIN
                           VendList.LOOKUPMODE := TRUE;
                           IF VendList.RUNMODAL = ACTION::LookupOK THEN
                             Text := VendList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 32  ;2   ;Field     ;
                Name=ItemNoFIlterCtrl;
                CaptionML=[DAN=Varenummerfilter;
                           ENU=Item No. Filter];
                SourceExpr=ItemNoFilter;
                OnValidate=BEGIN
                             ItemNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1002 : Page 31;
                         BEGIN
                           ItemList.LOOKUPMODE := TRUE;
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN
                             Text := ItemList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 30  ;2   ;Field     ;
                CaptionML=[DAN=Startdatofilter;
                           ENU=Starting Date Filter];
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             ApplicationMgt@1030 : Codeunit 1;
                           BEGIN
                             IF ApplicationMgt.MakeDateFilter(StartingDateFilter) = 0 THEN;
                             StartingDateFilterOnAfterValid;
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                SourceExpr="Vendor No." }

    { 2   ;2   ;Field     ;
                SourceExpr="Item No." }

    { 23  ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Currency Code";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Unit of Measure Code" }

    { 12  ;2   ;Field     ;
                SourceExpr="Minimum Quantity" }

    { 10  ;2   ;Field     ;
                SourceExpr="Direct Unit Cost" }

    { 8   ;2   ;Field     ;
                SourceExpr="Starting Date" }

    { 14  ;2   ;Field     ;
                SourceExpr="Ending Date" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      VendNoFilter@1000 : Code[30];
      ItemNoFilter@1001 : Code[30];
      StartingDateFilter@1002 : Text[30];
      Vend@1003 : Record 23;
      NoDataWithinFilterErr@1004 : TextConst '@@@=%1: Field(Code), %2: GetFilter(Code);DAN=Der er ikke %1 i filteret %2.;ENU=There is no %1 within the filter %2.';
      IsLookupMode@1005 : Boolean;
      MultipleVendorsSelectedErr@1006 : TextConst 'DAN=Mere end én kreditor bruger købspriserne. Hvis du vil kopiere priser, må feltet Leverandørnr.filter kun indeholde én kreditor.;ENU=More than one vendor uses these purchase prices. To copy prices, the Vendor No. Filter field must contain one vendor only.';

    LOCAL PROCEDURE GetRecFilters@2();
    BEGIN
      IF GETFILTERS <> '' THEN BEGIN
        VendNoFilter := GETFILTER("Vendor No.");
        ItemNoFilter := GETFILTER("Item No.");
        EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
      END;
    END;

    PROCEDURE SetRecFilters@1();
    BEGIN
      IF VendNoFilter <> '' THEN
        SETFILTER("Vendor No.",VendNoFilter)
      ELSE
        SETRANGE("Vendor No.");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      IF ItemNoFilter <> '' THEN BEGIN
        SETFILTER("Item No.",ItemNoFilter);
      END ELSE
        SETRANGE("Item No.");

      CheckFilters(DATABASE::Vendor,VendNoFilter);
      CheckFilters(DATABASE::Item,ItemNoFilter);

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      SourceTableName@1002 : Text[100];
      Description@1001 : Text[250];
    BEGIN
      GetRecFilters;

      IF ItemNoFilter <> '' THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,27)
      ELSE
        SourceTableName := '';

      Vend."No." := COPYSTR(VendNoFilter,1,MAXSTRLEN(Vend."No."));
      IF Vend.FIND THEN
        Description := Vend.Name;

      EXIT(STRSUBSTNO('%1 %2 %3 %4 ',VendNoFilter,Description,SourceTableName,ItemNoFilter));
    END;

    LOCAL PROCEDURE VendNoFilterOnAfterValidate@19075608();
    VAR
      Item@1000 : Record 27;
    BEGIN
      IF Item.GET("Item No.") THEN
        CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemNoFilterOnAfterValidate@19009808();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    PROCEDURE CheckFilters@4(TableNo@1000 : Integer;FilterTxt@1001 : Text[250]);
    VAR
      FilterRecordRef@1002 : RecordRef;
      FilterFieldRef@1003 : FieldRef;
    BEGIN
      IF FilterTxt = '' THEN
        EXIT;
      CLEAR(FilterRecordRef);
      CLEAR(FilterFieldRef);
      FilterRecordRef.OPEN(TableNo);
      FilterFieldRef := FilterRecordRef.FIELD(1);
      FilterFieldRef.SETFILTER(FilterTxt);
      IF FilterRecordRef.ISEMPTY THEN
        ERROR(NoDataWithinFilterErr,FilterRecordRef.CAPTION,FilterTxt);
    END;

    LOCAL PROCEDURE CopyPrices@6();
    VAR
      Vendor@1001 : Record 23;
      PurchasePrice@1004 : Record 7012;
      SelectedPurchasePrice@1005 : Record 7012;
      PurchasePrices@1002 : Page 7012;
      CopyToVendorNo@1003 : Code[20];
    BEGIN
      Vendor.SETFILTER("No.",VendNoFilter);
      IF Vendor.COUNT <> 1 THEN
        ERROR(MultipleVendorsSelectedErr);
      CopyToVendorNo := COPYSTR(VendNoFilter,1,MAXSTRLEN(CopyToVendorNo));

      PurchasePrice.SETFILTER("Vendor No.",'<>%1',VendNoFilter);
      PurchasePrices.LOOKUPMODE(TRUE);
      PurchasePrices.SETTABLEVIEW(PurchasePrice);
      IF PurchasePrices.RUNMODAL = ACTION::LookupOK THEN BEGIN
        PurchasePrices.GetSelectionFilter(SelectedPurchasePrice);
        CopyPurchPriceToVendorsPurchPrice(SelectedPurchasePrice,CopyToVendorNo);
      END;
    END;

    PROCEDURE GetSelectionFilter@5(VAR PurchasePrice@1001 : Record 7012);
    BEGIN
      CurrPage.SETSELECTIONFILTER(PurchasePrice);
    END;

    BEGIN
    END.
  }
}

