OBJECT Page 9900 Import Data
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[DAN=Indlæs data;
               ENU=Import Data];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000006;
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    InstructionalTextML=;
    OnInit=BEGIN
             OriginalTenantId := '';
             ContainsApplication := FALSE;
             ContainsApplicationData := FALSE;
             ContainsGlobalData := FALSE;
           END;

    OnAfterGetRecord=BEGIN
                       Selected := SelectedCompany.GET(Name);
                     END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::OK THEN BEGIN
                         IF IncludeApplicationData OR IncludeGlobalData THEN BEGIN
                           IF NOT CONFIRM(OverwriteQst,FALSE) THEN
                             EXIT(FALSE);
                         END;
                         IF IMPORTDATA(
                              FALSE,
                              FileName,
                              IncludeApplicationData,
                              IncludeGlobalData,
                              SelectedCompany)
                         THEN BEGIN
                           MESSAGE(CompletedMsg);
                           EXIT(TRUE)
                         END;
                         EXIT(FALSE);
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[DAN=Generelt;
                           ENU=General];
                GroupType=Group }

    { 12  ;2   ;Field     ;
                Name=FileName;
                Lookup=No;
                DrillDown=No;
                AssistEdit=Yes;
                CaptionML=[DAN=Filnavn;
                           ENU=File Name];
                SourceExpr=FileName;
                Editable=TRUE;
                OnValidate=VAR
                             Companies@1000 : TEMPORARY Record 2000000006;
                           BEGIN
                             IF NOT DATAFILEINFORMATION(
                                  FALSE,
                                  FileName,
                                  Description,
                                  ContainsApplication,
                                  ContainsApplicationData,
                                  ContainsGlobalData,
                                  OriginalTenantId,
                                  ExportDate,
                                  Companies)
                             THEN
                               EXIT;

                             DELETEALL;
                             ContainsCompanies := Companies.FINDSET;
                             IF ContainsCompanies THEN
                               REPEAT
                                 Rec := Companies;
                                 INSERT;
                               UNTIL Companies.NEXT = 0;

                             IncludeApplicationData := FALSE;
                             IncludeGlobalData := FALSE;
                             IncludeAllCompanies := ContainsCompanies;

                             MarkAll;
                           END;

                OnAssistEdit=VAR
                               Companies@1000 : TEMPORARY Record 2000000006;
                             BEGIN
                               IF NOT DATAFILEINFORMATION(
                                    TRUE,
                                    FileName,
                                    Description,
                                    ContainsApplication,
                                    ContainsApplicationData,
                                    ContainsGlobalData,
                                    OriginalTenantId,
                                    ExportDate,
                                    Companies)
                               THEN
                                 EXIT;

                               DELETEALL;
                               ContainsCompanies := Companies.FINDSET;
                               IF ContainsCompanies THEN
                                 REPEAT
                                   Rec := Companies;
                                   INSERT;
                                 UNTIL Companies.NEXT = 0;

                               IncludeApplicationData := FALSE;
                               IncludeGlobalData := FALSE;
                               IncludeAllCompanies := ContainsCompanies;

                               MarkAll;
                             END;
                              }

    { 6   ;2   ;Field     ;
                Name=TenantId;
                CaptionML=[DAN=Lejer-id;
                           ENU=Tenant ID];
                SourceExpr=TENANTID;
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                Name=Description;
                CaptionML=[DAN=Beskrivelse;
                           ENU=Description];
                SourceExpr=Description;
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                Name=ExportDate;
                CaptionML=[DAN=Eksportdato;
                           ENU=Date of Export];
                SourceExpr=ExportDate;
                Editable=FALSE }

    { 13  ;2   ;Group     ;
                CaptionML=[DAN=Indlæs;
                           ENU=Import];
                GroupType=Group }

    { 14  ;3   ;Field     ;
                Name=IncludeAllCompanies;
                CaptionML=[DAN=Alle regnskaber;
                           ENU=All Companies];
                SourceExpr=IncludeAllCompanies;
                Editable=ContainsCompanies;
                OnValidate=BEGIN
                             MarkAll;
                           END;
                            }

    { 11  ;3   ;Field     ;
                Name=IncludeGlobalData;
                CaptionML=[DAN=Globale data;
                           ENU=Global Data];
                SourceExpr=IncludeGlobalData;
                Editable=ContainsGlobalData }

    { 4   ;3   ;Field     ;
                Name=IncludeApplicationData;
                CaptionML=[DAN=Programdata;
                           ENU=Application Data];
                SourceExpr=IncludeApplicationData;
                Editable=ContainsApplicationData }

    { 8   ;2   ;Group     ;
                GroupType=Repeater }

    { 10  ;3   ;Field     ;
                CaptionML=[DAN=Indlæs;
                           ENU=Import];
                SourceExpr=Selected;
                OnValidate=BEGIN
                             IF Selected THEN BEGIN
                               SelectedCompany := Rec;
                               IF SelectedCompany.INSERT THEN;
                             END ELSE BEGIN
                               IncludeAllCompanies := FALSE;
                               IF SelectedCompany.GET(Name) THEN
                                 SelectedCompany.DELETE;
                             END;
                           END;
                            }

    { 9   ;3   ;Field     ;
                Width=30;
                CaptionML=[DAN=Virksomhedsnavn;
                           ENU=Company Name];
                SourceExpr=Name }

  }
  CODE
  {
    VAR
      SelectedCompany@1002 : TEMPORARY Record 2000000006;
      FileName@1007 : Text;
      Description@1008 : Text;
      OriginalTenantId@1009 : Text;
      ExportDate@1012 : DateTime;
      Selected@1001 : Boolean;
      ContainsApplication@1006 : Boolean;
      ContainsApplicationData@1004 : Boolean;
      ContainsGlobalData@1003 : Boolean;
      ContainsCompanies@1005 : Boolean;
      IncludeAllCompanies@1000 : Boolean;
      IncludeApplicationData@1010 : Boolean;
      IncludeGlobalData@1011 : Boolean;
      OverwriteQst@1013 : TextConst 'DAN=Programdata, globale data eller begge typer data overskrives. Er du sikker på, at du vil fortsætte?;ENU=Application data, global data, or both types of data will be overwritten. Are you sure that you want to continue?';
      CompletedMsg@1014 : TextConst 'DAN=Dataene blev indlæst.;ENU=The data was imported successfully.';

    LOCAL PROCEDURE MarkAll@2();
    BEGIN
      SelectedCompany.DELETEALL;

      IF IncludeAllCompanies THEN
        IF FINDSET THEN
          REPEAT
            SelectedCompany := Rec;
            SelectedCompany.INSERT;
          UNTIL NEXT = 0;

      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

