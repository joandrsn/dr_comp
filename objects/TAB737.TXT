OBJECT Table 737 VAT Return Period
{
  OBJECT-PROPERTIES
  {
    Date=25-04-19;
    Time=12:00:00;
    Version List=NAVW19.00.00.51333;
  }
  PROPERTIES
  {
    OnInsert=VAR
               NoSeriesMgt@1001 : Codeunit 396;
             BEGIN
               IF "No." = '' THEN
                 NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series",WORKDATE,"No.","No. Series");
             END;

    OnDelete=VAR
               VATReportHeader@1000 : Record 740;
             BEGIN
               IF VATReportHeader.GET(VATReportHeader."VAT Report Config. Code"::"VAT Return","VAT Return No.") THEN
                 ERROR(DeleteExistingVATRetErr);
             END;

    CaptionML=[DAN=Momsangivelsesperiode;
               ENU=VAT Return Period];
    LookupPageID=Page737;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[DAN=Nr.;
                                                              ENU=No.] }
    { 2   ;   ;No. Series          ;Code10        ;CaptionML=[DAN=Nummerserie;
                                                              ENU=No. Series] }
    { 3   ;   ;Period Key          ;Code10        ;CaptionML=[DAN=Periodenøgle;
                                                              ENU=Period Key] }
    { 4   ;   ;Start Date          ;Date          ;CaptionML=[DAN=Startdato;
                                                              ENU=Start Date] }
    { 5   ;   ;End Date            ;Date          ;CaptionML=[DAN=Slutdato;
                                                              ENU=End Date] }
    { 6   ;   ;Due Date            ;Date          ;CaptionML=[DAN=Forfaldsdato;
                                                              ENU=Due Date] }
    { 7   ;   ;Status              ;Option        ;CaptionML=[DAN=Status;
                                                              ENU=Status];
                                                   OptionCaptionML=[DAN=Åben,Lukket;
                                                                    ENU=Open,Closed];
                                                   OptionString=Open,Closed }
    { 8   ;   ;Received Date       ;Date          ;CaptionML=[DAN=Modtagelsesdato;
                                                              ENU=Received Date] }
    { 20  ;   ;VAT Return No.      ;Code20        ;TableRelation=IF (VAT Return No.=FILTER(<>'')) "VAT Report Header".No. WHERE (VAT Report Config. Code=CONST(VAT Return),
                                                                                                                                 No.=FIELD(VAT Return No.));
                                                   OnLookup=BEGIN
                                                              DrillDownVATReturn;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[DAN=Momsangivelsesnr.;
                                                              ENU=VAT Return No.];
                                                   Editable=No }
    { 21  ;   ;VAT Return Status   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("VAT Report Header".Status WHERE (VAT Report Config. Code=CONST(VAT Return),
                                                                                                        No.=FIELD(VAT Return No.)));
                                                   CaptionML=[DAN=Status for momsangivelse;
                                                              ENU=VAT Return Status];
                                                   OptionCaptionML=[DAN=Åben,Frigivet,Sendt,Accepteret,Lukket,Afvist,Annulleret;
                                                                    ENU=Open,Released,Submitted,Accepted,Closed,Rejected,Canceled];
                                                   OptionString=Open,Released,Submitted,Accepted,Closed,Rejected,Canceled;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeleteExistingVATRetErr@1000 : TextConst 'DAN=Du kan ikke slette en momsangivelsesperiode, der har en tilknyttet momsangivelse.;ENU=You cannot delete a VAT return period that has a linked VAT return.';
      OverdueTxt@1002 : TextConst '@@@="%1 - date; %2 - days count";DAN=Din momsangivelse er overskredet siden %1 (%2 dage);ENU=Your VAT return is overdue since %1 (%2 days)';
      OpenTxt@1001 : TextConst '@@@="%1 - date; %2 - days count";DAN=Din momsangivelse forfalder %1 (om %2 dage);ENU=Your VAT return is due %1 (in %2 days)';
      VATReportSetup@1003 : Record 743;
      VATReportSetupGot@1004 : Boolean;

    LOCAL PROCEDURE GetNoSeriesCode@4() : Code[10];
    VAR
      VATReportSetup@1000 : Record 743;
    BEGIN
      VATReportSetup.GET;
      VATReportSetup.TESTFIELD("VAT Return Period No. Series");
      EXIT(VATReportSetup."VAT Return Period No. Series");
    END;

    PROCEDURE CopyToVATReturn@1(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      TESTFIELD("Start Date");
      TESTFIELD("End Date");
      VATReportHeader."Return Period No." := "No.";
      VATReportHeader."Period Year" := DATE2DMY("End Date",3);
      VATReportHeader."Start Date" := "Start Date";
      VATReportHeader."End Date" := "End Date";
      ParseDatePeriod(VATReportHeader);
    END;

    LOCAL PROCEDURE ParseDatePeriod@2(VAR VATReportHeader@1000 : Record 740);
    VAR
      Date@1001 : Record 2000000007;
    BEGIN
      Date.SETRANGE("Period Start",VATReportHeader."Start Date");
      Date.SETRANGE("Period End",CLOSINGDATE(VATReportHeader."End Date"));
      IF Date.FINDFIRST THEN BEGIN
        CASE Date."Period Type" OF
          Date."Period Type"::Month:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Month;
          Date."Period Type"::Quarter:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Quarter;
          Date."Period Type"::Year:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Year;
        END;
        VATReportHeader."Period No." := Date."Period No.";
      END;
    END;

    PROCEDURE FindVATReturnPeriod@3(VAR VATReturnPeriod@1000 : Record 737;StartDate@1001 : Date;EndDate@1002 : Date) : Boolean;
    BEGIN
      VATReturnPeriod.SETRANGE("Start Date",StartDate);
      VATReturnPeriod.SETRANGE("End Date",EndDate);
      EXIT(VATReturnPeriod.FINDFIRST);
    END;

    PROCEDURE DiffersFromVATReturnPeriod@5(VAR VATReturnPeriod@1000 : Record 737) : Boolean;
    BEGIN
      EXIT(
        ("Due Date" <> VATReturnPeriod."Due Date") OR
        (Status <> VATReturnPeriod.Status) OR
        ("Received Date" <> VATReturnPeriod."Received Date") OR
        ("Period Key" <> VATReturnPeriod."Period Key"));
    END;

    PROCEDURE DrillDownVATReturn@6();
    VAR
      VATReportMgt@1000 : Codeunit 737;
    BEGIN
      IF (Status = Status::Open) OR ("VAT Return No." <> '') THEN
        VATReportMgt.OpenVATReturnCardFromVATPeriod(Rec);
    END;

    LOCAL PROCEDURE GetVATReportSetup@8();
    BEGIN
      IF VATReportSetupGot THEN
        EXIT;

      VATReportSetup.GET;
      VATReportSetupGot := TRUE;
    END;

    PROCEDURE CheckOpenOrOverdue@7() : Text;
    BEGIN
      GetVATReportSetup;
      IF (Status = Status::Open) AND ("Due Date" <> 0D) THEN
        CASE TRUE OF
          // Overdue
          ("Due Date" < WORKDATE):
            EXIT(STRSUBSTNO(OverdueTxt,"Due Date",WORKDATE - "Due Date"));
          // Open
          VATReportSetup.IsPeriodReminderCalculation AND
          ("Due Date" >= WORKDATE) AND
          ("Due Date" <= CALCDATE(VATReportSetup."Period Reminder Calculation",WORKDATE)):
            EXIT(STRSUBSTNO(OpenTxt,"Due Date","Due Date" - WORKDATE));
        END;
    END;

    BEGIN
    END.
  }
}

