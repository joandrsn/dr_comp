OBJECT Page 9831 User Group Members
{
  OBJECT-PROPERTIES
  {
    Date=03-08-16;
    Time=12:00:00;
    Version List=NAVW19.00.00.46621;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Medlemmer af brugergruppe;
               ENU=User Group Members];
    SourceTable=Table9001;
    DelayedInsert=Yes;
    PopulateAllFields=Yes;
    DataCaptionFields=User Group Code,User Group Name;
    PageType=List;
    OnOpenPage=BEGIN
                 SelectedCompany := COMPANYNAME;
                 UpdateCompany;
               END;

    OnAfterGetRecord=BEGIN
                       CALCFIELDS("User Name");
                       UserName := "User Name";
                     END;

    OnNewRecord=BEGIN
                  UserName := '';
                END;

    OnInsertRecord=BEGIN
                     EXIT(NOT ISNULLGUID("User Security ID"));
                   END;

    OnModifyRecord=BEGIN
                     TESTFIELD("User Name");
                   END;

    ActionList=ACTIONS
    {
      { 9       ;    ;ActionContainer;
                      CaptionML=[DAN=Ny;
                                 ENU=New];
                      ActionContainerType=NewDocumentItems }
      { 10      ;1   ;Action    ;
                      Name=AddUsers;
                      CaptionML=[DAN=Tilf√∏j brugere;
                                 ENU=Add Users];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Users;
                      OnAction=BEGIN
                                 AddUsers(Company.Name);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                CaptionML=[DAN=Generelt;
                           ENU=General];
                GroupType=Group }

    { 5   ;2   ;Field     ;
                CaptionML=[DAN=Firmanavn;
                           ENU=Company Name];
                SourceExpr=SelectedCompany;
                TableRelation=Company;
                OnValidate=BEGIN
                             UpdateCompany;
                           END;
                            }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                CaptionML=[DAN=Brugernavn;
                           ENU=User Name];
                SourceExpr=UserName;
                OnValidate=VAR
                             User@1000 : Record 2000000120;
                           BEGIN
                             IF UserName = '' THEN
                               EXIT;
                             User.SETRANGE("User Name",UserName);
                             User.FINDFIRST;
                             INIT;
                             VALIDATE("User Security ID",User."User Security ID");
                             VALIDATE("Company Name",SelectedCompany);
                             CALCFIELDS("User Name");
                             INSERT(TRUE);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           User@1000 : Record 2000000120;
                           Users@1001 : Page 9800;
                         BEGIN
                           IF Text <> '' THEN BEGIN
                             User.SETRANGE("User Name",Text);
                             IF User.FINDFIRST THEN;
                             Users.SETRECORD(User);
                           END;
                           Users.LOOKUPMODE := TRUE;
                           IF Users.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Users.GETRECORD(User);
                             IF GET("User Group Code","User Security ID",SelectedCompany) THEN
                               DELETE(TRUE);
                             INIT;
                             VALIDATE("User Security ID",User."User Security ID");
                             VALIDATE("Company Name",SelectedCompany);
                             CALCFIELDS("User Name");
                             INSERT(TRUE);
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 4   ;2   ;Field     ;
                CaptionML=[DAN=Fulde navn;
                           ENU=Full Name];
                SourceExpr="User Full Name" }

  }
  CODE
  {
    VAR
      Company@1001 : Record 2000000006;
      SelectedCompany@1000 : Text[30];
      UserName@1002 : Code[50];

    LOCAL PROCEDURE UpdateCompany@5();
    BEGIN
      Company.Name := SelectedCompany;
      IF SelectedCompany <> '' THEN BEGIN
        Company.FIND('=<>');
        SelectedCompany := Company.Name;
      END;
      SETRANGE("Company Name",Company.Name);
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

