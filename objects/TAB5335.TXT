OBJECT Table 5335 Integration Table Mapping
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Integrationstabelkobling;
               ENU=Integration Table Mapping];
    LookupPageID=Page5335;
    DrillDownPageID=Page5335;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code20        ;CaptionML=[DAN=Navn;
                                                              ENU=Name] }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[DAN=Tabel-id;
                                                              ENU=Table ID] }
    { 3   ;   ;Integration Table ID;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[DAN=Integrationstabel-id;
                                                              ENU=Integration Table ID] }
    { 4   ;   ;Synch. Codeunit ID  ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DAN=Synkr. codeunit-id;
                                                              ENU=Synch. Codeunit ID] }
    { 5   ;   ;Integration Table UID Fld. No.;Integer;
                                                   CaptionML=[DAN=UID-feltnr. for integrationstabel;
                                                              ENU=Integration Table UID Fld. No.];
                                                   Description=Integration Table Unique Identifier Field No. }
    { 6   ;   ;Int. Tbl. Modified On Fld. No.;Integer;
                                                   CaptionML=[DAN=Int.tbl. rettet på feltnr.;
                                                              ENU=Int. Tbl. Modified On Fld. No.];
                                                   Description=Integration Table Modified On Field No. }
    { 8   ;   ;Table Config Template Code;Code10  ;TableRelation="Config. Template Header".Code WHERE (Table ID=FIELD(Table ID));
                                                   CaptionML=[DAN=Skabelonkode til tabelkonfiguration;
                                                              ENU=Table Config Template Code] }
    { 9   ;   ;Int. Tbl. Config Template Code;Code10;
                                                   TableRelation="Config. Template Header".Code WHERE (Table ID=FIELD(Integration Table ID));
                                                   CaptionML=[DAN=Skabelonkode til int.tabelkonfiguration;
                                                              ENU=Int. Tbl. Config Template Code] }
    { 10  ;   ;Direction           ;Option        ;CaptionML=[DAN=Retning;
                                                              ENU=Direction];
                                                   OptionCaptionML=[DAN=Begge retninger,TilIntegrationstabel,FraIntegrationstabel;
                                                                    ENU=Bidirectional,ToIntegrationTable,FromIntegrationTable];
                                                   OptionString=Bidirectional,ToIntegrationTable,FromIntegrationTable }
    { 11  ;   ;Int. Tbl. Caption Prefix;Text30    ;CaptionML=[DAN=Int. Tab.tekstpræfiks;
                                                              ENU=Int. Tbl. Caption Prefix] }
    { 12  ;   ;Synch. Int. Tbl. Mod. On Fltr.;DateTime;
                                                   CaptionML=[DAN=Synkr. Int.tbl. rettet på feltnr.;
                                                              ENU=Synch. Int. Tbl. Mod. On Fltr.];
                                                   Description=Scheduled synch. Integration Table Modified On Filter }
    { 13  ;   ;Synch. Modified On Filter;DateTime ;CaptionML=[DAN=Synkr. rettet på filter;
                                                              ENU=Synch. Modified On Filter];
                                                   Description=Scheduled synch. Modified On Filter }
    { 14  ;   ;Table Filter        ;BLOB          ;CaptionML=[DAN=Tabelfilter;
                                                              ENU=Table Filter] }
    { 15  ;   ;Integration Table Filter;BLOB      ;CaptionML=[DAN=Integrationstabelfilter;
                                                              ENU=Integration Table Filter] }
    { 16  ;   ;Synch. Only Coupled Records;Boolean;InitValue=Yes;
                                                   CaptionML=[DAN=Synkr. kun sammenkædede poster;
                                                              ENU=Synch. Only Coupled Records] }
    { 17  ;   ;Parent Name         ;Code20        ;CaptionML=[DAN=Overordnet navn;
                                                              ENU=Parent Name] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetExtendedIntegrationTableCaption@1() : Text;
    VAR
      TableMetadata@1000 : Record 2000000136;
    BEGIN
      IF NOT TableMetadata.GET("Integration Table ID") THEN
        EXIT('');
      IF "Int. Tbl. Caption Prefix" <> '' THEN
        EXIT(STRSUBSTNO('%1 %2',"Int. Tbl. Caption Prefix",TableMetadata.Caption));
      EXIT(TableMetadata.Caption);
    END;

    PROCEDURE SetTableFilter@5(Filter@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Table Filter".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(Filter);
    END;

    PROCEDURE GetTableFilter@6() Value : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Table Filter");
      "Table Filter".CREATEINSTREAM(InStream);
      InStream.READ(Value);
    END;

    PROCEDURE SetIntegrationTableFilter@7(Filter@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Integration Table Filter".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(Filter);
    END;

    PROCEDURE GetIntegrationTableFilter@8() Value : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      CALCFIELDS("Integration Table Filter");
      "Integration Table Filter".CREATEINSTREAM(InStream);
      InStream.READ(Value);
    END;

    PROCEDURE GetAllowedSyncDirection@93(TableNumber@1000 : Integer) : Integer;
    VAR
      IntegrationTableMapping@1001 : Record 5335;
    BEGIN
      IntegrationTableMapping.SETRANGE("Table ID",TableNumber);
      IF IntegrationTableMapping.FINDFIRST THEN
        EXIT(IntegrationTableMapping.Direction);

      IntegrationTableMapping.RESET;
      IntegrationTableMapping.SETRANGE("Integration Table ID",TableNumber);
      IF IntegrationTableMapping.FINDFIRST THEN
        EXIT(IntegrationTableMapping.Direction);

      EXIT(IntegrationTableMapping.Direction::Bidirectional);
    END;

    PROCEDURE SynchronizeNow@2(ResetLastSynchModifiedOnDateTime@1000 : Boolean);
    BEGIN
      IF ResetLastSynchModifiedOnDateTime THEN BEGIN
        CLEAR("Synch. Modified On Filter");
        CLEAR("Synch. Int. Tbl. Mod. On Fltr.");
        MODIFY;
        COMMIT;
      END;
      CODEUNIT.RUN("Synch. Codeunit ID",Rec);
    END;

    PROCEDURE GetRecordRef@3(ID@1000 : Variant;VAR IntegrationRecordRef@1001 : RecordRef) : Boolean;
    VAR
      IDFieldRef@1002 : FieldRef;
      RecordID@1003 : RecordID;
    BEGIN
      IntegrationRecordRef.CLOSE;
      IF ID.ISGUID THEN BEGIN
        // Integratio
        IntegrationRecordRef.OPEN("Integration Table ID");
        IDFieldRef := IntegrationRecordRef.FIELD("Integration Table UID Fld. No.");
        IDFieldRef.SETFILTER(ID);
        IF IntegrationRecordRef.FINDFIRST THEN
          EXIT(TRUE);
      END ELSE
        IF ID.ISRECORDID THEN BEGIN
          RecordID := ID;
          IF RecordID.TABLENO <> "Table ID" THEN
            EXIT(FALSE);
          IntegrationRecordRef.GET(ID);
          EXIT(TRUE);
        END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

