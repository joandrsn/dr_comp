OBJECT Report 6655 Batch Post Sales Return Orders
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Massebogfør salgsreturvareordrer;
               ENU=Batch Post Sales Return Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Return Order));
               ReqFilterHeadingML=[DAN=Salgsreturvareordre;
                                   ENU=Sales Return Order];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  IF ApprovalsMgmt.IsSalesApprovalsWorkflowEnabled("Sales Header") AND (Status <> Status::Released) THEN
                                    CurrReport.SKIP;

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  Receive := ReceiveReq;
                                  Invoice := InvReq;
                                  CLEAR(SalesPost);
                                  SalesPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF SalesPost.RUN("Sales Header") THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   ReplacePostingDate := FALSE;
                   ReplaceDocumentDate := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 9   ;2   ;Field     ;
                  Name=ReceiveReq;
                  CaptionML=[DAN=Modtag;
                             ENU=Receive];
                  SourceExpr=ReceiveReq }

      { 7   ;2   ;Field     ;
                  Name=InvReq;
                  CaptionML=[DAN=Fakturer;
                             ENU=Invoice];
                  SourceExpr=InvReq }

      { 1   ;2   ;Field     ;
                  Name=PostingDateReq;
                  CaptionML=[DAN=Bogføringsdato;
                             ENU=Posting Date];
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[DAN=Erstat bogføringsdato;
                             ENU=Replace Posting Date];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  Name=ReplaceDocumentDate;
                  CaptionML=[DAN=Erstat bilagsdato;
                             ENU=Replace Document Date];
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  Name=CalcInvDisc;
                  CaptionML=[DAN=Beregn fakturarabat;
                             ENU=Calc. Inv. Discount];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DAN=Angiv bogføringsdatoen.;ENU=Enter the posting date.';
      Text001@1001 : TextConst 'DAN=Returv.ordrer bogføres #1########## @2@@@@@@@@@@@@@;ENU=Posting return orders  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'DAN=%1 returvareordre af i alt %2 blev bogført.;ENU=%1 return orders out of a total of %2 have now been posted.';
      Text003@1003 : TextConst 'DAN=Den valutakurs, som er knyttet til den nye bogføringsdato i salgshovedet, gælder ikke for salgslinjerne.;ENU=The exchange rate associated with the new posting date on the sales header will not apply to the sales lines.';
      SalesLine@1005 : Record 37;
      SalesSetup@1006 : Record 311;
      SalesCalcDisc@1007 : Codeunit 60;
      SalesPost@1008 : Codeunit 80;
      Window@1009 : Dialog;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReceiveReq@1014 : Boolean;
      InvReq@1015 : Boolean;
      ReplacePostingDate@1016 : Boolean;
      ReplaceDocumentDate@1017 : Boolean;
      CalcInvDisc@1018 : Boolean;

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type","Sales Header"."Document Type");
      SalesLine.SETRANGE("Document No.","Sales Header"."No.");
      IF SalesLine.FINDFIRST THEN
        IF SalesCalcDisc.RUN(SalesLine) THEN BEGIN
          "Sales Header".GET("Sales Header"."Document Type","Sales Header"."No.");
          COMMIT;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

