OBJECT Table 279 Extended Text Header
{
  OBJECT-PROPERTIES
  {
    Date=09-09-14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Language Code,Text No.;
    OnInsert=BEGIN
               SetUniqueTextNo;

               IF "Table Name" = "Table Name"::Resource THEN BEGIN
                 "Purchase Quote" := FALSE;
                 "Purchase Invoice" := FALSE;
                 "Purchase Blanket Order" := FALSE;
                 "Purchase Order" := FALSE;
                 "Purchase Credit Memo" := FALSE;
               END;
             END;

    OnDelete=VAR
               ExtTextLine@1000 : Record 280;
             BEGIN
               ExtTextLine.SETRANGE("Table Name","Table Name");
               ExtTextLine.SETRANGE("No.","No.");
               ExtTextLine.SETRANGE("Language Code","Language Code");
               ExtTextLine.SETRANGE("Text No.","Text No.");
               ExtTextLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF ("Table Name" <> xRec."Table Name") OR ("No." <> xRec."No.") THEN
                 ERROR(STRSUBSTNO(RenameRecordErr,FIELDCAPTION("Table Name"),FIELDCAPTION("No.")));

               SetUniqueTextNo;

               RecreateTextLines;
             END;

    CaptionML=[DAN=Udvidet tekst - hoved;
               ENU=Extended Text Header];
    LookupPageID=Page391;
  }
  FIELDS
  {
    { 1   ;   ;Table Name          ;Option        ;CaptionML=[DAN=Tabelnavn;
                                                              ENU=Table Name];
                                                   OptionCaptionML=[DAN=Tekst,Finanskonto,Vare,Ressource;
                                                                    ENU=Standard Text,G/L Account,Item,Resource];
                                                   OptionString=Standard Text,G/L Account,Item,Resource }
    { 2   ;   ;No.                 ;Code20        ;TableRelation=IF (Table Name=CONST(Standard Text)) "Standard Text"
                                                                 ELSE IF (Table Name=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Table Name=CONST(Item)) Item
                                                                 ELSE IF (Table Name=CONST(Resource)) Resource;
                                                   CaptionML=[DAN=Nummer;
                                                              ENU=No.] }
    { 3   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   OnValidate=BEGIN
                                                                "All Language Codes" := "Language Code" = '';
                                                              END;

                                                   CaptionML=[DAN=Sprogkode;
                                                              ENU=Language Code] }
    { 4   ;   ;Text No.            ;Integer       ;CaptionML=[DAN=Tekstnr.;
                                                              ENU=Text No.];
                                                   Editable=No }
    { 5   ;   ;Starting Date       ;Date          ;CaptionML=[DAN=Startdato;
                                                              ENU=Starting Date] }
    { 6   ;   ;Ending Date         ;Date          ;CaptionML=[DAN=Slutdato;
                                                              ENU=Ending Date] }
    { 7   ;   ;All Language Codes  ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "All Language Codes" THEN
                                                                  "Language Code" := ''
                                                                ELSE
                                                                  TESTFIELD("Language Code");
                                                              END;

                                                   CaptionML=[DAN=Alle sprogkoder;
                                                              ENU=All Language Codes] }
    { 8   ;   ;Description         ;Text30        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 11  ;   ;Sales Quote         ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 36=R;
                                                   CaptionML=[DAN=Salgstilbud;
                                                              ENU=Sales Quote] }
    { 12  ;   ;Sales Invoice       ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 36=R;
                                                   CaptionML=[DAN=Salgsfaktura;
                                                              ENU=Sales Invoice] }
    { 13  ;   ;Sales Order         ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=[DAN=Salgsordre;
                                                              ENU=Sales Order] }
    { 14  ;   ;Sales Credit Memo   ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 36=R;
                                                   CaptionML=[DAN=Salgskreditnota;
                                                              ENU=Sales Credit Memo] }
    { 15  ;   ;Purchase Quote      ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Purchase Quote" THEN
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData 38=R;
                                                   CaptionML=[DAN=Købsrekvisition;
                                                              ENU=Purchase Quote] }
    { 16  ;   ;Purchase Invoice    ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Purchase Invoice" THEN
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData 38=R;
                                                   CaptionML=[DAN=Købsfaktura;
                                                              ENU=Purchase Invoice] }
    { 17  ;   ;Purchase Order      ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Purchase Order" THEN
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[DAN=Købsordre;
                                                              ENU=Purchase Order] }
    { 18  ;   ;Purchase Credit Memo;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Purchase Credit Memo" THEN
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData 38=R;
                                                   CaptionML=[DAN=Købskreditnota;
                                                              ENU=Purchase Credit Memo] }
    { 19  ;   ;Reminder            ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 18=R;
                                                   CaptionML=[DAN=Rykkermeddelelse;
                                                              ENU=Reminder] }
    { 20  ;   ;Finance Charge Memo ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 18=R;
                                                   CaptionML=[DAN=Rentenota;
                                                              ENU=Finance Charge Memo] }
    { 21  ;   ;Sales Blanket Order ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=[DAN=Salgsrammeordre;
                                                              ENU=Sales Blanket Order] }
    { 22  ;   ;Purchase Blanket Order;Boolean     ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Purchase Blanket Order" THEN
                                                                  NoResourcePurch;
                                                              END;

                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[DAN=Købsrammeordre;
                                                              ENU=Purchase Blanket Order] }
    { 23  ;   ;Prepmt. Sales Invoice;Boolean      ;InitValue=Yes;
                                                   AccessByPermission=TableData 36=R;
                                                   CaptionML=[DAN=Salgsfaktura for forudbetaling;
                                                              ENU=Prepmt. Sales Invoice] }
    { 24  ;   ;Prepmt. Sales Credit Memo;Boolean  ;InitValue=Yes;
                                                   AccessByPermission=TableData 36=R;
                                                   CaptionML=[DAN=Salgskreditnota for forudbetaling;
                                                              ENU=Prepmt. Sales Credit Memo] }
    { 25  ;   ;Prepmt. Purchase Invoice;Boolean   ;InitValue=Yes;
                                                   AccessByPermission=TableData 38=R;
                                                   CaptionML=[DAN=Købsfaktura for forudbetaling;
                                                              ENU=Prepmt. Purchase Invoice] }
    { 26  ;   ;Prepmt. Purchase Credit Memo;Boolean;
                                                   InitValue=Yes;
                                                   AccessByPermission=TableData 38=R;
                                                   CaptionML=[DAN=Købskreditnota for forudbetaling;
                                                              ENU=Prepmt. Purchase Credit Memo] }
    { 5900;   ;Service Order       ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 5900=R;
                                                   CaptionML=[DAN=Serviceordre;
                                                              ENU=Service Order] }
    { 5901;   ;Service Quote       ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 5900=R;
                                                   CaptionML=[DAN=Servicetilbud;
                                                              ENU=Service Quote] }
    { 5902;   ;Service Invoice     ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 5900=R;
                                                   CaptionML=[DAN=Servicefaktura;
                                                              ENU=Service Invoice] }
    { 5903;   ;Service Credit Memo ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 5900=R;
                                                   CaptionML=[DAN=Servicekreditnota;
                                                              ENU=Service Credit Memo] }
    { 6600;   ;Sales Return Order  ;Boolean       ;InitValue=Yes;
                                                   AccessByPermission=TableData 6660=R;
                                                   CaptionML=[DAN=Salgsreturvareordre;
                                                              ENU=Sales Return Order] }
    { 6605;   ;Purchase Return Order;Boolean      ;InitValue=Yes;
                                                   AccessByPermission=TableData 6650=R;
                                                   CaptionML=[DAN=Købsreturvareordre;
                                                              ENU=Purchase Return Order] }
  }
  KEYS
  {
    {    ;Table Name,No.,Language Code,Text No.   ;Clustered=Yes }
    {    ;Table Name,No.,Language Code,All Language Codes,Starting Date,Ending Date }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UntitledMsg@1002 : TextConst 'DAN=ikke navngivet;ENU=untitled';
      Text001@1001 : TextConst 'DAN=Du kan ikke købe ressourcer.;ENU=You cannot purchase resources.';
      RenameRecordErr@1000 : TextConst '@@@=%1 is TableName Field %2 is No.Table Field;DAN=%1 eller %2 kan ikke omdøbes.;ENU=You cannot rename %1 or %2.';

    LOCAL PROCEDURE NoResourcePurch@1();
    BEGIN
      IF "Table Name" = "Table Name"::Resource THEN
        ERROR(Text001);
    END;

    LOCAL PROCEDURE SetUniqueTextNo@5();
    VAR
      ExtTextHeader@1000 : Record 279;
    BEGIN
      ExtTextHeader.SETRANGE("Table Name","Table Name");
      ExtTextHeader.SETRANGE("No.","No.");
      ExtTextHeader.SETRANGE("Language Code","Language Code");

      IF ExtTextHeader.FINDLAST THEN
        "Text No." := ExtTextHeader."Text No." + 1
      ELSE
        "Text No." := 1;
    END;

    LOCAL PROCEDURE RecreateTextLines@2();
    VAR
      ExtTextLine@1000 : Record 280;
      TmpExtTextLine@1001 : Record 280;
    BEGIN
      ExtTextLine.SETRANGE("Table Name","Table Name");
      ExtTextLine.SETRANGE("No.","No.");
      ExtTextLine.SETRANGE("Language Code",xRec."Language Code");
      ExtTextLine.SETRANGE("Text No.",xRec."Text No.");

      IF ExtTextLine.FIND('-') THEN
        REPEAT
          TmpExtTextLine := ExtTextLine;
          TmpExtTextLine."Text No." := "Text No.";
          TmpExtTextLine."Language Code" := "Language Code";
          TmpExtTextLine.INSERT;
        UNTIL ExtTextLine.NEXT = 0;

      ExtTextLine.DELETEALL;
    END;

    PROCEDURE GetCaption@3() : Text[80];
    VAR
      GLAcc@1000 : Record 15;
      Item@1001 : Record 27;
      Res@1002 : Record 156;
      StandardText@1003 : Record 7;
      Descr@1004 : Text[50];
    BEGIN
      IF "Text No." <> 0 THEN BEGIN
        CASE "Table Name" OF
          "Table Name"::"Standard Text":
            BEGIN
              IF StandardText.Code <> "No." THEN
                StandardText.GET("No.");
              Descr := StandardText.Description;
            END;
          "Table Name"::"G/L Account":
            BEGIN
              IF GLAcc."No." <> "No." THEN
                GLAcc.GET("No.");
              Descr := GLAcc.Name;
            END;
          "Table Name"::Item:
            BEGIN
              IF Item."No." <> "No." THEN
                Item.GET("No.");
              Descr := Item.Description;
            END;
          "Table Name"::Resource:
            BEGIN
              IF Res."No." <> "No." THEN
                Res.GET("No.");
              Descr := Res.Name;
            END;
        END;
        EXIT(STRSUBSTNO('%1 %2 %3 %4',"No.",Descr,"Language Code",FORMAT("Text No.")));
      END;
      EXIT(UntitledMsg);
    END;

    BEGIN
    END.
  }
}

