OBJECT Report 496 Batch Post Purchase Orders
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Massebogfør købsordrer;
               ENU=Batch Post Purchase Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               ReqFilterHeadingML=[DAN=Købsordre;
                                   ENU=Purchase Order];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  IF ApprovalsMgmt.IsPurchaseApprovalsWorkflowEnabled("Purchase Header") OR (Status = Status::"Pending Approval") THEN
                                    CurrReport.SKIP;

                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  Receive := ReceiveReq;
                                  Invoice := InvReq;
                                  CLEAR(PurchPost);
                                  PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF IsApprovedForPostingBatch THEN
                                    IF PurchPost.RUN("Purchase Header") THEN BEGIN
                                      CounterOK := CounterOK + 1;
                                      IF MARKEDONLY THEN
                                        MARK(FALSE);
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitValue;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 1   ;2   ;Field     ;
                  Name=Receive;
                  CaptionML=[DAN=Modtag;
                             ENU=Receive];
                  SourceExpr=ReceiveReq }

      { 2   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[DAN=Fakturer;
                             ENU=Invoice];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[DAN=Bogføringsdato;
                             ENU=Posting Date];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[DAN=Erstat bogføringsdato;
                             ENU=Replace Posting Date];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ReplaceDocumentDate;
                  CaptionML=[DAN=Erstat bilagsdato;
                             ENU=Replace Document Date];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  Name=CalcInvDiscount;
                  CaptionML=[DAN=Beregn fakturarabat;
                             ENU=Calc. Inv. Discount];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DAN=Angiv bogføringsdatoen.;ENU=Enter the posting date.';
      Text001@1001 : TextConst 'DAN=Ordrer bogføres #1########## @2@@@@@@@@@@@@@;ENU=Posting orders  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'DAN=%1 ordrer ud af i alt %2 er nu bogførte.;ENU=%1 orders out of a total of %2 have now been posted.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      ReceiveReq@1008 : Boolean;
      InvReq@1009 : Boolean;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'DAN=Den valutakurs, som er knyttet til den nye bogføringsdato i købshovedet, gælder ikke for købslinjerne.;ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.';

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FINDFIRST THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitializeRequest@2(NewReceiveReq@1006 : Boolean;NewInvReq@1003 : Boolean;NewPostingDateReq@1001 : Date;NewReplacePostingDate@1002 : Boolean;NewReplaceDocumentDate@1004 : Boolean;NewCalcInvDisc@1005 : Boolean);
    BEGIN
      InitValue;
      ReceiveReq := NewReceiveReq;
      InvReq := NewInvReq;
      PostingDateReq := NewPostingDateReq;
      ReplacePostingDate := NewReplacePostingDate;
      ReplaceDocumentDate := NewReplaceDocumentDate;
      IF NewCalcInvDisc THEN
        PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
      CalcInvDisc := NewCalcInvDisc;
    END;

    LOCAL PROCEDURE InitValue@3();
    BEGIN
      PurchSetup.GET;
      CalcInvDisc := PurchSetup."Calc. Inv. Discount";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

