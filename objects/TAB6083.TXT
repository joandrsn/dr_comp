OBJECT Table 6083 Serv. Price Adjustment Detail
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateRecord;
             END;

    OnModify=BEGIN
               ValidateRecord;
             END;

    OnRename=BEGIN
               ValidateRecord;
             END;

    CaptionML=[DAN=Serv.prisreguleringsopl.;
               ENU=Serv. Price Adjustment Detail];
  }
  FIELDS
  {
    { 1   ;   ;Serv. Price Adjmt. Gr. Code;Code10 ;TableRelation="Service Price Adjustment Group";
                                                   CaptionML=[DAN=Serviceprisreg.gruppekode;
                                                              ENU=Serv. Price Adjmt. Gr. Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Type                ;Option        ;CaptionML=[DAN=Type;
                                                              ENU=Type];
                                                   OptionCaptionML=[DAN=Vare,Ressource,Ressourcegruppe,Serviceomkostning,Finanskonto;
                                                                    ENU=Item,Resource,Resource Group,Service Cost,G/L Account];
                                                   OptionString=Item,Resource,Resource Group,Service Cost,G/L Account }
    { 3   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST(Resource Group)) "Resource Group"
                                                                 ELSE IF (Type=CONST(Service Cost)) "Service Cost"
                                                                 ELSE IF (Type=CONST(G/L Account)) "G/L Account";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                Resource@1001 : Record 156;
                                                                ResourceGrp@1002 : Record 152;
                                                                ServiceCost@1003 : Record 5905;
                                                              BEGIN
                                                                IF "No." <> '' THEN
                                                                  CASE Type OF
                                                                    Type::Item:
                                                                      BEGIN
                                                                        Item.GET("No.");
                                                                        Description := Item.Description;
                                                                      END;
                                                                    Type::Resource:
                                                                      BEGIN
                                                                        Resource.GET("No.");
                                                                        Description := Resource.Name;
                                                                      END;
                                                                    Type::"Resource Group":
                                                                      BEGIN
                                                                        ResourceGrp.GET("No.");
                                                                        Description := ResourceGrp.Name;
                                                                      END;
                                                                    Type::"Service Cost":
                                                                      BEGIN
                                                                        ServiceCost.GET("No.");
                                                                        Description := ServiceCost.Description;
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[DAN=Nummer;
                                                              ENU=No.] }
    { 4   ;   ;Work Type           ;Code10        ;TableRelation="Work Type";
                                                   OnValidate=VAR
                                                                ServPriceAdjmtDetail@1000 : Record 6083;
                                                                ServPriceAdjmtDetail2@1001 : Record 6083;
                                                              BEGIN
                                                                IF NOT (Type IN [Type::Resource,Type::"Resource Group"]) THEN BEGIN
                                                                  ServPriceAdjmtDetail.Type := Type::Resource;
                                                                  ServPriceAdjmtDetail2.Type := Type::"Resource Group";
                                                                  ERROR(Text003,
                                                                    FIELDCAPTION("Work Type"),
                                                                    FIELDCAPTION(Type),
                                                                    ServPriceAdjmtDetail.Type,
                                                                    ServPriceAdjmtDetail2.Type);
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Arbejdstype;
                                                              ENU=Work Type] }
    { 5   ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[DAN=Produktbogføringsgruppe;
                                                              ENU=Gen. Prod. Posting Group] }
    { 6   ;   ;Description         ;Text50        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
  }
  KEYS
  {
    {    ;Serv. Price Adjmt. Gr. Code,Type,No.,Work Type,Gen. Prod. Posting Group;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DAN=%1 findes allerede med reguleringer for specifikke tal af typen %2. Slet disse linjer, hvis du vil regulere alle records af typen %2.;ENU=%1 already exists with adjustments for specific %2 numbers. Delete these lines if you need an adjustment for all %2 records.';
      Text002@1000 : TextConst 'DAN=%1 findes allerede for %2 med %3 sat til tom. Slet denne linje, hvis du vil foretage reguleringer af specifikke %4-tal.;ENU=%1 already exists for this %2 with %3 set to blank. Delete this line if you need adjustments for specific %4 numbers.';
      Text003@1002 : TextConst 'DAN=%1 kan kun angives, når %2 er %3 eller %4.;ENU=%1 can only be entered when %2 is %3 or %4.';

    LOCAL PROCEDURE ValidateRecord@1();
    VAR
      ServPriceAdjmtDetail@1000 : Record 6083;
    BEGIN
      IF "No." = '' THEN BEGIN
        ServPriceAdjmtDetail.RESET;
        ServPriceAdjmtDetail.SETRANGE("Serv. Price Adjmt. Gr. Code","Serv. Price Adjmt. Gr. Code");
        ServPriceAdjmtDetail.SETRANGE(Type,Type);
        ServPriceAdjmtDetail.SETRANGE("Work Type","Work Type");
        ServPriceAdjmtDetail.SETRANGE("Gen. Prod. Posting Group","Gen. Prod. Posting Group");
        IF ServPriceAdjmtDetail.FINDFIRST THEN BEGIN
          IF
             (ServPriceAdjmtDetail."Serv. Price Adjmt. Gr. Code" <> "Serv. Price Adjmt. Gr. Code") OR
             (ServPriceAdjmtDetail.Type <> Type) OR
             (ServPriceAdjmtDetail."No." <> "No.") OR
             (ServPriceAdjmtDetail."Work Type" <> "Work Type") OR
             (ServPriceAdjmtDetail."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group")
          THEN
            ERROR(Text001,ServPriceAdjmtDetail.TABLECAPTION,FORMAT(Type))
        END;
      END ELSE
        IF ServPriceAdjmtDetail.GET(
             "Serv. Price Adjmt. Gr. Code",
             Type,
             '',
             "Work Type",
             "Gen. Prod. Posting Group")
        THEN
          ERROR(Text002,
            ServPriceAdjmtDetail.TABLECAPTION,
            FIELDCAPTION(Type),
            FIELDCAPTION("No."),
            FORMAT(Type));
    END;

    BEGIN
    END.
  }
}

