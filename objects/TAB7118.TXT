OBJECT Table 7118 Analysis Column
{
  OBJECT-PROPERTIES
  {
    Date=26-01-18;
    Time=12:00:00;
    Version List=NAVW19.00.00.49968;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Analysekolonne;
               ENU=Analysis Column];
    LookupPageID=Page7114;
    DrillDownPageID=Page7114;
  }
  FIELDS
  {
    { 1   ;   ;Analysis Area       ;Option        ;CaptionML=[DAN=Analyseområde;
                                                              ENU=Analysis Area];
                                                   OptionCaptionML=[DAN=Salg,Køb,Lager;
                                                                    ENU=Sales,Purchase,Inventory];
                                                   OptionString=Sales,Purchase,Inventory }
    { 2   ;   ;Analysis Column Template;Code10    ;TableRelation="Analysis Column Template".Name WHERE (Analysis Area=FIELD(Analysis Area));
                                                   CaptionML=[DAN=Analysekolonneskabelon;
                                                              ENU=Analysis Column Template] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DAN=Linjenr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Column No.          ;Code10        ;CaptionML=[DAN=Kolonnenr.;
                                                              ENU=Column No.] }
    { 5   ;   ;Column Header       ;Text50        ;CaptionML=[DAN=Kolonnehoved;
                                                              ENU=Column Header] }
    { 6   ;   ;Column Type         ;Option        ;InitValue=Net Change;
                                                   CaptionML=[DAN=Kolonnetype;
                                                              ENU=Column Type];
                                                   OptionCaptionML=[DAN=Formel,Bevægelse,Saldo til dato,Startsaldo,Regnskabsår-til-dato,Rest. regnskabsår,Hele regnskabsåret;
                                                                    ENU=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year];
                                                   OptionString=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year }
    { 7   ;   ;Ledger Entry Type   ;Option        ;CaptionML=[DAN=Posteringstype;
                                                              ENU=Ledger Entry Type];
                                                   OptionCaptionML=[DAN=Vareposter,Varebudgetposter;
                                                                    ENU=Item Entries,Item Budget Entries];
                                                   OptionString=Item Entries,Item Budget Entries }
    { 8   ;   ;Formula             ;Code80        ;OnValidate=BEGIN
                                                                ItemSchedLine.CheckFormula(Formula);
                                                              END;

                                                   CaptionML=[DAN=Formel;
                                                              ENU=Formula] }
    { 9   ;   ;Comparison Date Formula;DateFormula;OnValidate=BEGIN
                                                                IF FORMAT("Comparison Date Formula") <> '' THEN
                                                                  VALIDATE("Comparison Period Formula",'');
                                                              END;

                                                   CaptionML=[DAN=Sammenligningsdatoformel;
                                                              ENU=Comparison Date Formula] }
    { 10  ;   ;Show Opposite Sign  ;Boolean       ;CaptionML=[DAN=Vis modsat fortegn;
                                                              ENU=Show Opposite Sign] }
    { 11  ;   ;Show                ;Option        ;InitValue=Always;
                                                   CaptionML=[DAN=Vis;
                                                              ENU=Show];
                                                   OptionCaptionML=[DAN=Altid,Aldrig,Hvis positiv,Hvis negativ;
                                                                    ENU=Always,Never,When Positive,When Negative];
                                                   OptionString=Always,Never,When Positive,When Negative;
                                                   NotBlank=Yes }
    { 12  ;   ;Rounding Factor     ;Option        ;CaptionML=[DAN=Afrundingsfaktor;
                                                              ENU=Rounding Factor];
                                                   OptionCaptionML=[DAN=Ingen,1,1000,1000000;
                                                                    ENU=None,1,1000,1000000];
                                                   OptionString=None,1,1000,1000000 }
    { 13  ;   ;Comparison Period Formula;Code20   ;OnValidate=VAR
                                                                Steps@1001 : Integer;
                                                                Type@1000 : ' ,Period,Fiscal year,Fiscal Halfyear,Fiscal Quarter';
                                                                RangeFromType@1007 : 'Int,CP,LP';
                                                                RangeToType@1006 : 'Int,CP,LP';
                                                                RangeFromInt@1005 : Integer;
                                                                RangeToInt@1004 : Integer;
                                                              BEGIN
                                                                "Comparison Period Formula LCID" := GLOBALLANGUAGE;
                                                                ParsePeriodFormula(
                                                                  "Comparison Period Formula",
                                                                  Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);
                                                                IF "Comparison Period Formula" <> '' THEN
                                                                  CLEAR("Comparison Date Formula");
                                                              END;

                                                   CaptionML=[DAN=Sammenligningsperiodeformel;
                                                              ENU=Comparison Period Formula] }
    { 14  ;   ;Analysis Type Code  ;Code10        ;TableRelation="Analysis Type";
                                                   OnValidate=VAR
                                                                ItemAnalysisType@1000 : Record 7113;
                                                              BEGIN
                                                                IF "Analysis Type Code" <> '' THEN BEGIN
                                                                  ItemAnalysisType.GET("Analysis Type Code");
                                                                  IF "Column Header" = '' THEN
                                                                    "Column Header" := ItemAnalysisType.Name;
                                                                  "Item Ledger Entry Type Filter" := ItemAnalysisType."Item Ledger Entry Type Filter";
                                                                  "Value Entry Type Filter" := ItemAnalysisType."Value Entry Type Filter";
                                                                  "Value Type" := ItemAnalysisType."Value Type";
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Analysetypekode;
                                                              ENU=Analysis Type Code] }
    { 15  ;   ;Item Ledger Entry Type Filter;Text250;
                                                   OnValidate=BEGIN
                                                                AnalysisRepMgmt.ValidateFilter(
                                                                  "Item Ledger Entry Type Filter",DATABASE::"Analysis Column",
                                                                  FIELDNO("Item Ledger Entry Type Filter"),TRUE);
                                                              END;

                                                   CaptionML=[DAN=Vareposttypefilter;
                                                              ENU=Item Ledger Entry Type Filter] }
    { 16  ;   ;Value Entry Type Filter;Text250    ;OnValidate=BEGIN
                                                                AnalysisRepMgmt.ValidateFilter(
                                                                  "Value Entry Type Filter",DATABASE::"Analysis Column",
                                                                  FIELDNO("Value Entry Type Filter"),TRUE);
                                                              END;

                                                   CaptionML=[DAN=Værdiposttypefilter;
                                                              ENU=Value Entry Type Filter] }
    { 17  ;   ;Value Type          ;Option        ;CaptionML=[DAN=Værditype;
                                                              ENU=Value Type];
                                                   OptionCaptionML=[DAN=" ,Antal,Salgsbeløb,Kostbeløb,Ikke-lagermæssigt beløb,Enhedspris,Kostpris (standard),Indirekte omkostning,Kostpris";
                                                                    ENU=" ,Quantity,Sales Amount,Cost Amount,Non-Invntble Amount,Unit Price,Standard Cost,Indirect Cost,Unit Cost"];
                                                   OptionString=[ ,Quantity,Sales Amount,Cost Amount,Non-Invntble Amount,Unit Price,Standard Cost,Indirect Cost,Unit Cost] }
    { 18  ;   ;Invoiced            ;Boolean       ;CaptionML=[DAN=Faktureret;
                                                              ENU=Invoiced] }
    { 30  ;   ;Comparison Period Formula LCID;Integer;
                                                   CaptionML=[DAN=LCID-sammenligningsperiodeformel;
                                                              ENU=Comparison Period Formula LCID] }
  }
  KEYS
  {
    {    ;Analysis Area,Analysis Column Template,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ItemSchedLine@1000 : TEMPORARY Record 7114;
      PeriodFormulaErr@1003 : TextConst '@@@=%1 - value of Comparison Period Formula field;DAN=%1 er ikke en gyldig periodeformel.;ENU=%1 is not a valid Period Formula.';
      Text002@1002 : TextConst '@@@=Period;DAN=P;ENU=P';
      Text003@1001 : TextConst '@@@=Fiscal year;DAN=RÅ;ENU=FY';
      Text004@1005 : TextConst '@@@=Current Period;DAN=NP;ENU=CP';
      Text005@1004 : TextConst '@@@=Last period;DAN=FP;ENU=LP';
      AnalysisRepMgmt@1006 : Codeunit 7110;

    PROCEDURE ParsePeriodFormula@26(FormulaExpression@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : ' ,Period,Fiscal Year';VAR RangeFromType@1009 : 'Int,CP,LP';VAR RangeToType@1008 : 'Int,CP,LP';VAR RangeFromInt@1003 : Integer;VAR RangeToInt@1000 : Integer);
    VAR
      OldLanguageID@1005 : Integer;
      FormulaParsed@1004 : Boolean;
    BEGIN
      IF "Comparison Period Formula LCID" = 0 THEN
        "Comparison Period Formula LCID" := GLOBALLANGUAGE;

      OldLanguageID := GLOBALLANGUAGE("Comparison Period Formula LCID");
      FormulaParsed := TryParsePeriodFormula(FormulaExpression,Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);
      GLOBALLANGUAGE(OldLanguageID);

      IF NOT FormulaParsed THEN
        ERROR(GETLASTERRORTEXT);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryParsePeriodFormula@10(FormulaExpression@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : ' ,Period,Fiscal Year';VAR RangeFromType@1009 : 'Int,CP,LP';VAR RangeToType@1008 : 'Int,CP,LP';VAR RangeFromInt@1003 : Integer;VAR RangeToInt@1000 : Integer);
    VAR
      OriginalFormula@1004 : Code[20];
    BEGIN
      // <PeriodFormula> ::= <signed integer> <formula> | blank
      // <signed integer> ::= <sign> <positive integer> | blank
      // <sign> ::= + | - | blank
      // <positive integer> ::= <digit 1-9> <digits>
      // <digit 1-9> ::= 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
      // <digits> ::= 0 <digits> | <digit 1-9> <digits> | blank
      // <formula> ::= P | FY <range> | FH <range> | FQ <range>
      // <range> ::= blank | [<range2>]
      // <range2> ::= <index> .. <index> | <index>
      // <index> ::= <positive integer> | CP | LP

      OriginalFormula := FormulaExpression;
      FormulaExpression := DELCHR(FormulaExpression);

      IF NOT ParseFormula(FormulaExpression,Steps,Type) THEN
        ERROR(PeriodFormulaErr,OriginalFormula);

      IF Type = Type::"Fiscal Year" THEN
        IF NOT ParseRange(FormulaExpression,RangeFromType,RangeFromInt,RangeToType,RangeToInt) THEN
          ERROR(PeriodFormulaErr,OriginalFormula);

      IF FormulaExpression <> '' THEN
        ERROR(PeriodFormulaErr,OriginalFormula);
    END;

    LOCAL PROCEDURE ParseFormula@5(VAR FormulaExpression@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : ' ,Period,Fiscal Year') : Boolean;
    BEGIN
      Steps := 0;
      Type := Type::" ";

      IF FormulaExpression = '' THEN
        EXIT(TRUE);

      IF NOT ParseSignedInteger(FormulaExpression,Steps) THEN
        EXIT(FALSE);

      IF FormulaExpression = '' THEN
        EXIT(FALSE);

      IF NOT ParseType(FormulaExpression,Type) THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseSignedInteger@1(VAR FormulaExpression@1000 : Code[20];VAR Int@1001 : Integer) : Boolean;
    BEGIN
      Int := 0;

      CASE COPYSTR(FormulaExpression,1,1) OF
        '-':
          BEGIN
            FormulaExpression := COPYSTR(FormulaExpression,2);
            IF NOT ParseInt(FormulaExpression,Int,FALSE) THEN
              EXIT(FALSE);
            Int := -Int;
          END;
        '+':
          BEGIN
            FormulaExpression := COPYSTR(FormulaExpression,2);
            IF NOT ParseInt(FormulaExpression,Int,FALSE) THEN
              EXIT(FALSE);
          END;
        ELSE
          BEGIN
          IF NOT ParseInt(FormulaExpression,Int,TRUE) THEN
            EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseInt@2(VAR FormulaExpression@1001 : Code[20];VAR Int@1000 : Integer;AllowNotInt@1003 : Boolean) : Boolean;
    VAR
      IntegerStr@1002 : Code[20];
    BEGIN
      IF COPYSTR(FormulaExpression,1,1) IN ['1'..'9'] THEN
        REPEAT
          IntegerStr := IntegerStr + COPYSTR(FormulaExpression,1,1);
          FormulaExpression := COPYSTR(FormulaExpression,2);
          IF FormulaExpression = '' THEN
            EXIT(FALSE);
        UNTIL NOT (COPYSTR(FormulaExpression,1,1) IN ['0'..'9'])
      ELSE
        EXIT(AllowNotInt);
      EVALUATE(Int,IntegerStr);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseType@3(VAR FormulaExpression@1001 : Code[20];VAR Type@1000 : ' ,Period,Fiscal Year') : Boolean;
    BEGIN
      CASE ReadToken(FormulaExpression) OF
        Text002:
          Type := Type::Period;
        Text003:
          Type := Type::"Fiscal Year";
        ELSE
          EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseRange@6(VAR FormulaExpression@1002 : Code[20];VAR FromType@1001 : 'Int,CP,LP';VAR FromInt@1004 : Integer;VAR ToType@1006 : 'Int,CP,LP';VAR ToInt@1005 : Integer) : Boolean;
    BEGIN
      FromType := FromType::CP;
      ToType := ToType::CP;

      IF FormulaExpression = '' THEN
        EXIT(TRUE);

      IF NOT ParseToken(FormulaExpression,'[') THEN
        EXIT(FALSE);

      IF NOT ParseIndex(FormulaExpression,FromType,FromInt) THEN
        EXIT(FALSE);
      IF FormulaExpression = '' THEN
        EXIT(FALSE);

      IF COPYSTR(FormulaExpression,1,1) = '.' THEN BEGIN
        IF NOT ParseToken(FormulaExpression,'..') THEN
          EXIT(FALSE);
        IF NOT ParseIndex(FormulaExpression,ToType,ToInt) THEN
          EXIT(FALSE);
      END ELSE BEGIN
        ToType := FromType;
        ToInt := FromInt;
      END;

      IF NOT ParseToken(FormulaExpression,']') THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseIndex@11(VAR FormulaExpression@1002 : Code[20];VAR IndexType@1001 : 'Int,CP,LP';VAR Index@1004 : Integer) : Boolean;
    BEGIN
      IF FormulaExpression = '' THEN
        EXIT(FALSE);

      IF ParseInt(FormulaExpression,Index,FALSE) THEN
        IndexType := IndexType::Int
      ELSE
        CASE ReadToken(FormulaExpression) OF
          Text004:
            IndexType := IndexType::CP;
          Text005:
            IndexType := IndexType::LP;
          ELSE
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseToken@13(VAR FormulaExpression@1000 : Code[20];Token@1001 : Code[20]) : Boolean;
    BEGIN
      IF COPYSTR(FormulaExpression,1,STRLEN(Token)) <> Token THEN
        EXIT(FALSE);
      FormulaExpression := COPYSTR(FormulaExpression,STRLEN(Token) + 1);
      EXIT(TRUE)
    END;

    LOCAL PROCEDURE ReadToken@7(VAR FormulaExpression@1000 : Code[20]) : Code[20];
    VAR
      Token@1001 : Code[20];
      p@1002 : Integer;
    BEGIN
      FOR p := 1 TO STRLEN(FormulaExpression) DO BEGIN
        IF COPYSTR(FormulaExpression,p,1) IN ['[',']','.'] THEN BEGIN
          FormulaExpression := COPYSTR(FormulaExpression,STRLEN(Token) + 1);
          EXIT(Token);
        END;
        Token := Token + COPYSTR(FormulaExpression,p,1);
      END;

      FormulaExpression := '';
      EXIT(Token);
    END;

    BEGIN
    END.
  }
}

