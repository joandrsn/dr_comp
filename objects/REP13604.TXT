OBJECT Report 13604 Create Elec. Service Invoices
{
  OBJECT-PROPERTIES
  {
    Date=09-09-14;
    Time=12:00:00;
    Version List=NAVDK8.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Opret elek. servicefakturaer;
               ENU=Create Elec. Service Invoices];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5614;    ;DataItem;                    ;
               DataItemTable=Table5992;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ServInvHeader@1101100000 : Record 5992;
                             BEGIN
                               Counter := 0;

                               // Any electronic service invoices?
                               ServInvHeader.COPY("Service Invoice Header");
                               ServInvHeader.FILTERGROUP(8);
                               ServInvHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT ServInvHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic service invoices?
                               ServInvHeader.SETRANGE("EAN No.",'');
                               IF ServInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               ServInvHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               ServInvHeader.SETRANGE("Electronic Invoice Created",TRUE);
                               IF ServInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"OIOUBL Export Service Invoice","Service Invoice Header");
                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,Bill-to Customer No.,EAN No.,Electronic Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Counter@1101100000 : Integer;
      Text000@1101100002 : TextConst 'DAN=En eller flere fakturaer, som opfylder filterkriterierne, bliver sprunget over, da de ikke er elektroniske fakturaer.\\Vil du fortsætte?;ENU=One or more invoices that match your filter criteria are not electronic invoices and will be skipped.\\Do you want to continue?';
      Text001@1101100003 : TextConst 'DAN=En eller flere fakturaer, som stemmer overens med dine filterkriterier, er oprettet tidligere.\\Vil du fortsætte?;ENU=One or more invoices that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1101100005 : TextConst 'DAN=Der er oprettet %1 elektronisk(e) faktura(er).;ENU=Successfully created %1 electronic invoice(s).';
      Text003@1101100001 : TextConst 'DAN=Der er intet at oprette.;ENU=There is nothing to create.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

