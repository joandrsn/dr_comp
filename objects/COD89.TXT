OBJECT Codeunit 89 Sales-Post + Email
{
  OBJECT-PROPERTIES
  {
    Date=23-01-17;
    Time=12:00:00;
    Version List=NAVW19.00.00.47838;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            SalesHeader.COPY(Rec);
            Code;
            Rec := SalesHeader;
          END;

  }
  CODE
  {
    VAR
      PostAndSendInvoiceQst@1001 : TextConst 'DAN=Skal %1 bogføres og sendes?;ENU=Do you want to post and send the %1?';
      SalesHeader@1003 : Record 36;
      SalesInvHeader@1005 : Record 112;
      SalesCrMemoHeader@1006 : Record 114;
      FileManagement@1009 : Codeunit 419;
      HideMailDialog@1007 : Boolean;
      PostAndSaveInvoiceQst@1000 : TextConst 'DAN=Skal %1 bogføres og gemmes?;ENU=Do you want to post and save the %1?';
      NotSupportedDocumentTypeSendingErr@1004 : TextConst 'DAN=%1 er ikke blevet bogført, fordi forsendelse af dokumenter af typen %1 ikke understøttes.;ENU=The %1 is not posted because sending document of type %1 is not supported.';
      NotSupportedDocumentTypeSavingErr@1002 : TextConst 'DAN=%1 er ikke blevet bogført, fordi lagring af dokumenter af typen %1 ikke understøttes.;ENU=The %1 is not posted because saving document of type %1 is not supported.';

    LOCAL PROCEDURE Code@2();
    BEGIN
      WITH SalesHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Invoice,
          "Document Type"::"Credit Memo":
            IF NOT ConfirmPostAndDistribute(SalesHeader) THEN
              EXIT;
          ELSE
            ErrorPostAndDistribute(SalesHeader);
        END;

        CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
        COMMIT;
        SendDocumentReport(SalesHeader);
      END;
    END;

    LOCAL PROCEDURE SendDocumentReport@3(VAR SalesHeader@1000 : Record 36);
    VAR
      ReportDistributionManagement@1001 : Codeunit 452;
    BEGIN
      WITH SalesHeader DO
        CASE "Document Type" OF
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesInvHeader."No." := "No."
              ELSE
                SalesInvHeader."No." := "Last Posting No.";
              SalesInvHeader.FIND;
              SalesInvHeader.SETRECFILTER;
              ReportDistributionManagement.InitializeFrom(HideMailDialog);
              ReportDistributionManagement.EmailDocumentReport(SalesInvHeader);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesCrMemoHeader."No." := "No."
              ELSE
                SalesCrMemoHeader."No." := "Last Posting No.";
              SalesCrMemoHeader.FIND;
              SalesCrMemoHeader.SETRECFILTER;
              ReportDistributionManagement.InitializeFrom(HideMailDialog);
              ReportDistributionManagement.EmailDocumentReport(SalesCrMemoHeader);
            END
        END
    END;

    PROCEDURE InitializeFrom@10(NewHideMailDialog@1001 : Boolean);
    BEGIN
      HideMailDialog := NewHideMailDialog;
    END;

    LOCAL PROCEDURE ConfirmPostAndDistribute@1(VAR SalesHeader@1000 : Record 36) : Boolean;
    VAR
      PostAndDistributeQuestion@1001 : Text;
      ConfirmOK@1002 : Boolean;
    BEGIN
      IF FileManagement.IsWebClient THEN
        PostAndDistributeQuestion := PostAndSaveInvoiceQst
      ELSE
        PostAndDistributeQuestion := PostAndSendInvoiceQst;

      ConfirmOK := CONFIRM(PostAndDistributeQuestion,FALSE,SalesHeader."Document Type");

      EXIT(ConfirmOK);
    END;

    LOCAL PROCEDURE ErrorPostAndDistribute@4(VAR SalesHeader@1000 : Record 36);
    VAR
      NotSupportedDocumentType@1001 : Text;
    BEGIN
      IF FileManagement.IsWebClient THEN
        NotSupportedDocumentType := NotSupportedDocumentTypeSavingErr
      ELSE
        NotSupportedDocumentType := NotSupportedDocumentTypeSendingErr;

      ERROR(NotSupportedDocumentType,SalesHeader."Document Type");
    END;

    BEGIN
    END.
  }
}

