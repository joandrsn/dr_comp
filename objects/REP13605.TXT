OBJECT Report 13605 Create Elec. Service Cr. Memos
{
  OBJECT-PROPERTIES
  {
    Date=09-09-14;
    Time=12:00:00;
    Version List=NAVDK8.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Opret elek. servicekreditnotaer;
               ENU=Create Elec. Service Cr. Memos];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6248;    ;DataItem;                    ;
               DataItemTable=Table5994;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ServCrMemoHeader@1101100000 : Record 5994;
                             BEGIN
                               Counter := 0;

                               // Any electronic service credit memos?
                               ServCrMemoHeader.COPY("Service Cr.Memo Header");
                               ServCrMemoHeader.FILTERGROUP(8);
                               ServCrMemoHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT ServCrMemoHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic service credit memos?
                               ServCrMemoHeader.SETRANGE("EAN No.",'');
                               IF ServCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               ServCrMemoHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               ServCrMemoHeader.SETRANGE("Electronic Credit Memo Created",TRUE);
                               IF ServCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"OIOUBL Export Service Cr. Memo","Service Cr.Memo Header");
                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,Bill-to Customer No.,EAN No.,Electronic Credit Memo Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Counter@1101100000 : Integer;
      Text000@1101100002 : TextConst 'DAN=En eller flere kreditnotaer, som opfylder filterkriterierne, bliver sprunget over, da de ikke er elektroniske kreditnotaer.\\Vil du fortsætte?;ENU=One or more credit memos that match your filter criteria are not electronic credit memos and will be skipped.\\Do you want to continue?';
      Text001@1101100003 : TextConst 'DAN=En eller flere kreditnotaer, som opfylder filterkriterierne, er blevet oprettet tidligere.\\Vil du fortsætte?;ENU=One or more credit memos that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1101100005 : TextConst 'DAN=%1 elektronisk(e) kreditnota(er) er oprettet.;ENU=Successfully created %1 electronic credit memo(s).';
      Text003@1101100001 : TextConst 'DAN=Der er intet at oprette.;ENU=There is nothing to create.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

