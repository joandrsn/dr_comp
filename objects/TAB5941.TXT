OBJECT Table 5941 Service Item Component
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               IF Active AND (Type <> xRec.Type) THEN BEGIN
                 ServItemComponent.RESET;
                 ServItemComponent.SETCURRENTKEY(Active,"Parent Service Item No.","Line No.");
                 ServItemComponent.SETRANGE(Active,FALSE);
                 ServItemComponent.SETRANGE("Parent Service Item No.","Parent Service Item No.");
                 IF ServItemComponent.FINDLAST THEN
                   NextNo := ServItemComponent."Line No." + 1
                 ELSE
                   NextNo := 1;
                 ServItemComponent := xRec;
                 ServItemComponent.Active := FALSE;
                 ServItemComponent."Line No." := NextNo;
                 ServItemComponent."From Line No." := "Line No.";
                 ServItemComponent."Last Date Modified" := TODAY;
                 ServItemComponent.INSERT;
               END;
             END;

    OnDelete=BEGIN
               IF Active THEN BEGIN
                 ServItemComponent.RESET;
                 ServItemComponent.SETCURRENTKEY(Active,"Parent Service Item No.","From Line No.");
                 ServItemComponent.SETRANGE(Active,FALSE);
                 ServItemComponent.SETRANGE("Parent Service Item No.","Parent Service Item No.");
                 ServItemComponent.SETRANGE("From Line No.","Line No.");
                 IF ServItemComponent.FINDFIRST THEN
                   IF NOT CONFIRM(Text002,FALSE,"No.") THEN
                     ERROR('');
                 ServItemComponent.DELETEALL;
               END;
             END;

    CaptionML=[DAN=Serviceartikelkomponent;
               ENU=Service Item Component];
    LookupPageID=Page5986;
    DrillDownPageID=Page5986;
  }
  FIELDS
  {
    { 1   ;   ;Parent Service Item No.;Code20     ;TableRelation="Service Item";
                                                   CaptionML=[DAN=Overordnet serviceart.nr.;
                                                              ENU=Parent Service Item No.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DAN=Linjenr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Active              ;Boolean       ;CaptionML=[DAN=Aktiv;
                                                              ENU=Active] }
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN
                                                                  VALIDATE("No.",'');
                                                              END;

                                                   CaptionML=[DAN=Type;
                                                              ENU=Type];
                                                   OptionCaptionML=[DAN=Serviceartikel,Vare;
                                                                    ENU=Service Item,Item];
                                                   OptionString=Service Item,Item }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Service Item)) "Service Item"
                                                                 ELSE IF (Type=CONST(Item)) Item;
                                                   OnValidate=BEGIN
                                                                IF "No." <> '' THEN BEGIN
                                                                  IF Type = Type::"Service Item" THEN BEGIN
                                                                    IF "No." = "Parent Service Item No." THEN
                                                                      ERROR(
                                                                        Text000,
                                                                        Type,"No.",TABLECAPTION,ServItem.TABLECAPTION,"Parent Service Item No.");
                                                                    ServItem.GET("No.");
                                                                    "Serial No." := ServItem."Serial No.";
                                                                    "Variant Code" := ServItem."Variant Code";
                                                                    Description := ServItem.Description;
                                                                    "Description 2" := ServItem."Description 2";
                                                                  END ELSE BEGIN
                                                                    Item.GET("No.");
                                                                    "Serial No." := '';
                                                                    Description := Item.Description;
                                                                    "Description 2" := Item."Description 2";
                                                                  END;
                                                                  "Date Installed" := WORKDATE;
                                                                END ELSE BEGIN
                                                                  "No." := '';
                                                                  "Date Installed" := 0D;
                                                                  "Serial No." := '';
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                END;

                                                                VALIDATE("Serial No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF Type = Type::Item THEN BEGIN
                                                                Item."No." := xRec."No.";
                                                                IF PAGE.RUNMODAL(0,Item) = ACTION::LookupOK THEN
                                                                  VALIDATE("No.",Item."No.");
                                                              END ELSE BEGIN
                                                                ServItem.GET("Parent Service Item No.");
                                                                ServItem2.RESET;
                                                                ServItem2.SETCURRENTKEY("Customer No.","Ship-to Code");
                                                                ServItem2.SETRANGE("Customer No.",ServItem."Customer No.");
                                                                ServItem2.SETRANGE("Ship-to Code",ServItem."Ship-to Code");
                                                                ServItem2."No." := "No.";
                                                                IF PAGE.RUNMODAL(0,ServItem2) = ACTION::LookupOK THEN
                                                                  VALIDATE("No.",ServItem2."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[DAN=Nummer;
                                                              ENU=No.] }
    { 7   ;   ;Date Installed      ;Date          ;CaptionML=[DAN=Installeret den;
                                                              ENU=Date Installed] }
    { 8   ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF Type = Type::"Service Item" THEN
                                                                  IF "No." <> '' THEN BEGIN
                                                                    ServItem.GET("No.");
                                                                    TESTFIELD("Variant Code",ServItem."Variant Code");
                                                                  END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CASE Type OF
                                                                Type::Item:
                                                                  BEGIN
                                                                    ItemVariant.RESET;
                                                                    ItemVariant.SETRANGE("Item No.","No.");
                                                                    IF PAGE.RUNMODAL(PAGE::"Item Variants",ItemVariant) = ACTION::LookupOK THEN
                                                                      "Variant Code" := ItemVariant.Code;
                                                                  END;
                                                                Type::"Service Item":
                                                                  BEGIN
                                                                    IF "No." <> '' THEN BEGIN
                                                                      ServItem.GET("No.");
                                                                      ItemVariant.RESET;
                                                                      ItemVariant.SETRANGE("Item No.",ServItem."Item No.");
                                                                    END ELSE
                                                                      ItemVariant.SETRANGE("Item No.",'');
                                                                    IF PAGE.RUNMODAL(PAGE::"Item Variants",ItemVariant) = ACTION::LookupOK THEN
                                                                      VALIDATE("Variant Code",ItemVariant.Code);
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[DAN=Variantkode;
                                                              ENU=Variant Code] }
    { 11  ;   ;Serial No.          ;Code20        ;OnValidate=BEGIN
                                                                IF (Type = Type::"Service Item") AND
                                                                   ("No." <> '')
                                                                THEN BEGIN
                                                                  ServItem.GET("No.");
                                                                  IF "Serial No." <> ServItem."Serial No." THEN
                                                                    ERROR(Text001,FIELDCAPTION("Serial No."),Type,"No.");
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Serienr.;
                                                              ENU=Serial No.] }
    { 12  ;   ;Description         ;Text50        ;CaptionML=[DAN=Beskrivelse;
                                                              ENU=Description] }
    { 13  ;   ;Description 2       ;Text50        ;CaptionML=[DAN=Beskrivelse 2;
                                                              ENU=Description 2] }
    { 15  ;   ;Service Order No.   ;Code20        ;OnLookup=BEGIN
                                                              ServOrderMgt.ServHeaderLookup(1,"Service Order No.");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DAN=Serviceordrenr.;
                                                              ENU=Service Order No.] }
    { 16  ;   ;From Line No.       ;Integer       ;CaptionML=[DAN=Fra linjenr.;
                                                              ENU=From Line No.] }
    { 17  ;   ;Last Date Modified  ;Date          ;CaptionML=[DAN=Rettet den;
                                                              ENU=Last Date Modified] }
  }
  KEYS
  {
    {    ;Active,Parent Service Item No.,Line No. ;Clustered=Yes }
    {    ;Active,Parent Service Item No.,From Line No. }
    {    ;Type,No.,Active                          }
    {    ;Active,Parent Service Item No.,Type,No.  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DAN=%1 %2 kan ikke være en komponent i %3 til %4 %5;ENU=%1 %2 can not be a component in %3 for %4 %5';
      Text001@1001 : TextConst 'DAN=%1 skal være den samme som i %2 %3.;ENU=%1 must be the same as in %2 %3.';
      ServItemComponent@1002 : Record 5941;
      ServItem@1003 : Record 5940;
      ServItem2@1004 : Record 5940;
      Item@1005 : Record 27;
      ItemVariant@1006 : Record 5401;
      ServOrderMgt@1007 : Codeunit 5900;
      NextNo@1008 : Integer;
      Text002@1009 : TextConst 'DAN=Komponenten %1 har udskiftninger\Vil du slette komponenten?;ENU=Component %1 has replacements\Do you want to delete this Component?';

    PROCEDURE AssistEditSerialNo@1();
    VAR
      ItemLedgEntry@1000 : Record 32;
    BEGIN
      TESTFIELD(Type,Type::Item);
      CLEAR(ItemLedgEntry);
      ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
      ItemLedgEntry.SETRANGE("Item No.","No.");
      ItemLedgEntry.SETRANGE(Open,TRUE);
      IF "Variant Code" <> '' THEN
        ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
      ItemLedgEntry.SETFILTER("Serial No.",'<>%1','');
      IF PAGE.RUNMODAL(0,ItemLedgEntry) = ACTION::LookupOK THEN
        "Serial No." := ItemLedgEntry."Serial No.";
    END;

    BEGIN
    END.
  }
}

