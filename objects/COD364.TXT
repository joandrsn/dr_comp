OBJECT Codeunit 364 PostPurchLines-Delete
{
  OBJECT-PROPERTIES
  {
    Date=30-08-18;
    Time=12:00:00;
    Version List=NAVW19.00.00.50583;
  }
  PROPERTIES
  {
    Permissions=TableData 121=rd,
                TableData 123=d,
                TableData 125=d,
                TableData 6651=rd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PurchRcptLine@1000 : Record 121;
      PurchInvLine@1001 : Record 123;
      PurchCrMemoLine@1002 : Record 125;
      PurchShptLine@1003 : Record 6651;
      ItemTrackingMgt@1004 : Codeunit 6500;
      MoveEntries@1005 : Codeunit 361;
      DocumentDeletionErr@1006 : TextConst '@@@=%1 - Posting Date;DAN=Du kan ikke slette bogførte købsdokumenter, der er bogført efter %1. \\Datoen er defineret af feltet Tillad sletning af dokument før i vinduet Opsætning af Køb.;ENU=You cannot delete posted purchase documents that are posted after %1. \\The date is defined by the Allow Document Deletion Before field in the Purchases & Payables window.';

    PROCEDURE DeletePurchRcptLines@1(PurchRcptHeader@1000 : Record 120);
    BEGIN
      PurchRcptLine.SETRANGE("Document No.",PurchRcptHeader."No.");
      IF PurchRcptLine.FIND('-') THEN
        REPEAT
          PurchRcptLine.TESTFIELD("Quantity Invoiced",PurchRcptLine.Quantity);
          PurchRcptLine.DELETE;
        UNTIL PurchRcptLine.NEXT = 0;
      ItemTrackingMgt.DeleteItemEntryRelation(
        DATABASE::"Purch. Rcpt. Line",0,PurchRcptHeader."No.",'',0,0,TRUE);

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Purch. Rcpt. Header",PurchRcptHeader."No.");
    END;

    PROCEDURE DeletePurchInvLines@2(PurchInvHeader@1000 : Record 122);
    BEGIN
      PurchInvLine.SETRANGE("Document No.",PurchInvHeader."No.");
      IF PurchInvLine.FIND('-') THEN
        REPEAT
          PurchInvLine.DELETE;
          ItemTrackingMgt.DeleteValueEntryRelation(PurchInvLine.RowID1);
        UNTIL PurchInvLine.NEXT = 0;

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Purch. Inv. Header",PurchInvHeader."No.");
    END;

    PROCEDURE DeletePurchCrMemoLines@3(PurchCrMemoHeader@1000 : Record 124);
    BEGIN
      PurchCrMemoLine.SETRANGE("Document No.",PurchCrMemoHeader."No.");
      IF PurchCrMemoLine.FIND('-') THEN
        REPEAT
          PurchCrMemoLine.DELETE;
        UNTIL PurchCrMemoLine.NEXT = 0;
      ItemTrackingMgt.DeleteItemEntryRelation(
        DATABASE::"Purch. Cr. Memo Line",0,PurchCrMemoHeader."No.",'',0,0,TRUE);

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Purch. Cr. Memo Hdr.",PurchCrMemoHeader."No.");
    END;

    PROCEDURE DeletePurchShptLines@5800(ReturnShptHeader@1000 : Record 6650);
    BEGIN
      PurchShptLine.SETRANGE("Document No.",ReturnShptHeader."No.");
      IF PurchShptLine.FIND('-') THEN
        REPEAT
          PurchShptLine.TESTFIELD("Quantity Invoiced",PurchShptLine.Quantity);
          PurchShptLine.DELETE;
        UNTIL PurchShptLine.NEXT = 0;
      ItemTrackingMgt.DeleteItemEntryRelation(
        DATABASE::"Return Shipment Line",0,ReturnShptHeader."No.",'',0,0,TRUE);

      MoveEntries.MoveDocRelatedEntries(DATABASE::"Return Shipment Header",ReturnShptHeader."No.");
    END;

    PROCEDURE IsDocumentDeletionAllowed@4(PostingDate@1001 : Date);
    VAR
      PurchSetup@1000 : Record 312;
    BEGIN
      PurchSetup.GET;
      PurchSetup.TESTFIELD("Allow Document Deletion Before");
      IF PostingDate >= PurchSetup."Allow Document Deletion Before" THEN
        ERROR(STRSUBSTNO(DocumentDeletionErr,PurchSetup."Allow Document Deletion Before"));
    END;

    BEGIN
    END.
  }
}

