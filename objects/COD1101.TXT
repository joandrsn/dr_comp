OBJECT Codeunit 1101 CA Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=1101;
    OnRun=BEGIN
            SourceCodeSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      SourceCodeSetup@1000 : Record 242;
      Text000@1001 : TextConst 'DAN=Omkostningstypen eller balancekontoomk.typen skal defineres.\Linje %1, dokument %2, beløb %3.;ENU=Cost type or balance cost type must be defined.\Line %1, document %2, amount %3.';
      Text001@1002 : TextConst 'DAN=Du kan ikke både definere et omkostningssted og et omkostningsemne.\Linje %1, dokument %2, beløb %3.;ENU=You cannot define both cost center and cost object.\Line %1, document %2, amount %3.';
      Text002@1003 : TextConst 'DAN=Omkostningsstedet til saldo eller omkostningsemnet til saldo skal defineres.\Linje %1, dokument %2, beløb %3.;ENU=Balance cost center or balance cost object must be defined.\Line %1, document %2, amount %3.';
      Text003@1004 : TextConst 'DAN=Du kan ikke både definere et omkostningssted til saldo og et omkostningsemne til saldo.\Linje %1, dokument %2, beløb %3.;ENU=You cannot define both balance cost center and balance cost object.\Line %1, document %2, amount %3.';
      Text004@1005 : TextConst 'DAN=Omkostningsstedet eller omkostningsemnet skal defineres. \Linje %1, dokument %2, beløb %3.;ENU=Cost center or cost object must be defined. \Line %1, document %2, amount %3.';
      Text005@1006 : TextConst '@@@=starts with "Posting Date";DAN=er ikke inden for den tilladte bogføringsperiode;ENU=is not within the permitted range of posting dates';

    PROCEDURE RunCheck@3(VAR CostJnlLine@1000 : Record 1101);
    VAR
      CostType@1001 : Record 1103;
      GenJnlCheckLine@1002 : Codeunit 11;
    BEGIN
      WITH CostJnlLine DO BEGIN
        TESTFIELD("Posting Date");
        TESTFIELD("Document No.");

        SourceCodeSetup.GET;
        TESTFIELD(Amount);

        IF ("Cost Type No." = '') AND ("Bal. Cost Type No." = '') THEN
          ERROR(Text000,"Line No.","Document No.",Amount);

        IF "Cost Type No." <> '' THEN BEGIN
          CostType.GET("Cost Type No.");
          CostType.TESTFIELD(Blocked,FALSE);
          CostType.TESTFIELD(Type,CostType.Type::"Cost Type");

          IF "Source Code" <> SourceCodeSetup."G/L Entry to CA" THEN
            IF ("Cost Center Code" = '') AND ("Cost Object Code" = '') THEN
              ERROR(Text004,"Line No.","Document No.",Amount);
          IF ("Cost Center Code" <> '') AND ("Cost Object Code" <> '') THEN
            ERROR(Text001,"Line No.","Document No.",Amount);
        END;

        IF "Bal. Cost Type No." <> '' THEN BEGIN
          CostType.GET("Bal. Cost Type No.");
          CostType.TESTFIELD(Blocked,FALSE);
          CostType.TESTFIELD(Type,CostType.Type::"Cost Type");

          IF ("Bal. Cost Center Code" = '') AND ("Bal. Cost Object Code" = '') THEN
            ERROR(Text002,"Line No.","Document No.",Amount);
          IF ("Bal. Cost Center Code" <> '') AND ("Bal. Cost Object Code" <> '') THEN
            ERROR(Text003,"Line No.","Document No.",Amount);
        END;

        IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN
          FIELDERROR("Posting Date",Text005);
      END;
    END;

    BEGIN
    END.
  }
}

