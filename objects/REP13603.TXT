OBJECT Report 13603 Create Elec. Fin. Chrg. Memos
{
  OBJECT-PROPERTIES
  {
    Date=09-09-14;
    Time=12:00:00;
    Version List=NAVDK8.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Opret elektroniske rentenotaer;
               ENU=Create Elec. Fin. Chrg. Memos];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 6218;    ;DataItem;                    ;
               DataItemTable=Table304;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               IssuedFinChargeMemoHeader@1101100000 : Record 304;
                             BEGIN
                               Counter := 0;

                               // Any electronic finance charges?
                               IssuedFinChargeMemoHeader.COPY("Issued Fin. Charge Memo Header");
                               IssuedFinChargeMemoHeader.FILTERGROUP(8);
                               IssuedFinChargeMemoHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT IssuedFinChargeMemoHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic finance charges?
                               IssuedFinChargeMemoHeader.SETRANGE("EAN No.",'');
                               IF IssuedFinChargeMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               IssuedFinChargeMemoHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               IssuedFinChargeMemoHeader.SETRANGE("Elec. Fin. Charge Memo Created",TRUE);
                               IF IssuedFinChargeMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"OIOUBL Exp. Issued Fin. Chrg.","Issued Fin. Charge Memo Header");

                                  IF LogInteraction THEN
                                    SegManagement.LogDocument(
                                      19,"No.",0,0,DATABASE::Customer,"Customer No.",'','',"Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,EAN No.,Elec. Fin. Charge Memo Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[DAN=Logfør interaktion;
                             ENU=Log Interaction];
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SegManagement@1101100003 : Codeunit 5051;
      Counter@1101100000 : Integer;
      LogInteraction@1101100002 : Boolean;
      Text000@1101100007 : TextConst 'DAN=En eller flere af de udstedte rentenotaer, som opfylder filterkriterierne, bliver sprunget over, da de ikke er elektroniske rentenotaer.\\Vil du fortsætte?;ENU=One or more issued finance charges that match your filter criteria are not electronic finance charges and will be skipped.\\Do you want to continue?';
      Text001@1101100006 : TextConst 'DAN=En eller flere elektroniske rentenotaer, som opfylder filterkriterierne, er blevet oprettet tidligere.\\Vil du fortsætte?;ENU=One or more electronic finance charges that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1101100005 : TextConst 'DAN=Der er oprettet %1 elektroniske rentenotaer.;ENU=Successfully created %1 electronic finance charges.';
      Text003@1101100004 : TextConst 'DAN=Der er intet at oprette.;ENU=There is nothing to create.';
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100000();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(19) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

