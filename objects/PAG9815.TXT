OBJECT Page 9815 Std. Password Dialog
{
  OBJECT-PROPERTIES
  {
    Date=23-01-17;
    Time=12:00:00;
    Version List=NAVW19.00.00.47838;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Angiv adgangskode;
               ENU=Set Password];
    PageType=StandardDialog;
    OnInit=BEGIN
             ValidatePassword := TRUE;
             RequiresPasswordConfirmation := TRUE;
           END;

    OnOpenPage=BEGIN
                 ValidPassword := FALSE;
               END;

    OnQueryClosePage=BEGIN
                       ValidPassword := FALSE;
                       IF CloseAction = ACTION::OK THEN BEGIN
                         IF RequiresPasswordConfirmation AND (SetPassword <> ConfirmPassword) THEN
                           ERROR(PasswordMismatchErr);
                         IF EnableBlankPasswordState AND (SetPassword = '') THEN BEGIN
                           IF NOT CONFIRM(ConfirmBlankPasswordQst) THEN
                             ERROR(PasswordTooSimpleErr);
                         END ELSE BEGIN
                           IF SetPassword = '' THEN
                             ERROR(PasswordBlankIsNotAllowedErr);
                           IF ValidatePassword AND NOT IdentityManagement.ValidatePasswordStrength(SetPassword) THEN
                             ERROR(PasswordTooSimpleErr);
                         END;
                         ValidPassword := TRUE;
                       END
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                Name=SetPassword;
                ExtendedDatatype=Masked;
                CaptionML=[DAN=Adgangskode;
                           ENU=Password];
                SourceExpr=SetPassword;
                OnValidate=BEGIN
                             IF ValidatePassword AND NOT IdentityManagement.ValidatePasswordStrength(SetPassword) THEN
                               ERROR(PasswordTooSimpleErr);
                           END;
                            }

    { 3   ;1   ;Field     ;
                Name=ConfirmPassword;
                ExtendedDatatype=Masked;
                CaptionML=[DAN=Bekræft adgangskode;
                           ENU=Confirm Password];
                SourceExpr=ConfirmPassword;
                Visible=RequiresPasswordConfirmation;
                OnValidate=BEGIN
                             IF RequiresPasswordConfirmation AND (SetPassword <> ConfirmPassword) THEN
                               ERROR(PasswordMismatchErr);
                           END;
                            }

  }
  CODE
  {
    VAR
      PasswordMismatchErr@1003 : TextConst 'DAN=Adgangskoderne stemmer ikke overens.;ENU=The specified passwords are not the same.';
      PasswordTooSimpleErr@1004 : TextConst 'DAN=Den indtastede adgangskode opfylder ikke kravene. Den skal være på mindst otte tegn og indeholde både små og store bogstaver samt tal.;ENU=The specified password does not meet the requirements. It must be at least 8 characters long and contain at least one uppercase letter, one lowercase letter, and one number.';
      PasswordNotValidatedErr@1007 : TextConst 'DAN=Adgangskoden blev ikke valideret korrekt eller blev ikke accepteret.;ENU=The password did not validate correctly, or it was not accepted.';
      PasswordBlankIsNotAllowedErr@1012 : TextConst 'DAN=Du skal angive en adgangskode.;ENU=You must enter a password.';
      ConfirmBlankPasswordQst@1008 : TextConst 'DAN=Vil du afslutte uden at angive en adgangskode?;ENU=Do you want to exit without entering a password?';
      IdentityManagement@1002 : Codeunit 9801;
      SetPassword@1000 : Text[250] INDATASET;
      ConfirmPassword@1001 : Text[250] INDATASET;
      ValidPassword@1005 : Boolean;
      EnableBlankPasswordState@1006 : Boolean;
      ValidatePassword@1009 : Boolean;
      GetPasswordCaptionTxt@1010 : TextConst 'DAN=Angiv adgangskode;ENU=Enter Password';
      RequiresPasswordConfirmation@1011 : Boolean;

    PROCEDURE GetPasswordValue@1() : Text;
    BEGIN
      IF ValidPassword = TRUE THEN
        EXIT(SetPassword);

      ERROR(PasswordNotValidatedErr);
    END;

    PROCEDURE EnableBlankPassword@2(enable@1000 : Boolean);
    BEGIN
      EnableBlankPasswordState := enable;
    END;

    PROCEDURE EnableGetPasswordMode@3(NewValidatePassword@1000 : Boolean);
    BEGIN
      ValidatePassword := NewValidatePassword;
      CurrPage.CAPTION := GetPasswordCaptionTxt;
    END;

    PROCEDURE DisablePasswordConfirmation@5();
    BEGIN
      RequiresPasswordConfirmation := FALSE;
    END;

    BEGIN
    END.
  }
}

