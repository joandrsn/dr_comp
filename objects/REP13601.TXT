OBJECT Report 13601 Create Electronic Credit Memos
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVDK9.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Opret elektroniske kreditnotaer;
               ENU=Create Electronic Credit Memos];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 8098;    ;DataItem;                    ;
               DataItemTable=Table114;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               SalesCrMemoHeader@1101100000 : Record 114;
                             BEGIN
                               Counter := 0;

                               // Any electronic credit memos?
                               SalesCrMemoHeader.COPY("Sales Cr.Memo Header");
                               SalesCrMemoHeader.FILTERGROUP(8);
                               SalesCrMemoHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT SalesCrMemoHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic credit memos?
                               SalesCrMemoHeader.SETRANGE("EAN No.",'');
                               IF SalesCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               SalesCrMemoHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               SalesCrMemoHeader.SETRANGE("Electronic Credit Memo Created",TRUE);
                               IF SalesCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=VAR
                                  OIOUBLExportSalesCrMemo@1060000 : Codeunit 13610;
                                BEGIN
                                  OIOUBLExportSalesCrMemo.ExportXML("Sales Cr.Memo Header");

                                  IF LogInteraction THEN
                                    SegManagement.LogDocument(
                                      6,"No.",0,0,DATABASE::Customer,"Sell-to Customer No.","Salesperson Code",
                                      "Campaign No.","Posting Description",'');

                                  COMMIT;

                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.,EAN No.,Electronic Credit Memo Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[DAN=Logfør interaktion;
                             ENU=Log Interaction];
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SegManagement@1101100007 : Codeunit 5051;
      Counter@1101100000 : Integer;
      Text000@1101100002 : TextConst 'DAN=En eller flere kreditnotaer, som opfylder filterkriterierne, bliver sprunget over, da de ikke er elektroniske kreditnotaer.\\Vil du fortsætte?;ENU=One or more credit memos that match your filter criteria are not electronic credit memos and will be skipped.\\Do you want to continue?';
      Text001@1101100003 : TextConst 'DAN=En eller flere kreditnotaer, som opfylder filterkriterierne, er blevet oprettet tidligere.\\Vil du fortsætte?;ENU=One or more credit memos that match your filter criteria have been created before.\\Do you want to continue?';
      Text002@1101100004 : TextConst 'DAN=%1 elektronisk(e) kreditnota(er) er oprettet.;ENU=Successfully created %1 electronic credit memos.';
      Text003@1101100005 : TextConst 'DAN=Der er intet at oprette.;ENU=There is nothing to create.';
      LogInteraction@1101100006 : Boolean;
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100006();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(4) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

