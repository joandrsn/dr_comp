OBJECT Table 870 Social Listening Setup
{
  OBJECT-PROPERTIES
  {
    Date=15-09-15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Signup URL" = '' THEN
                 "Signup URL" := SocialListeningMgt.GetSignupURL;
               IF "Terms of Use URL" = '' THEN
                 "Terms of Use URL" := SocialListeningMgt.GetTermsOfUseURL;
             END;

    CaptionML=[DAN=Konfiguration af Social Engagement;
               ENU=Social Engagement Setup];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[DAN=Primærnøgle;
                                                              ENU=Primary Key] }
    { 2   ;   ;Solution ID         ;Text250       ;OnValidate=BEGIN
                                                                IF "Solution ID" = '' THEN BEGIN
                                                                  VALIDATE("Show on Items",FALSE);
                                                                  VALIDATE("Show on Customers",FALSE);
                                                                  VALIDATE("Show on Vendors",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Løsnings-id;
                                                              ENU=Solution ID];
                                                   Editable=No }
    { 3   ;   ;Show on Items       ;Boolean       ;OnValidate=BEGIN
                                                                IF "Show on Items" THEN BEGIN
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Vis på varer;
                                                              ENU=Show on Items] }
    { 4   ;   ;Show on Customers   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Show on Customers" THEN BEGIN
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Vis på kunder;
                                                              ENU=Show on Customers] }
    { 5   ;   ;Show on Vendors     ;Boolean       ;OnValidate=BEGIN
                                                                IF "Show on Vendors" THEN BEGIN
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Vis på leverandører;
                                                              ENU=Show on Vendors] }
    { 6   ;   ;Accept License Agreement;Boolean   ;OnValidate=BEGIN
                                                                IF NOT "Accept License Agreement" THEN BEGIN
                                                                  VALIDATE("Show on Items",FALSE);
                                                                  VALIDATE("Show on Customers",FALSE);
                                                                  VALIDATE("Show on Vendors",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[DAN=Acceptér licensaftale;
                                                              ENU=Accept License Agreement] }
    { 7   ;   ;Terms of Use URL    ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[DAN=URL-adresse til Vilkår for anvendelse;
                                                              ENU=Terms of Use URL];
                                                   NotBlank=Yes }
    { 8   ;   ;Signup URL          ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[DAN=URL-adresse til tilmelding;
                                                              ENU=Signup URL];
                                                   NotBlank=Yes }
    { 9   ;   ;Social Listening URL;Text250       ;OnValidate=BEGIN
                                                                IF "Social Listening URL" = '' THEN BEGIN
                                                                  VALIDATE("Solution ID",'');
                                                                  EXIT;
                                                                END;

                                                                IF STRPOS("Social Listening URL",'https://') <> 1 THEN
                                                                  ERROR(MustStartWithErr,FIELDCAPTION("Social Listening URL"));
                                                                IF STRPOS("Social Listening URL",'/app/') = 0 THEN
                                                                  ERROR(MustContainSolutionIDErr,FIELDCAPTION("Social Listening URL"));

                                                                VALIDATE("Solution ID",SocialListeningMgt.ConvertURLToID("Social Listening URL",'/app/'));
                                                              END;

                                                   ExtendedDatatype=URL;
                                                   CaptionML=[DAN=URL-adresse til Social Engagement;
                                                              ENU=Social Engagement URL] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SocialListeningMgt@1000 : Codeunit 871;
      MustStartWithErr@1001 : TextConst 'DAN=%1 skal starte med ''https://''.;ENU=The %1 must start with ''https://''.';
      MustContainSolutionIDErr@1002 : TextConst 'DAN=%1 skal indeholde løsnings-id''et.;ENU=The %1 must contain the Solution ID.';

    BEGIN
    END.
  }
}

