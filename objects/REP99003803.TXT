OBJECT Report 99003803 Copy Production Forecast
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Kopier produktionsforecast;
               ENU=Copy Production Forecast];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 2936;    ;DataItem;                    ;
               DataItemTable=Table99000852;
               DataItemTableView=SORTING(Entry No.);
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 EXIT;

                               LOCKTABLE;

                               IF ProdForecastEntry2.FINDLAST THEN
                                 LastEntryNo := ProdForecastEntry2."Entry No.";

                               NextEntryNo := LastEntryNo + 1;

                               ProdForecastName.SETRANGE(Name,ToProdForecastEntry."Production Forecast Name");
                               IF NOT ProdForecastName.FINDFIRST THEN BEGIN
                                 ProdForecastName.Name := ToProdForecastEntry."Production Forecast Name";
                                 ProdForecastName.INSERT;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Entry No." > LastEntryNo THEN
                                    CurrReport.BREAK;
                                  ProdForecastEntry2 := "Production Forecast Entry";

                                  IF ToProdForecastEntry."Production Forecast Name" <> '' THEN
                                    ProdForecastEntry2."Production Forecast Name" := ToProdForecastEntry."Production Forecast Name";
                                  IF ToProdForecastEntry."Item No." <> '' THEN
                                    ProdForecastEntry2."Item No." := ToProdForecastEntry."Item No.";
                                  IF ToProdForecastEntry."Location Code" <> '' THEN
                                    ProdForecastEntry2."Location Code" := ToProdForecastEntry."Location Code";
                                  ProdForecastEntry2."Component Forecast" := ToProdForecastEntry."Component Forecast";
                                  IF FORMAT(ChangeDateExpression) <> '' THEN
                                    ProdForecastEntry2."Forecast Date" := CALCDATE(ChangeDateExpression,"Forecast Date");

                                  ProdForecastEntry2."Entry No." := NextEntryNo;
                                  ProdForecastEntry2.INSERT;
                                  NextEntryNo := NextEntryNo + 1;
                                END;

               ReqFilterFields=Production Forecast Name,Item No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DAN=Indstillinger;
                             ENU=Options] }

      { 21  ;2   ;Group     ;
                  CaptionML=[DAN=Kopier til;
                             ENU=Copy to] }

      { 1   ;3   ;Field     ;
                  Name=ProductionForecastName;
                  CaptionML=[DAN=Produktionsforecastnavn;
                             ENU=Production Forecast Name];
                  SourceExpr=ToProdForecastEntry."Production Forecast Name";
                  TableRelation="Production Forecast Name" }

      { 2   ;3   ;Field     ;
                  Name=ItemNo;
                  CaptionML=[DAN=Varenr.;
                             ENU=Item No.];
                  SourceExpr=ToProdForecastEntry."Item No.";
                  TableRelation=Item }

      { 4   ;3   ;Field     ;
                  Name=LocationCode;
                  CaptionML=[DAN=Lokationskode;
                             ENU=Location Code];
                  SourceExpr=ToProdForecastEntry."Location Code";
                  TableRelation=Location }

      { 7   ;2   ;Field     ;
                  Name=ComponentForecast;
                  CaptionML=[DAN=Komponentforecast;
                             ENU=Component Forecast];
                  SourceExpr=ToProdForecastEntry."Component Forecast" }

      { 6   ;2   ;Field     ;
                  Name=DateChangeFormula;
                  CaptionML=[DAN=Flyt datoer med;
                             ENU=Date Change Formula];
                  SourceExpr=ChangeDateExpression }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DAN=Vil du kopiere dette produktionsforecast;ENU=Do you want to copy the production forecast?';
      ToProdForecastEntry@1001 : Record 99000852;
      ProdForecastEntry2@1002 : Record 99000852;
      ProdForecastName@1003 : Record 99000851;
      ChangeDateExpression@1004 : DateFormula;
      LastEntryNo@1005 : Integer;
      NextEntryNo@1006 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

