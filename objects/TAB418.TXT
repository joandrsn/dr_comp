OBJECT Table 418 IC Inbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=23-06-16;
    Time=12:00:00;
    Version List=NAVW19.00.00.46290;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICInboxJnlLine@1000 : Record 419;
               ICInboxPurchHdr@1001 : Record 436;
               ICInboxSalesHdr@1002 : Record 434;
             BEGIN
               CASE "Source Type" OF
                 "Source Type"::Journal:
                   BEGIN
                     ICInboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICInboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxJnlLine.FINDFIRST THEN
                       ICInboxJnlLine.DELETEALL(TRUE);
                   END;
                 "Source Type"::"Sales Document":
                   BEGIN
                     ICInboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxSalesHdr.FINDFIRST THEN
                       ICInboxSalesHdr.DELETE(TRUE);
                   END;
                 "Source Type"::"Purchase Document":
                   BEGIN
                     ICInboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxPurchHdr.FINDFIRST THEN
                       ICInboxPurchHdr.DELETE(TRUE);
                   END;
               END;
             END;

    CaptionML=[DAN=IC-indbakketransak.;
               ENU=IC Inbox Transaction];
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=[DAN=Transaktionsnr.;
                                                              ENU=Transaction No.];
                                                   Editable=No }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=[DAN=IC-partner kode;
                                                              ENU=IC Partner Code];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=[DAN=Kildetype;
                                                              ENU=Source Type];
                                                   OptionCaptionML=[DAN=Kladde,Salgsbilag,Købsdokument;
                                                                    ENU=Journal,Sales Document,Purchase Document];
                                                   OptionString=Journal,Sales Document,Purchase Document;
                                                   Editable=No }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=[DAN=Bilagstype;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DAN=" ,Betaling,Faktura,Kreditnota,Refusion,Ordre,Returvareordre";
                                                                    ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Refund,Order,Return Order];
                                                   Editable=No }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[DAN=Bilagsnr.;
                                                              ENU=Document No.];
                                                   Editable=No }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[DAN=Bogføringsdato;
                                                              ENU=Posting Date];
                                                   Editable=No }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=[DAN=Transaktionskilde;
                                                              ENU=Transaction Source];
                                                   OptionCaptionML=[DAN=Returneret af partner,Oprettet af partner;
                                                                    ENU=Returned by Partner,Created by Partner];
                                                   OptionString=Returned by Partner,Created by Partner;
                                                   Editable=No }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=[DAN=Bilagsdato;
                                                              ENU=Document Date];
                                                   Editable=No }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                IF (("Line Action" = "Line Action"::"Return to IC Partner") OR ("Line Action" = "Line Action"::Accept)) AND
                                                                   ("Transaction Source" = "Transaction Source"::"Returned by Partner")
                                                                THEN
                                                                  ERROR(Text000,"Transaction No.","IC Partner Code");

                                                                IF "Line Action" = "Line Action"::Accept THEN
                                                                  InboxCheckAccept;
                                                              END;

                                                   CaptionML=[DAN=Linjehandling;
                                                              ENU=Line Action];
                                                   OptionCaptionML=[DAN=Ingen handling,Accept,Returner til IC-partner,Annuller;
                                                                    ENU=No Action,Accept,Return to IC Partner,Cancel];
                                                   OptionString=No Action,Accept,Return to IC Partner,Cancel }
    { 11  ;   ;Original Document No.;Code20       ;CaptionML=[DAN=Oprindeligt bilagsnr.;
                                                              ENU=Original Document No.] }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=[DAN=IC-partner finanskontonr.;
                                                              ENU=IC Partner G/L Acc. No.] }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=[DAN=Kildelinjenr.;
                                                              ENU=Source Line No.] }
  }
  KEYS
  {
    {    ;Transaction No.,IC Partner Code,Transaction Source,Document Type;
                                                   Clustered=Yes }
    {    ;IC Partner Code                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DAN=Transaktionsnr. %1 er returneret af IC-partner %2.\Du kan kun annullere returnerede transaktioner.;ENU=Transaction No. %1 has been returned by IC Partner %2.\You can only cancel returned transactions.';

    PROCEDURE ShowDetails@1();
    VAR
      ICInBoxJnlLine@1005 : Record 419;
      ICInboxSalesHeader@1004 : Record 434;
      ICInboxPurchHeader@1003 : Record 436;
      FrmICInBoxJnlLine@1002 : Page 616;
      FrmICInboxSalesHeader@1001 : Page 644;
      FrmICInboxPurchHeader@1000 : Page 646;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::Journal:
          BEGIN
            ICInBoxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICInBoxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICInBoxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            FrmICInBoxJnlLine.SETTABLEVIEW(ICInBoxJnlLine);
            FrmICInBoxJnlLine.RUNMODAL;
          END;
        "Source Type"::"Sales Document":
          BEGIN
            ICInboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICInboxSalesHeader.SETTABLEVIEW(ICInboxSalesHeader);
            FrmICInboxSalesHeader.RUNMODAL;
          END;
        "Source Type"::"Purchase Document":
          BEGIN
            ICInboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICInboxPurchHeader.SETTABLEVIEW(ICInboxPurchHeader);
            FrmICInboxPurchHeader.RUNMODAL;
          END;
      END;
    END;

    LOCAL PROCEDURE InboxCheckAccept@40();
    VAR
      ICInboxTransaction2@1001 : Record 418;
      HandledICInboxTrans@1002 : Record 420;
      Text001@1003 : TextConst 'DAN=Transaktionsnr. %2 er en kopi af transaktionsnr. %1, som allerede er blevet angivet til Accept.\Vil du også acceptere transaktionsnr. %2?;ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Accept.\Do you also want to accept Transaction No. %2?';
      Text002@1000 : TextConst 'DAN=En kopi af transaktionsnr. %1 er allerede blevet accepteret, og den findes nu i vinduet Håndterede IC-udbakketransaktioner.\Vil du også acceptere transaktionsnr. %1?;ENU=A copy of Transaction No. %1 has already been accepted and is now in the Handled IC Inbox Transactions window.\Do you also want to accept Transaction No. %1?';
      ICInboxPurchHeader@1004 : Record 436;
      PurchHeader@1005 : Record 38;
      PurchInvHeader@1006 : Record 122;
      Text003@1007 : TextConst 'DAN=Der findes allerede en købsordre for transaktion %1. Hvis du accepterer og bogfører dette dokument, skal du slette den oprindelige købsordre %2 for at undgå dobbelt bogføring.;ENU=A purchase order already exists for transaction %1. If you accept and post this document, you should delete the original purchase order %2 to avoid duplicate postings.';
      Text004@1008 : TextConst 'DAN=Købsfakturaen %1 er allerede blevet bogført for transaktionen %2. Hvis du accepterer og bogfører dette dokument, sker der dobbelt bogføring.\Er du sikker på, at du vil acceptere denne transaktion?;ENU=Purchase invoice %1 has already been posted for transaction %2. If you accept and post this document, you will have duplicate postings.\Are you sure you want to accept the transaction?';
    BEGIN
      HandledICInboxTrans.SETRANGE("IC Partner Code","IC Partner Code");
      HandledICInboxTrans.SETRANGE("Document Type","Document Type");
      HandledICInboxTrans.SETRANGE("Source Type","Source Type");
      HandledICInboxTrans.SETRANGE("Document No.","Document No.");
      IF HandledICInboxTrans.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text002,"Transaction No."),FALSE) THEN
          ERROR('');

      ICInboxTransaction2.SETRANGE("IC Partner Code","IC Partner Code");
      ICInboxTransaction2.SETRANGE("Document Type","Document Type");
      ICInboxTransaction2.SETRANGE("Source Type","Source Type");
      ICInboxTransaction2.SETRANGE("Document No.","Document No.");
      ICInboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICInboxTransaction2.SETRANGE("IC Partner G/L Acc. No.","IC Partner G/L Acc. No.");
      ICInboxTransaction2.SETRANGE("Source Line No.","Source Line No.");
      ICInboxTransaction2.SETRANGE("Line Action","Line Action"::Accept);
      IF ICInboxTransaction2.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text001,ICInboxTransaction2."Transaction No.","Transaction No."),FALSE) THEN
          ERROR('');
      IF ("Source Type" = "Source Type"::"Purchase Document") AND
         ("Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"])
      THEN BEGIN
        ICInboxPurchHeader.GET("Transaction No.","IC Partner Code","Transaction Source");
        IF ICInboxPurchHeader."Your Reference" <> '' THEN BEGIN
          PurchHeader.SETRANGE("Your Reference",ICInboxPurchHeader."Your Reference");
          IF PurchHeader.FINDFIRST THEN
            MESSAGE(Text003,ICInboxPurchHeader."IC Transaction No.",ICInboxPurchHeader."Your Reference")
          ELSE BEGIN
            PurchInvHeader.SETRANGE("Your Reference",ICInboxPurchHeader."Your Reference");
            IF PurchInvHeader.FINDFIRST THEN
              IF NOT CONFIRM(Text004,TRUE,ICInboxPurchHeader."Your Reference",ICInboxPurchHeader."IC Transaction No.") THEN
                "Line Action" := xRec."Line Action";
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

